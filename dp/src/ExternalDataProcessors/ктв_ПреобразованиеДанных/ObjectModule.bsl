Перем ПравилаВыгрузкиТипов;
Перем ПравилаЗагрузкиТипов;

#Область ПреобразованияОбъектовСлужебные

// Возвращает структуру, содержащую информацию
// о переданном виде объектов метаданных
//
// Параметры:
//  Имя					- Строка		- Имя вида объектов метаданных (например, константа,
//										  ПланСчетов и т.п.), может передаваться в ед. мн. числе
//  ТекИБ				- COMОбъект,	- Информационная база, из которой будет получена информация
//										  о виде объектов, Неопределено - текущая инф. база
//
// Возвращаемое значение:
//   Структура			- Содержимое:
//							Коллекция		- Менеджер вида объектов (например, СправочникиМенеджер)
//							МетаКоллекция	- Коллекция метаданных вида объектов (например, Справочники)
//							Имя				- Имя вида объектов (например, "ПланСчетов")
//							ИмяКоллекции	- Имя коллекции вида объектов (например,  "ПланыСчетов")
//							Представление	- Представление вида объектов (например, "План счетов")
//							ПредставлениеМ	- Представление коллекции вида объектов (например,  "Планы счетов")
//							ИмяТипа			- Имя типа (конкретный элемент, например, Контрагенты)
//							Картинка		- Картинка коллекции по умолчанию 
//							КартинкаОбъекта - Картинка объекта коллекции по умолчанию 
//							Конструктор		- Имя функции конструктора экземпляра объекта (например: СоздатьЭлемент)
//							КонструкторАльт - Имя альтернативной функции конструктора экземпляра объекта (например: СоздатьГруппу) 
//
Функция ПолучитьОписаниеВидаОбъектов(Имя, ТекИБ = Неопределено) Экспорт
	
	ВремСтруктураВида = Новый Структура("Коллекция,
										|МетаКоллекция,
										|Имя,
										|ИмяКоллекции,
										|Представление,
										|ПредставлениеМ,
										|ИмяТипа,
										|Картинка,
										|КартинкаОбъекта,
										|Конструктор,
										|КонструкторАльт");
	
	Если ТипЗнч(Имя) = Тип("Тип") Тогда
		МетаданныеТипа = Метаданные.НайтиПоТипу(Имя);
		ИмяТипа = МетаданныеТипа.ПолноеИмя();
		ПозТчк = Найти(ИмяТипа, ".");
		НИмя = НРег(Лев(ИмяТипа, ПозТчк - 1));
		ВремСтруктураВида.ИмяТипа = НРег(Сред(ИмяТипа, ПозТчк + 1));
	ИначеЕсли ТипЗнч(Имя) = Тип("Строка") Тогда
		Если Найти(Имя, ".") > 0 Тогда
			ПозТчк = Найти(Имя, ".");
			НИмя = НРег(Лев(Имя, ПозТчк - 1));
			ВремСтруктураВида.ИмяТипа = НРег(Сред(Имя, ПозТчк + 1));
		Иначе
			НИмя = НРег(СтрЗаменить(СтрЗаменить(Имя, "-", ""), " ", ""));
			ВремСтруктураВида.ИмяТипа = "";
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если НИмя = "константы" ИЛИ НИмя = "константа" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Константы, ТекИБ.Константы);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Константы, ТекИБ.Метаданные.Константы);
		ВремСтруктураВида.Имя			= "Константа";
		ВремСтруктураВида.ИмяКоллекции	= "Константы";
		ВремСтруктураВида.Представление	= "Константа";
		ВремСтруктураВида.ПредставлениеМ= "Константы";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Константа;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.Константа;
		#КонецЕсли
	ИначеЕсли НИмя = "справочники" ИЛИ НИмя = "справочник" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Справочники, ТекИБ.Справочники);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Справочники, ТекИБ.Метаданные.Справочники);
		ВремСтруктураВида.Имя			= "Справочник";
		ВремСтруктураВида.ИмяКоллекции	= "Справочники";
		ВремСтруктураВида.Представление	= "Справочник";
		ВремСтруктураВида.ПредставлениеМ= "Справочники";
		ВремСтруктураВида.Конструктор		= "СоздатьЭлемент";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьГруппу";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Справочник;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.СправочникОбъект;
		#КонецЕсли
	ИначеЕсли НИмя = "документы" ИЛИ НИмя = "документ" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Документы, ТекИБ.Документы);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Документы, ТекИБ.Метаданные.Документы);
		ВремСтруктураВида.Имя			= "Документ";
		ВремСтруктураВида.ИмяКоллекции	= "Документы";
		ВремСтруктураВида.Представление	= "Документ";
		ВремСтруктураВида.ПредставлениеМ= "Документы";
		ВремСтруктураВида.Конструктор		= "СоздатьДокумент";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Документ;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ДокументОбъект;
		#КонецЕсли
	ИначеЕсли НИмя = "журналыдокументов" ИЛИ НИмя = "журналдокументов" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, ЖурналыДокументов, ТекИБ.ЖурналыДокументов);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ЖурналыДокументов, ТекИБ.Метаданные.ЖурналыДокументов);
		ВремСтруктураВида.Имя			= "ЖурналДокументов";
		ВремСтруктураВида.ИмяКоллекции	= "ЖурналыДокументов";
		ВремСтруктураВида.Представление	= "Журнал документов";
		ВремСтруктураВида.ПредставлениеМ= "Журналы документов";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ЖурналДокументов;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ЖурналДокументов;
		#КонецЕсли
	ИначеЕсли НИмя = "планывидовхарактеристик" ИЛИ НИмя = "планвидовхарактеристик" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, ПланыВидовХарактеристик, ТекИБ.ПланыВидовХарактеристик);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ПланыВидовХарактеристик, ТекИБ.Метаданные.ПланыВидовХарактеристик);
		ВремСтруктураВида.Имя			= "ПланВидовХарактеристик";
		ВремСтруктураВида.ИмяКоллекции	= "ПланыВидовХарактеристик";
		ВремСтруктураВида.Представление	= "План видов характеристик";
		ВремСтруктураВида.ПредставлениеМ= "Планы видов характеристик";
		ВремСтруктураВида.Конструктор		= "СоздатьЭлемент";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьГруппу";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ПланВидовХарактеристик;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ПланВидовХарактеристик;
		#КонецЕсли
	ИначеЕсли НИмя = "планысчетов" ИЛИ НИмя = "плансчетов" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, ПланыСчетов, ТекИБ.ПланыСчетов);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ПланыСчетов, ТекИБ.Метаданные.ПланыСчетов);
		ВремСтруктураВида.Имя			= "ПланСчетов";
		ВремСтруктураВида.ИмяКоллекции	= "ПланыСчетов";
		ВремСтруктураВида.Представление	= "План счетов";
		ВремСтруктураВида.ПредставлениеМ= "Планы счетов";
		ВремСтруктураВида.Конструктор		= "СоздатьСчет";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ПланСчетов;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ПланСчетов;
		#КонецЕсли
	ИначеЕсли НИмя = "планывидоврасчета" ИЛИ НИмя = "планвидоврасчета" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, ПланыВидовРасчета, ТекИБ.ПланыВидовРасчета);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ПланыВидовРасчета, ТекИБ.Метаданные.ПланыВидовРасчета);
		ВремСтруктураВида.Имя			= "ПланВидовРасчета";
		ВремСтруктураВида.ИмяКоллекции	= "ПланыВидовРасчета";
		ВремСтруктураВида.Представление	= "План видов расчета";
		ВремСтруктураВида.ПредставлениеМ= "Планы видов расчета";
		ВремСтруктураВида.Конструктор		= "СоздатьВидРасчета";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ПланВидовРасчета;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ПланВидовРасчета;
		#КонецЕсли
	ИначеЕсли НИмя = "перечисления" ИЛИ НИмя = "перечисление" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Перечисления, ТекИБ.Перечисления);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Перечисления, ТекИБ.Метаданные.Перечисления);
		ВремСтруктураВида.Имя			= "Перечисление";
		ВремСтруктураВида.ИмяКоллекции	= "Перечисления";
		ВремСтруктураВида.Представление	= "Перечисление";
		ВремСтруктураВида.ПредставлениеМ= "Перечисления";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Перечисление;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.Перечисление;
		#КонецЕсли
	ИначеЕсли НИмя = "регистрысведений" ИЛИ НИмя = "регистрсведений" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, РегистрыСведений, ТекИБ.РегистрыСведений);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.РегистрыСведений, ТекИБ.Метаданные.РегистрыСведений);
		ВремСтруктураВида.Имя			= "РегистрСведений";
		ВремСтруктураВида.ИмяКоллекции	= "РегистрыСведений";
		ВремСтруктураВида.Представление	= "Регистр сведений";
		ВремСтруктураВида.ПредставлениеМ= "Регистры сведений";
		ВремСтруктураВида.Конструктор		= "СоздатьНаборЗаписей";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьМенеджерЗаписи";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.стд_РегистрыСведений;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РегистрСведений;
		#КонецЕсли
	ИначеЕсли НИмя = "регистрынакопления" ИЛИ НИмя = "регистрнакопления" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, РегистрыНакопления, ТекИБ.РегистрыНакопления);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.РегистрыНакопления, ТекИБ.Метаданные.РегистрыНакопления);
		ВремСтруктураВида.Имя			= "РегистрНакопления";
		ВремСтруктураВида.ИмяКоллекции	= "РегистрыНакопления";
		ВремСтруктураВида.Представление	= "Регистр накопления";
		ВремСтруктураВида.ПредставлениеМ= "Регистры накопления";
		ВремСтруктураВида.Конструктор		= "СоздатьНаборЗаписей";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьМенеджерЗаписи";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.РегистрНакопления;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РегистрНакопления;
		#КонецЕсли
	ИначеЕсли НИмя = "регистрыбухгалтерии" ИЛИ НИмя = "регистрбухгалтерии" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, РегистрыБухгалтерии, ТекИБ.РегистрыБухгалтерии);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.РегистрыБухгалтерии, ТекИБ.Метаданные.РегистрыБухгалтерии);
		ВремСтруктураВида.Имя			= "РегистрБухгалтерии";
		ВремСтруктураВида.ИмяКоллекции	= "РегистрыБухгалтерии";
		ВремСтруктураВида.Представление	= "Регистр бухгалтерии";
		ВремСтруктураВида.ПредставлениеМ= "Регистры бухгалтерии";
		ВремСтруктураВида.Конструктор		= "СоздатьНаборЗаписей";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьМенеджерЗаписи";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.РегистрБухгалтерии;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РегистрБухгалтерии;
		#КонецЕсли
	ИначеЕсли НИмя = "регистрырасчета" ИЛИ НИмя = "регистррасчета" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, РегистрыРасчета, ТекИБ.РегистрыРасчета);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.РегистрыРасчета, ТекИБ.Метаданные.РегистрыРасчета);
		ВремСтруктураВида.Имя			= "РегистрРасчета";
		ВремСтруктураВида.ИмяКоллекции	= "РегистрыРасчета";
		ВремСтруктураВида.Представление	= "Регистр расчета";
		ВремСтруктураВида.ПредставлениеМ= "Регистры расчета";
		ВремСтруктураВида.Конструктор		= "СоздатьНаборЗаписей";
		ВремСтруктураВида.КонструкторАльт	= "СоздатьМенеджерЗаписи";
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.РегистрРасчета;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РегистрРасчета;
		#КонецЕсли
	ИначеЕсли НИмя = "отчеты" ИЛИ НИмя = "отчет" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Отчеты, ТекИБ.Отчеты);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Отчеты, ТекИБ.Метаданные.Отчеты);
		ВремСтруктураВида.Имя			= "Отчет";
		ВремСтруктураВида.ИмяКоллекции	= "Отчеты";
		ВремСтруктураВида.Представление	= "Отчет";
		ВремСтруктураВида.ПредставлениеМ= "Отчеты";
		ВремСтруктураВида.Конструктор		= "Создать";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Отчет;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.Отчет;
		#КонецЕсли
	ИначеЕсли НИмя = "обработки" ИЛИ НИмя = "обработка" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Обработки, ТекИБ.Обработки);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Обработки, ТекИБ.Метаданные.Обработки);
		ВремСтруктураВида.Имя			= "Обработка";
		ВремСтруктураВида.ИмяКоллекции	= "Обработки";
		ВремСтруктураВида.Представление	= "Обработка";
		ВремСтруктураВида.ПредставлениеМ= "Обработки";
		ВремСтруктураВида.Конструктор		= "Создать";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Обработка;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.Обработка;
		#КонецЕсли
	ИначеЕсли НИмя = "бизнеспроцессы" ИЛИ НИмя = "бизнеспроцесс" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, БизнесПроцессы, ТекИБ.БизнесПроцессы);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.БизнесПроцессы, ТекИБ.Метаданные.БизнесПроцессы);
		ВремСтруктураВида.Имя			= "БизнесПроцесс";
		ВремСтруктураВида.ИмяКоллекции	= "БизнесПроцессы";
		ВремСтруктураВида.Представление	= "Бизнес-процесс";
		ВремСтруктураВида.ПредставлениеМ= "Бизнес-процессы";
		ВремСтруктураВида.Конструктор		= "СоздатьБизнесПроцесс";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.БизнесПроцесс;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.БизнесПроцессОбъект;
		#КонецЕсли
	ИначеЕсли НИмя = "задачи" ИЛИ НИмя = "задача" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, Задачи, ТекИБ.Задачи);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Задачи, ТекИБ.Метаданные.Задачи);
		ВремСтруктураВида.Имя			= "Задача";
		ВремСтруктураВида.ИмяКоллекции	= "Задачи";
		ВремСтруктураВида.Представление	= "Задача";
		ВремСтруктураВида.ПредставлениеМ= "Задачи";
		ВремСтруктураВида.Конструктор		= "СоздатьЗадачу";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.Задача;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ЗадачаОбъект;
		#КонецЕсли
	ИначеЕсли НИмя = "планыобмена" ИЛИ НИмя = "планобмена" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, ПланыОбмена, ТекИБ.ПланыОбмена);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ПланыОбмена, ТекИБ.Метаданные.ПланыОбмена);
		ВремСтруктураВида.Имя			= "ПланОбмена";
		ВремСтруктураВида.ИмяКоллекции	= "ПланыОбмена";
		ВремСтруктураВида.Представление	= "План обмена";
		ВремСтруктураВида.ПредставлениеМ= "Планы обмена";
		ВремСтруктураВида.Конструктор		= "СоздатьУзел";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ПланОбмена;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ПланОбмена;
		#КонецЕсли
	ИначеЕсли НИмя = "общиеформы" ИЛИ НИмя = "общаяформа" Тогда
		ВремСтруктураВида.Коллекция		= Неопределено;
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.ОбщиеФормы, ТекИБ.Метаданные.ОбщиеФормы);
		ВремСтруктураВида.Имя			= "ОбщаяФорма";
		ВремСтруктураВида.ИмяКоллекции	= "ОбщиеФормы";
		ВремСтруктураВида.Представление	= "Общая форма";
		ВремСтруктураВида.ПредставлениеМ= "Общие формы";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.РедактироватьВДиалоге;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РедактироватьВДиалоге;
		#КонецЕсли
	ИначеЕсли НИмя = "регламентныезадания" ИЛИ НИмя = "регламентноезадание" Тогда
		ВремСтруктураВида.Коллекция		= ?(ТекИБ = Неопределено, РегламентныеЗадания, ТекИБ.РегламентныеЗадания);
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.РегламентныеЗадания, ТекИБ.Метаданные.РегламентныеЗадания);
		ВремСтруктураВида.Имя			= "РегламентноеЗадание";
		ВремСтруктураВида.ИмяКоллекции	= "РегламентныеЗадания";
		ВремСтруктураВида.Представление	= "Регламентное задание";
		ВремСтруктураВида.ПредставлениеМ= "Регламентные задания";
		ВремСтруктураВида.Конструктор		= "СоздатьРегламентноеЗадание()";
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.РегламентныеЗадания;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.РегламентноеЗадание;
		#КонецЕсли
	ИначеЕсли НИмя = "роли" ИЛИ НИмя = "роль" Тогда
		ВремСтруктураВида.Коллекция		= Неопределено;
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Роли, ТекИБ.Метаданные.Роли);
		ВремСтруктураВида.Имя			= "Роль";
		ВремСтруктураВида.ИмяКоллекции	= "Роли";
		ВремСтруктураВида.Представление	= "Роль";
		ВремСтруктураВида.ПредставлениеМ= "Роли";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.ПользовательБезНеобходимыхСвойств;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.ПользовательБезНеобходимыхСвойств;
		#КонецЕсли
	ИначеЕсли НИмя = "интерфейсы" ИЛИ НИмя = "интерфейс" Тогда
		ВремСтруктураВида.Коллекция		= Неопределено;
		ВремСтруктураВида.МетаКоллекция	= ?(ТекИБ = Неопределено, Метаданные.Интерфейсы, ТекИБ.Метаданные.Интерфейсы);
		ВремСтруктураВида.Имя			= "Интерфейс";
		ВремСтруктураВида.ИмяКоллекции	= "Интерфейсы";
		ВремСтруктураВида.Представление	= "Интерфейс";
		ВремСтруктураВида.ПредставлениеМ= "Интерфейсы";
		ВремСтруктураВида.Конструктор		= Неопределено;
		ВремСтруктураВида.КонструкторАльт	= Неопределено;
		#Если Клиент Тогда
		ВремСтруктураВида.Картинка		= БиблиотекаКартинок.НастройкаСписка;
		ВремСтруктураВида.КартинкаОбъекта	= БиблиотекаКартинок.НастройкаСписка;
		#КонецЕсли
	КонецЕсли;
	
	Возврат ВремСтруктураВида;
	
КонецФункции // ПолучитьОписаниеВидаОбъектов()

// Возвращает имя пердопределенных данных для переданного значения
// требуется, т.к. в разных версиях различается способ получения имени предопределенных данных
//
// Параметры:
//  Значение			- ЛюбаяСсылка	- Имя значение ссылочного типа (СправочникСсылка, ПеречислениеСсылка и т.п.)
//
// Возвращаемое значение:
//   Строка				- Имя предопределенных данных, если значение не является предопределенным, возвращается пустая строка
//
Функция ПолучитьИмяПредопределенныхДанных(Значение) Экспорт
	
	//@skip-warning
	МетаданныеЗначения = Значение.Метаданные();
	
	ИмяПредопределенныхДанных = "";
	
	Текст83 = "Если Метаданные.Справочники.Содержит(МетаданныеЗначения)
			  | ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеЗначения)
			  | ИЛИ Метаданные.ПланыСчетов.Содержит(МетаданныеЗначения)
			  | ИЛИ Метаданные.ПланыВидовРасчета.Содержит(МетаданныеЗначения) Тогда
			  |    Если Значение.Предопределенный Тогда
			  |        ИмяПредопределенныхДанных = Значение.ИмяПредопределенныхДанных;
			  |    КонецЕсли;
			  | ИначеЕсли Метаданные.ПланыОбмена.Содержит(МетаданныеЗначения) Тогда
			  |    Если Значение.ЭтотУзел Тогда
			  |        ИмяПредопределенныхДанных = ""ЭтотУзел"";
			  |    КонецЕсли;
			  |КонецЕсли;";
	
	Текст82 = "Если Значение.Предопределенный Тогда
			  |    Если Метаданные.Справочники.Содержит(МетаданныеЗначения) Тогда
			  |        ИмяПредопределенныхДанных = Справочники[МетаданныеЗначения.Имя].ПолучитьИмяПредопределенного(Значение);
			  |    ИначеЕсли Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеЗначения) Тогда
			  |        ИмяПредопределенныхДанных = ПланыВидовХарактеристик[МетаданныеЗначения.Имя].ПолучитьИмяПредопределенного(Значение);
			  |    ИначеЕсли Метаданные.ПланыСчетов.Содержит(МетаданныеЗначения) Тогда
			  |        ИмяПредопределенныхДанных = ПланыСчетов[МетаданныеЗначения.Имя].ПолучитьИмяПредопределенного(Значение);
			  |    ИначеЕсли Метаданные.ПланыВидовРасчета.Содержит(МетаданныеЗначения) Тогда
			  |        ИмяПредопределенныхДанных = ПланыВидовРасчета[МетаданныеЗначения.Имя].ПолучитьИмяПредопределенного(Значение);
			  |    ИначеЕсли Метаданные.ПланыОбмена.Содержит(МетаданныеЗначения) Тогда
			  |        ИмяПредопределенныхДанных = ""ЭтотУзел"";
			  |    КонецЕсли;
			  |КонецЕсли;";
	Если ЗначениеЗаполнено(Значение) Тогда
		Это83 = Истина;
		Попытка
			Выполнить(Текст83);
		Исключение
			Это83 = Ложь;
				ТекстОшибки = СтрШаблон("Ошибка получения имени предопределенных данных ""%1"": %2%3",
				                        СокрЛП(Значение),
				                        Символы.ПС,
				                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Сообщить(ТекстОшибки);
		КонецПопытки;
		Если НЕ Это83 Тогда
			Попытка
				Выполнить(Текст82);
			Исключение
				ТекстОшибки = СтрШаблон("Ошибка получения имени предопределенных данных ""%1"": %2%3",
				                        СокрЛП(Значение),
				                        Символы.ПС,
				                        ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ВызватьИсключение ТекстОшибки;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
		
	Возврат ИмяПредопределенныхДанных;	
	
КонецФункции // ПолучитьИмяПредопределенныхДанных()
	
// Функция "расщепляет" строку на подстроки, используя заданный 
//		разделитель. Разделитель может иметь любую длину. 
//		Если в качестве разделителя задан пробел, рядом стоящие пробелы 
//		считаются одним разделителем, а ведущие и хвостовые пробелы параметра Стр
//		игнорируются.
//
//	Параметры: 
//		Стр            -    строка, которую необходимо разложить на подстроки. 
//		                    Параметр передается по значению.
//		Разделитель    -    строка-разделитель, по умолчанию - запятая.
//		ВключатьПустые -    Указывает необходимость включать в результат пустые строки.
//
//
//	Возвращаемое значение:
//		массив значений, элементы которого - подстроки
//
Функция РазложитьСтроку(Знач Стр, Разделитель = ",", ВключатьПустые = Истина) Экспорт
	
	Попытка
		Возврат СтрРазделить(Стр, Разделитель, ВключатьПустые);
	Исключение
	КонецПопытки;
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока Истина Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр, Поз - 1));
			Стр = СокрЛ(Сред(Стр, Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока Истина Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			Значение = Лев(Стр, Поз - 1);
			Если НЕ ПустаяСтрока(Значение) ИЛИ ВключатьПустые Тогда
				МассивСтрок.Добавить(Значение);
			КонецЕсли;
			Стр = Сред(Стр, Поз + ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции // РазложитьСтроку()

// Проверяет использование свойства (по имени) в соответствии со списками используемых и исключаемых свойств
//
// Параметры:
//  ИмяСвойства			- Строка					- Имя проверяемого свойства
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Булево				- Истина - свойство используется; Ложь - свойство не используется
//
Функция ИспользоватьСвойство(Знач ИмяСвойства, Знач СписокСвойств = Неопределено, Знач ИсключаяСвойства = Неопределено)
	
	Если ТипЗнч(СписокСвойств) = Тип("Строка") Тогда
		СписокСвойств = РазложитьСтроку(СписокСвойств, ",", Ложь);
	КонецЕсли;
	Если НЕ ТипЗнч(СписокСвойств) = Тип("Массив") Тогда
		СписокСвойств = Новый Массив();
	КонецЕсли;
	
	Если ТипЗнч(ИсключаяСвойства) = Тип("Строка") Тогда
		ИсключаяСвойства = РазложитьСтроку(ИсключаяСвойства, ",", Ложь);
	КонецЕсли;
	Если НЕ ТипЗнч(ИсключаяСвойства) = Тип("Массив") Тогда
		ИсключаяСвойства = Новый Массив();
	КонецЕсли;
	
	Если СписокСвойств.Количество() = 0 И ИсключаяСвойства.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ИсключаяСвойства.Найти(ИмяСвойства) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат (НЕ СписокСвойств.Найти(ИмяСвойства) = Неопределено) ИЛИ СписокСвойств.Количество() = 0;
	
КонецФункции // ИспользоватьСвойство()

Функция ПолучитьТипСтрокой(Знач ПарамТип) Экспорт
	
	МетаТип = Метаданные.НайтиПоТипу(ПарамТип);
	Если НЕ МетаТип = Неопределено Тогда
		 Возврат МетаТип.ПолноеИмя();
	КонецЕсли;
	
	ВремСтрока = СокрЛП(ПарамТип);
	Если Найти(ВремСтрока, " ") = 0 И Найти(ВремСтрока, "-") = 0 Тогда
		Возврат ВремСтрока;
	КонецЕсли;
	
	СтрокаТипа = "";
	ВерхнийРегистр = Истина;
	Для й = 1 По СтрДлина(ВремСтрока) Цикл
		Символ = Сред(ВремСтрока, й, 1);
		Если Символ = " " ИЛИ Символ = "-" Тогда
			ВерхнийРегистр = Истина;
			Продолжить;
		КонецЕсли;
		Если ВерхнийРегистр Тогда
			Символ = ВРег(Символ);
			ВерхнийРегистр = Ложь;
		КонецЕсли;
		СтрокаТипа = СтрокаТипа + Символ;
	КонецЦикла;
	
	Возврат СтрокаТипа;
	
КонецФункции // ПолучитьТипСтрокой()

// Преобразует описание типов в структуру
//
// Параметры:
//  ОписаниеТипов			- ОписаниеТипов				- Преобразуемое значение
//
// Возвращаемое значение:
//   Структура			- результат преобразования
//	   Типы							- Массив			- Список типов
//	   КвалификаторыЧисла			- Структура			- Описание квалификаторов числа
//		   Разрядность					- Число				- Разрядность
//		   РазрядностьДробнойЧасти		- Число				- Разрядность дробной части
//		   ДопустимыйЗнак				- ДопустимыйЗнак	- Допустимый знак
//	   КвалификаторыСтроки			- Структура			- Описание квалификаторов строки
//		   Длина						- Число				- Длина
//		   ДопустимаяДлина				- ДопустимаяДлина	- Разрядность дробной части
//	   КвалификаторыДаты			- Структура			- Описание квалификаторов даты
//		   ЧастиДаты					- ЧастиДаты			- Части даты (Дата / Время)
//	   КвалификаторыДвоичныхДанных	- Структура			- Описание квалификаторов двоичных данных
//		   Длина						- Число				- Длина
//		   ДопустимаяДлина				- ДопустимаяДлина	- Разрядность дробной части
//
Функция ОписаниеТиповВСтруктуру(ОписаниеТипов) Экспорт
	
	СтруктураОписаниеТипов = Новый Структура("Типы,
											 |КвалификаторыЧисла,
											 |КвалификаторыСтроки,
											 |КвалификаторыДаты,
											 |КвалификаторыДвоичныхДанных"
										    , Новый Массив()
										    , Новый Структура("Разрядность, РазрядностьДробнойЧасти, ДопустимыйЗнак", 10, 0)
										    , Новый Структура("Длина, ДопустимаяДлина", 10, "Переменная")
										    , Новый Структура("ЧастиДаты", "ДатаВремя")
										    , Новый Структура("Длина, ДопустимаяДлина", 0, "Переменная"));
								   
	Для Каждого ТекТип Из ОписаниеТипов.Типы() Цикл
		СтруктураОписаниеТипов.Типы.Добавить(ПолучитьТипСтрокой(ТекТип));
	КонецЦикла;
	
	СтруктураОписаниеТипов.КвалификаторыЧисла.Разрядность				= ОписаниеТипов.КвалификаторыЧисла.Разрядность;
	СтруктураОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти	= ОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти;
	СтруктураОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак			=
		?(ОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак = ДопустимыйЗнак.Неотрицательный, "Неотрицательный", "Любой");
	
	СтруктураОписаниеТипов.КвалификаторыСтроки.Длина			= ОписаниеТипов.КвалификаторыСтроки.Длина;
	СтруктураОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина	=
		?(ОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Фиксированная, "Фиксированная", "Переменная");
	
	СтруктураОписаниеТипов.КвалификаторыДаты.ЧастиДаты	=
		?(ОписаниеТипов.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время,
		  "Время",
		  ?(ОписаниеТипов.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата, "Дата", "ДатаВремя"));
	
	СтруктураОписаниеТипов.КвалификаторыДвоичныхДанных.Длина			= ОписаниеТипов.КвалификаторыДвоичныхДанных.Длина;
	СтруктураОписаниеТипов.КвалификаторыДвоичныхДанных.ДопустимаяДлина	=
		?(ОписаниеТипов.КвалификаторыДвоичныхДанных.ДопустимаяДлина = ДопустимаяДлина.Фиксированная, "Фиксированная", "Переменная");
		
	Возврат СтруктураОписаниеТипов;
	
КонецФункции // ОписаниеТиповВСтруктуру()

// Получает описание типов из структуры
//
// Параметры:
//   СтруктураОписаниеТипов			- Структура			- структура описания типов для преобразования
//	   Типы								- Массив			- Список типов
//	   КвалификаторыЧисла				- Структура			- Описание квалификаторов числа
//		   Разрядность						- Число				- Разрядность
//		   РазрядностьДробнойЧасти			- Число				- Разрядность дробной части
//		   ДопустимыйЗнак					- ДопустимыйЗнак	- Допустимый знак
//	   КвалификаторыСтроки				- Структура			- Описание квалификаторов строки
//		   Длина							- Число				- Длина
//		   ДопустимаяДлина					- ДопустимаяДлина	- Разрядность дробной части
//	   КвалификаторыДаты				- Структура			- Описание квалификаторов даты
//		   ЧастиДаты						- ЧастиДаты			- Части даты (Дата / Время)
//	   КвалификаторыДвоичныхДанных		- Структура			- Описание квалификаторов двоичных данных
//		   Длина							- Число				- Длина
//		   ДопустимаяДлина					- ДопустимаяДлина	- Разрядность дробной части
//
// Возвращаемое значение:
//  ОписаниеТипов			- Созданное описание типов
//
Функция ОписаниеТиповИзСтруктуры(СтруктураОписаниеТипов) Экспорт
	
	МассивТипов = Новый Массив();
	
	Для Каждого ТекТип Из СтруктураОписаниеТипов.Типы Цикл
		Попытка
			НовыйТип = Тип(СтрЗаменить(ТекТип, ".", "Ссылка."));
			МассивТипов.Добавить(НовыйТип);
		Исключение
			Продолжить;
		КонецПопытки;
	КонецЦикла;
		
	КвалификаторыЧисла = Новый КвалификаторыЧисла(СтруктураОписаниеТипов.КвалификаторыЧисла.Разрядность,
												  СтруктураОписаниеТипов.КвалификаторыЧисла.РазрядностьДробнойЧасти,
												  Вычислить("ДопустимыйЗнак." + СтруктураОписаниеТипов.КвалификаторыЧисла.ДопустимыйЗнак));
	
	КвалификаторыСтроки = Новый КвалификаторыСтроки(СтруктураОписаниеТипов.КвалификаторыСтроки.Длина,
													Вычислить("ДопустимаяДлина." + СтруктураОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина));
	
	КвалификаторыДаты = Новый КвалификаторыДаты(Вычислить("ЧастиДаты." + СтруктураОписаниеТипов.КвалификаторыДаты.ЧастиДаты));
	
	КвалификаторыДвоичныхДанных = Новый КвалификаторыДвоичныхДанных(СтруктураОписаниеТипов.КвалификаторыСтроки.Длина,
																	Вычислить("ДопустимаяДлина." + СтруктураОписаниеТипов.КвалификаторыДвоичныхДанных.ДопустимаяДлина));
																		  
	Возврат Новый ОписаниеТипов(МассивТипов, КвалификаторыЧисла, КвалификаторыСтроки, КвалификаторыДаты, КвалификаторыДвоичныхДанных);
	
КонецФункции // ОписаниеТиповИзСтруктуры()

// Проверяет использование свойства объекта и добавляет в структуру
//
// Параметры:
//  СтруктураОбъекта	- Структура					- Заполняемая структура объекта
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ИмяСвойства, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ИспользоватьСвойство(ИмяСвойства, СписокСвойств, ИсключаяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить(ИмяСвойства, ЗначениеВСтруктуру(Объект[ИмяСвойства]));
	
КонецПроцедуры //СвойствоОбъектаВСтруктуру()
	
// Проверяет использование свойства объекта и заполняет значение свойства из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Структура объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ИмяСвойства, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ СтруктураОбъекта.Свойство(ИмяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ИспользоватьСвойство(ИмяСвойства, СписокСвойств, ИсключаяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Объект[ИмяСвойства] = ЗначениеИзСтруктуры(СтруктураОбъекта[ИмяСвойства]);
	
КонецПроцедуры // СвойствоОбъектаИзСтруктуры()
	
// Добавляет данные табличной части объекта в структуру
//
// Параметры:
//  СтруктураОбъекта	- Структура					- Заполняемая структура объекта
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  ИмяТЧ				- Строка					- Имя преобразуемой табличной части
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ИмяТЧ, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ИспользоватьСвойство("__ТЧ." + ИмяТЧ, СписокСвойств, ИсключаяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураОбъекта.Свойство("__ТЧ") Тогда
		СтруктураОбъекта.Вставить("__ТЧ", Новый Структура());
	КонецЕсли;
	
	МетаОбъект = Объект.Метаданные();
	
	МетаТЧ = МетаОбъект.ТабличныеЧасти.Найти(ИмяТЧ);
	
	Если МетаТЧ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтрок = Новый Массив();
	Для Каждого ТекСтрока Из Объект[ИмяТЧ] Цикл
		
		СтруктураСтроки = Новый Структура();
		СтруктураСтроки.Вставить("__Индекс", Объект[ИмяТЧ].Индекс(ТекСтрока));
		СтруктураСтроки.Вставить("НомерСтроки", ТекСтрока.НомерСтроки);
		
		Для Каждого ТекРеквизит Из МетаТЧ.Реквизиты Цикл
			Если НЕ ИспользоватьСвойство(ИмяТЧ + "." + ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекРеквизит.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекРеквизит.Имя]));
		КонецЦикла;
		
		МассивСтрок.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	СтруктураОбъекта.__ТЧ.Вставить(ИмяТЧ, МассивСтрок);
	
КонецПроцедуры // ТЧОбъектаВСтруктуру()

// Заполняет табличную часть объекта из структуры
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Структура данных объекта
//  ИмяТЧ				- Строка					- Имя преобразуемой табличной части
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ИмяТЧ, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ СтруктураОбъекта.Свойство("__ТЧ") Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ СтруктураОбъекта.__ТЧ.Свойство(ИмяТЧ) Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ ИспользоватьСвойство("__ТЧ." + ИмяТЧ, СписокСвойств, ИсключаяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	МетаОбъект = Объект.Метаданные();
	
	МетаТЧ = МетаОбъект.ТабличныеЧасти.Найти(ИмяТЧ);
	
	Если МетаТЧ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Объект[ИмяТЧ].Очистить();
	
	МассивСтрок = СтруктураОбъекта.__ТЧ[ИмяТЧ];
	
	Для Каждого ТекСтрока Из МассивСтрок Цикл
		
		НоваяСтрока = Объект[ИмяТЧ].Добавить();
		
		Для Каждого ТекРеквизит Из МетаТЧ.Реквизиты Цикл
			
			Если НЕ ТекСтрока.Свойство(ТекРеквизит.Имя) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ИспользоватьСвойство(ИмяТЧ + "." + ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока[ТекРеквизит.Имя] = ЗначениеИзСтруктуры(ТекСтрока[ТекРеквизит.Имя]);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ТЧОбъектаИзСтруктуры()
	
// Преобразует значения субконто записи регистра бухгалтерии в структуру
//
// Параметры:
//  Запись				- РегистрБухгалтерииЗапись	- Преобразуемая запись регистра бухгалтерии
//  ДтКт				- Строка					- признак Дт/Кт - "Дт", "Кт", "" 
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Массив(Структура)			- результат преобразования
//		НомерСтроки		- Число								- Номер субконто
//		ВидСубконто		- ПланВидовХарактеристикСсылка		- Вид субконто
//		Значение		- Произвольный						- Значение субконто
//
Функция ЗначенияСубконтоЗаписиВСтруктуру(Запись, ДтКт = "", СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	ВидыСубконто = Запись["Счет" + ДтКт].ВидыСубконто;
	ЗначенияСубконто = Запись["Субконто" + ДтКт];
	
	МассивСубконто = Новый Массив();
	й = 1;
	Для Каждого ТекСтрокаВида Из ВидыСубконто Цикл
		Если НЕ ИспользоватьСвойство("Субконто" + ДтКт + Формат(й, "ЧДЦ=; ЧГ="), СписокСвойств, ИсключаяСвойства) Тогда
			й = й + 1;
			Продолжить;
		КонецЕсли;
		МассивСубконто.Добавить(Новый Структура("НомерСтроки, ВидСубконто, Значение"
												, й
												, ЗначениеВСтруктуру(ТекСтрокаВида.ВидСубконто)
												, ЗначениеВСтруктуру(ЗначенияСубконто[ТекСтрокаВида.ВидСубконто])));
		й = й + 1;
	КонецЦикла;
	
	Возврат МассивСубконто;
	
КонецФункции // ЗначенияСубконтоВСтруктуру()

// Заполняет значения субконто записи регистра бухгалтерии из структуры
//
// Параметры:
//  Запись				- РегистрБухгалтерииЗапись	- Заполняемая запись регистра бухгалтерии
//  СтруктураЗаписи		- Структура					- Структура данных записи регистра бухгалтерии 
//  ДтКт				- Строка					- признак Дт/Кт - "Дт", "Кт", "" 
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ЗначенияСубконтоЗаписиИзСтруктуры(Запись, СтруктураЗаписи, ДтКт = "", СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	ВидыСубконто = Запись["Счет" + ДтКт].ВидыСубконто;
	ЗначенияСубконто = Запись["Субконто" + ДтКт];
	
	МассивСубконто = СтруктураЗаписи["Субконто" + ДтКт];
	
	й = 1;
	Для Каждого ТекСтрока Из МассивСубконто Цикл
		
		Если НЕ ИспользоватьСвойство("Субконто" + ДтКт + Формат(й, "ЧДЦ=; ЧГ="), СписокСвойств, ИсключаяСвойства) Тогда
			й = й + 1;
			Продолжить;
		КонецЕсли;
		
		ВидСубконто = ЗначениеИзСтруктуры(ТекСтрока.ВидСубконто);
		
		Если НЕ ЗначениеЗаполнено(ВидСубконто) Тогда
			й = й + 1;
			Продолжить;
		КонецЕсли;
		
		СтрокаВида = ВидыСубконто.Найти(ВидСубконто, "ВидСубконто");
		
		Если СтрокаВида = Неопределено Тогда
			й = й + 1;
			Продолжить;
		КонецЕсли;
		
		ЗначенияСубконто[ВидСубконто] = ЗначениеИзСтруктуры(ТекСтрока.Значение);
		
		й = й + 1;
		
	КонецЦикла;
	
КонецПроцедуры // ЗначенияСубконтоИзСтруктуры()

// Заполняет значение измерения записи регистра бухгалтерии из структуры
// с учетом признаков учета счета
//
// Параметры:
//  Запись				- РегистрБухгалтерииЗапись		- Заполняемая запись регистра бухгалтерии
//  СтруктураЗаписи		- Структура						- Структура данных записи регистра бухгалтерии 
//  ОписаниеПоля		- ОбъектМетаданных.Измерение	- Описание метаданных измерения регистра 
//  ДтКт				- Строка						- признак Дт/Кт - "Дт", "Кт", "" 
//
Процедура ЗаполнитьИзмерениеРегистраБухгалтерии(Запись, СтруктураЗаписи, ОписаниеПоля, ДтКт)
	
	ЗаполнятьЗначение = Истина;
	
	Если ЗначениеЗаполнено(ОписаниеПоля.ПризнакУчета) Тогда
		Если НЕ Запись["Счет" + ДтКт][ОписаниеПоля.ПризнакУчета.Имя] Тогда
			ЗаполнятьЗначение = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнятьЗначение Тогда
		Запись[ОписаниеПоля.Имя + ДтКт] = ЗначениеИзСтруктуры(СтруктураЗаписи[ОписаниеПоля.Имя + ДтКт]);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьИзмерениеРегистраБухгалтерии()

// Заполняет значение ресурса записи регистра бухгалтерии из структуры
// с учетом признаков учета счета
//
// Параметры:
//  Запись				- РегистрБухгалтерииЗапись		- Заполняемая запись регистра бухгалтерии
//  СтруктураЗаписи		- Структура						- Структура данных записи регистра бухгалтерии 
//  ОписаниеПоля		- ОбъектМетаданных.Ресурс		- Описание метаданных ресурса регистра 
//  ДтКт				- Строка						- признак Дт/Кт - "Дт", "Кт", "" 
//
Процедура ЗаполнитьРесурсРегистраБухгалтерии(Запись, СтруктураЗаписи, ОписаниеПоля, ДтКт)
	
	ЗаполнятьЗначение = Истина;
	
	Если ЗначениеЗаполнено(ОписаниеПоля.ПризнакУчета) Тогда
		Если НЕ Запись["Счет" + ДтКт][ОписаниеПоля.ПризнакУчета.Имя] Тогда
			ЗаполнятьЗначение = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнятьЗначение Тогда
		Запись[ОписаниеПоля.Имя + ДтКт] = ЗначениеИзСтруктуры(СтруктураЗаписи[ОписаниеПоля.Имя + ДтКт]);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРесурсРегистраБухгалтерии()

// Функция - возвращает представление описания объекта в текстовом формате JSON
//
// Параметры:
//  ОписаниеОбъекта	 - Структура, Массив(Структура)	 - описание объекта для преобразования
// 
// Возвращаемое значение:
//  Строка - представление описания объекта в текстовом формате JSON
//
Функция ЗаписатьОписаниеОбъектаВJSON(Знач ОписаниеОбъекта) Экспорт
	
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Unix, Символы.Таб));
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON();
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.УниверсальнаяДата;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.Microsoft;
	
	Попытка
		ЗаписатьJSON(Запись, ОписаниеОбъекта, НастройкиСериализации);
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
	Возврат Запись.Закрыть();
	
КонецФункции // ЗаписатьОписаниеОбъектаВJSON()

// Функция - возвращает описание объекта, прочитанное из текстового формата JSON
//
// Параметры:
//  ТекстОписанияОбъекта       - Строка           - представление описания объекта в текстовом формате JSON
//  СвойстваСоЗначениемДата    - Массив, Строка   - список свойств, которые должны быть преобразованы в формат даты
// 
// Возвращаемое значение:
//  Структура, Массив(Структура)	 - описание объекта 
//
Функция ПрочитатьОписаниеОбъектаИзJSON(Знач ТекстОписанияОбъекта, Знач СвойстваСоЗначениемДата = Неопределено) Экспорт
	
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(ТекстОписанияОбъекта);
		
	Если ТипЗнч(СвойстваСоЗначениемДата) = Тип("Строка") Тогда
		СвойстваСоЗначениемДата = РазложитьСтроку(СвойстваСоЗначениемДата, ",", Ложь);
	ИначеЕсли НЕ ТипЗнч(СвойстваСоЗначениемДата) = Тип("Массив") Тогда
		СвойстваСоЗначениемДата = Новый Массив();
	КонецЕсли;
		
	Попытка
		Возврат ПрочитатьJSON(Чтение, Ложь, СвойстваСоЗначениемДата, ФорматДатыJSON.Microsoft, "ОбработчикЧтенияПараметровИзJSON", ЭтотОбъект);
	Исключение
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецФункции // ПрочитатьОписаниеОбъектаИзJSON()

Функция ОбработчикЧтенияПараметровИзJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Если Лев(Значение, 6) = "/Date(" Тогда
			Значение = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.Microsoft);
			Если Значение < '00020101000000' Тогда
				Значение = '00010101000000';
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции // ОбработчикЧтенияПараметровИзJSON()

#КонецОбласти // ПреобразованияОбъектовСлужебные

#Область СохранениеЗначенийСлужебные

// Преобразует переданное значение в структуру, значения атомарных типов, возвращаются как есть
//
// Параметры:
//  Значение			- Произвольный				- Преобразуемое значение
//
// Возвращаемое значение:
//   Структура, Массив, Число, Строка, Дата, Булево			- результат преобразования
//
Функция ЗначениеВСтруктуру(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка")
	 ИЛИ ТипЗнч(Значение) = Тип("Число")
	 ИЛИ ТипЗнч(Значение) = Тип("Булево")
	 ИЛИ ТипЗнч(Значение) = Тип("Дата") Тогда
		Возврат Значение;
	ИначеЕсли ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
		Возврат "/ValueStorage(" + ЗначениеВСтрокуВнутр(Значение) + ")/";
	ИначеЕсли ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		Возврат "/UniqueID(" + СокрЛП(Значение) + ")/";
	ИначеЕсли ТипЗнч(Значение) = Тип("NULL") Тогда
		Возврат "/NULL()/";
	ИначеЕсли ТипЗнч(Значение) = Тип("Неопределено") Тогда
		Возврат "/Undefined()/";
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив")
		  ИЛИ ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
		Возврат МассивВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Структура")
		  ИЛИ ТипЗнч(Значение) = Тип("ФиксированнаяСтруктура") Тогда
		Возврат СтруктураВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Соответствие")
		  ИЛИ ТипЗнч(Значение) = Тип("ФиксированноеСоответствие") Тогда
		Возврат СоответствиеВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
		Возврат СписокЗначенийВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		Возврат ТаблицаЗначенийВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ДеревоЗначений") Тогда
		Возврат ДеревоЗначенийВСтруктуру(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ОписаниеТипов") Тогда
		Возврат ОписаниеТиповВСтруктуру(Значение);
	КонецЕсли;
	
	МетаОбъект = Метаданные.НайтиПоТипу(ТипЗнч(Значение));
	Если НЕ МетаОбъект = Неопределено Тогда
		Возврат СсылкаВСтруктуру(Значение);
	Иначе
		Возврат "/UnknownType(" + ЗначениеВСтрокуВнутр(Значение) + ")/";
	КонецЕсли;
	
КонецФункции // ЗначениеВСтруктуру()

// Преобразует переданную ссылку в структуру, значения атомарных типов, возвращаются как есть
//
// Параметры:
//  Значение			- ЛюбаяСсылка				- Преобразуемое ссылка
//
// Возвращаемое значение:
//   Структура			- результат преобразования
//
Функция СсылкаВСтруктуру(Значение) Экспорт
	
	МетаданныеЗначения = Метаданные.НайтиПоТипу(ТипЗнч(Значение));
	
	Если МетаданныеЗначения = Неопределено Тогда
		Возврат Значение;
	ИначеЕсли ВРег(Лев(МетаданныеЗначения.ПолноеИмя(), 13)) = ВРег("Перечисление.") Тогда
		ИмяПредопределенныхДанных = "";
		Если ЗначениеЗаполнено(Значение) Тогда
			Индекс = Перечисления[МетаданныеЗначения.Имя].Индекс(Значение);
			ИмяПредопределенныхДанных = МетаданныеЗначения.ЗначенияПеречисления.Получить(Индекс).Имя;
		КонецЕсли;
		ОписаниеЗначения = Новый Структура("Тип, Ид, ИдПолный, Предопределенный, ИмяПредопределенныхДанных, Представление"
										, МетаданныеЗначения.ПолноеИмя()
										, ИмяПредопределенныхДанных
										, ЗначениеВСтрокуВнутр(Значение)
										, Истина
										, ИмяПредопределенныхДанных
										, СокрЛП(Значение));
	Иначе
		Предопределенный = Ложь;
		Если Метаданные.Справочники.Содержит(МетаданныеЗначения)
		 ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеЗначения)
		 ИЛИ Метаданные.ПланыСчетов.Содержит(МетаданныеЗначения)
		 ИЛИ Метаданные.ПланыВидовРасчета.Содержит(МетаданныеЗначения) Тогда
			Предопределенный = Значение.Предопределенный;
		ИначеЕсли Метаданные.ПланыОбмена.Содержит(МетаданныеЗначения) Тогда
			Предопределенный = Значение.ЭтотУзел;
		КонецЕсли;
		ОписаниеЗначения = Новый Структура("Тип, Ид, ИдПолный, Предопределенный, ИмяПредопределенныхДанных, Представление"
										, МетаданныеЗначения.ПолноеИмя()
										, СокрЛП(Значение.УникальныйИдентификатор())
										, ЗначениеВСтрокуВнутр(Значение)
										, Предопределенный
										, ПолучитьИмяПредопределенныхДанных(Значение)
										, СокрЛП(Значение));
	КонецЕсли;
	
	ПравилоВыгрузки = ПолучитьПравилоВыгрузкиТипа(МетаданныеЗначения.ПолноеИмя());
	
	Если ПравилоВыгрузки = Неопределено Тогда
		Возврат ОписаниеЗначения;
	КонецЕсли;
			
	КодДополненияЗначения = "";
	Если НЕ ПравилоВыгрузки.Модуль = Неопределено Тогда
		КодДополненияЗначения = КодДополненияЗначения + "ПравилоВыгрузки.Модуль.";
	КонецЕсли;
	
	КодДополненияЗначения = КодДополненияЗначения + СокрЛП(ПравилоВыгрузки.ФункцияДополнения) + "(ОписаниеЗначения, Значение);";
	
	Попытка
		Выполнить(КодДополненияЗначения);
	Исключение
		ТекстОшибки = "Ошибка дополнения значения: " + СокрЛП(Значение) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
	Возврат ОписаниеЗначения;
	
КонецФункции // СсылкаВСтруктуру()

// Получает правило преобразования в структуру для указанного типа значений
//
// Параметры:
//  ПолноеИмяТипа         - Строка    - Имя типа преобразуемого значения
//
// Возвращаемое значение:
//	Структура					- Правило выгрузки типа
//		ФункцияДополнения - Строка    - имя функции дополнительной обработки данных при выгрузке
//		Модуль            - Модуль    - модуль из которого будет вызвана функция дополнения
//
Функция ПолучитьПравилоВыгрузкиТипа(ПолноеИмяТипа)
	
	Правила = ПолучитьПравилаВыгрузкиТипов();
	
	Возврат Правила[ПолноеИмяТипа];
	
КонецФункции // ПолучитьПравилоВыгрузкиТипа()

// Добавляет правило преобразования в структуру для указанного типа значений
//
// Параметры:
//	ТипИсточника           - Строка                   - Имя типа преобразуемого значения
//	ФункцияДополнения      - Строка                   - Имя функции преобразования значения
//  Модуль                 - УправляемаяФорма, Модуль - модуль, содержащий функцию дополнения,
//                                                      Неопределено - функция будет вызвана
//                                                      из модуля обработки (этот модуль)
//
Процедура ДобавитьПравилоВыгрузкиТипа(ТипИсточника, ФункцияДополнения, Модуль = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ПравилаВыгрузкиТипов) = Тип("Соответствие") Тогда
		ПравилаВыгрузкиТипов = Новый Соответствие();
	КонецЕсли;
	
	ПравилаВыгрузкиТипов.Вставить(ТипИсточника, Новый Структура("ФункцияДополнения, Модуль", ФункцияДополнения, Модуль));
	
КонецПроцедуры // ДобавитьПравилоВыгрузкиТипа()

// Переопределяемая процедура, позволяющая дополнить алгоритмы преобразования данных в структуру
//
// Параметры:
//
// Возвращаемое значение:
//   Соответствие			- правила выгрузки типов
//		<ТипЗначения>			- Структура			- Имя выгружаемого типа значения
//			ФункцияДополнения	- Строка			- Имя функции дополнительной обработки данных при выгрузке
//
Функция ПолучитьПравилаВыгрузкиТипов()
	
	Если НЕ ТипЗнч(ПравилаВыгрузкиТипов) = Тип("Соответствие") Тогда
		ПравилаВыгрузкиТипов = Новый Соответствие();
	КонецЕсли;
	
	Возврат ПравилаВыгрузкиТипов;
	
КонецФункции // ПолучитьПравилаВыгрузкиТипов()

#КонецОбласти // СохранениеЗначенийСлужебные

#Область ПолучениеЗначенийСлужебные

// Получает значение из переданного описания, значения атомарных типов, возвращаются как есть
//
// Параметры:
//  СтруктураЗначения          - Структура, Число, Строка,      - Описание значение
//                               Дата, Булево
//  ПолучатьБитыеСсылки        - Булево                         - Истина - если объект по ссылке не существует,
//                                                                будет возвращена битая ссылка;
//                                                                Ложь - будет возвращена пустая ссылка
//
// Возвращаемое значение:
//   Произвольный			- результат преобразования
//
Функция ЗначениеИзСтруктуры(СтруктураЗначения, ПолучатьБитыеСсылки = Ложь) Экспорт
	
	Если ТипЗнч(СтруктураЗначения) = Тип("Строка") Тогда
		Если Лев(СтруктураЗначения, 1) = "/" Тогда
			Если Лев(СтруктураЗначения, 6) = "/Date(" Тогда
				ВремДата = ПрочитатьДатуJSON(СтруктураЗначения, ФорматДатыJSON.Microsoft);
				Если ВремДата < '00020101000000' Тогда
					Возврат '00010101000000';
				Иначе
					Возврат ВремДата;
				КонецЕсли;
			ИначеЕсли Лев(СтруктураЗначения, 14) = "/ValueStorage(" Тогда
				Если Прав(СтруктураЗначения, 2) = ")/" Тогда
					Возврат ЗначениеИзСтрокиВнутр(Сред(СтруктураЗначения, 15, СтрДлина(СтруктураЗначения) - 16));
				КонецЕсли;
			ИначеЕсли Лев(СтруктураЗначения, 10) = "/UniqueID(" Тогда
				Если Прав(СтруктураЗначения, 2) = ")/" Тогда
					Возврат Новый УникальныйИдентификатор(Сред(СтруктураЗначения, 11, СтрДлина(СтруктураЗначения) - 12));
				КонецЕсли;
			ИначеЕсли СтруктураЗначения = "/NULL()/" Тогда
				Возврат NULL;
			ИначеЕсли СтруктураЗначения = "/Undefined()/" Тогда
				Возврат Неопределено;
			ИначеЕсли Лев(СтруктураЗначения, 13) = "/UnknownType(" Тогда
				Если Прав(СтруктураЗначения, 2) = ")/" Тогда
					Возврат ЗначениеИзСтрокиВнутр(Сред(СтруктураЗначения, 14, СтрДлина(СтруктураЗначения) - 15));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Возврат СтруктураЗначения;
	ИначеЕсли ТипЗнч(СтруктураЗначения) = Тип("Число")
	 ИЛИ ТипЗнч(СтруктураЗначения) = Тип("Булево")
	 ИЛИ ТипЗнч(СтруктураЗначения) = Тип("Дата")
	 ИЛИ ТипЗнч(СтруктураЗначения) = Тип("ХранилищеЗначения") Тогда
		Возврат СтруктураЗначения;
	КонецЕсли;
	
	ВремТип = СтруктураТипЗнч(СтруктураЗначения);
	
	Если ВремТип = Тип("Массив") Тогда
		Возврат МассивИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("Структура") Тогда
		Возврат СтруктураИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("Соответствие") Тогда
		Возврат СоответствиеИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("СписокЗначений") Тогда
		Возврат СписокЗначенийИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("ТаблицаЗначений") Тогда
		Возврат ТаблицаЗначенийИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("ДеревоЗначений") Тогда
		Возврат ДеревоЗначенийИзСтруктуры(СтруктураЗначения);
	ИначеЕсли ВремТип = Тип("ОписаниеТипов") Тогда
		Возврат ОписаниеТиповИзСтруктуры(СтруктураЗначения);
	КонецЕсли;
	
	МетаОбъект = Метаданные.НайтиПоТипу(ВремТип);
	Если НЕ МетаОбъект = Неопределено Тогда
		Возврат СсылкаИзСтруктуры(СтруктураЗначения, ПолучатьБитыеСсылки);
	КонецЕсли;
	
	Возврат СтруктураЗначения;
	
КонецФункции // ЗначениеИзСтруктуры()

// Получает ссылку по переданной структуре
//
// Параметры:
//  ОписаниеЗначения			- Структура			- Структура описания ссылки
//  ПолучатьБитыеСсылки         - Булево            - Истина - если объект по ссылке не существует,
//                                                    будет возвращена битая ссылка;
//                                                    Ложь - будет возвращена пустая ссылка
//
// Возвращаемое значение:
//   ЛюбаяСсылка			- полученная ссылка
//
Функция СсылкаИзСтруктуры(ОписаниеЗначения, ПолучатьБитыеСсылки = Ложь) Экспорт
	
	Если Не ТипЗнч(ОписаниеЗначения) = Тип("Структура") Тогда
		Возврат ОписаниеЗначения;
	КонецЕсли;
	
	ПравилоЗагрузки = ПолучитьПравилоЗагрузкиТипа(ОписаниеЗначения.Тип);
	
	ОписаниеВида = ПолучитьОписаниеВидаОбъектов(ПравилоЗагрузки.ТипЗначения);

	МетаОбъект = Метаданные.НайтиПоПолномуИмени(ПравилоЗагрузки.ТипЗначения);
	
	Если МетаОбъект = Неопределено Тогда
		ТекстОшибки = "Не найден тип " + ПравилоЗагрузки.ТипЗначения 
		            + " для значения: """
		            + ОписаниеЗначения.Представление
		            + " (" + ОписаниеЗначения.Ид + ")""";
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Коллекция = ОписаниеВида.Коллекция;
	
	Результат = Коллекция[МетаОбъект.Имя].ПустаяСсылка();
	
	Если НЕ Найти(ОписаниеЗначения.ИдПолный, "00000000000000000000000000000000") = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	КодПоискаСсылки = "Результат = ";
	Если НЕ ПравилоЗагрузки.Модуль = Неопределено Тогда
		КодПоискаСсылки = КодПоискаСсылки + "ПравилоЗагрузки.Модуль.";
	КонецЕсли;
	
	КодПоискаСсылки = КодПоискаСсылки + СокрЛП(ПравилоЗагрузки.ФункцияПоиска) + "(ОписаниеВида, МетаОбъект, ОписаниеЗначения);";
	
	Попытка
		Выполнить(КодПоискаСсылки);
	Исключение
		ТекстОшибки = "Ошибка поиска ссылки по описанию: " + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Сообщить("Не найдено значение "
			+ МетаОбъект.ПолноеИмя() + ": """
			+ ОписаниеЗначения.Представление
			+ " (" + ОписаниеЗначения.Ид + ")""");
		Если ПолучатьБитыеСсылки Тогда
			Результат = Коллекция[МетаОбъект.Имя].ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеЗначения.Ид));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // СсылкаИзСтруктуры()

// Получает правило преобразования данных из структуры для указанного типа значений
//
// Параметры:
//  ПолноеИмяТипа			- Строка				- Имя типа преобразуемого значения
//
// Возвращаемое значение:
//	Структура					- Правило выгрузки типа
//		ТипЗначения			- Строка			- Имя типа значения целевых данных
//		ФункцияПоиска		- Строка			- Имя функции поиска значения
//
Функция ПолучитьПравилоЗагрузкиТипа(ПолноеИмяТипа)
	
	Правила = ПолучитьПравилаЗагрузкиТипов();
	
	Правило = Правила[ПолноеИмяТипа];
	
	Если Правило = Неопределено Тогда
		Правило = Новый Структура("ТипЗначения, ФункцияПоиска, Модуль", ПолноеИмяТипа, "НайтиЗначениеПоОписанию");
	КонецЕсли;
	
	Возврат Правило;
	
КонецФункции // ПолучитьПравилоЗагрузкиТипа()

// Добавляет правило преобразования данных из структуры для указанного типа значений
//
// Параметры:
//	ТипИсточника             - Строка                   - Имя типа преобразуемого значения
//	ТипПриемника             - Строка                   - Имя типа целевого значения
//	ФункцияПоиска            - Строка                   - Имя функции поиска значения
//  Модуль                   - УправляемаяФорма, Модуль - модуль, содержащий функцию поиска,
//                                                        Неопределено - функция будет вызвана
//                                                        из модуля обработки (этот модуль)
//
Процедура ДобавитьПравилоЗагрузкиТипа(ТипИсточника, ТипПриемника, ФункцияПоиска, Модуль = Неопределено) Экспорт
	
	Если НЕ ТипЗнч(ПравилаЗагрузкиТипов) = Тип("Соответствие") Тогда
		ПравилаЗагрузкиТипов = Новый Соответствие();
	КонецЕсли;
	
	ПравилаЗагрузкиТипов.Вставить(ТипИсточника, Новый Структура("ТипЗначения, ФункцияПоиска, Модуль", ТипПриемника, ФункцияПоиска, Модуль));
	
КонецПроцедуры // ДобавитьПравилоЗагрузкиТипа()

// Переопределяемая процедура, позволяющая дополнить алгоритмы преобразования данных из структуры
//
// Параметры:
//
// Возвращаемое значение:
//   Соответствие(Структура)			- правила выгрузки типов
//		<ТипЗначения>			- Строка			- Имя типа значения преобразованных данных
//		ТипЗначения					- Строка			- Имя типа значения целевых данных
//		ФункцияПоиска				- Строка			- Имя функции поиска значения
//
Функция ПолучитьПравилаЗагрузкиТипов()
	
	Если НЕ ТипЗнч(ПравилаЗагрузкиТипов) = Тип("Соответствие") Тогда
		ПравилаЗагрузкиТипов = Новый Соответствие();
	КонецЕсли;
	
	Возврат ПравилаЗагрузкиТипов;
	
КонецФункции // ПолучитьПравилаЗагрузкиТипов()

// Функция поиска значений по структуре описания (шаблон процедуры поиска)
// Ищет значение по имени предопределенных данных, затем по полному иидентификатору, затем по представлению (наименованию)
//
// Параметры:
//		ОписаниеВида			- Структура				- Описание вида значения
//		МетаОбъект				- ОписаниеМетаданных	- Описание метаданных обрабатываемого типа значений
// 		ОписаниеЗначения		- Структура				- Структура значения для обработки
//
// Возвращаемое значение:
//   ЛюбаяСсылка			- найденное значение
//
//@skip-warning
Функция НайтиЗначениеПоОписанию(ОписаниеВида, МетаОбъект, ОписаниеЗначения)
	
	Коллекция = ОписаниеВида.Коллекция;
	
	Если ЗначениеЗаполнено(ОписаниеЗначения.ИмяПредопределенныхДанных) Тогда
		Если ВРег(ОписаниеВида.Имя) = ВРег("ПланОбмена")
		   И ВРег(ОписаниеЗначения.ИмяПредопределенныхДанных) = ВРег("ЭтотУзел") Тогда
			Возврат Коллекция[МетаОбъект.Имя].ЭтотУзел();
		Иначе
			Возврат Коллекция[МетаОбъект.Имя][ОписаниеЗначения.ИмяПредопределенныхДанных];
		КонецЕсли;
	Иначе
		ВремСсылка = ЗначениеИзСтрокиВнутр(ОписаниеЗначения.ИдПолный);
		Если ЗначениеЗаполнено(ВремСсылка) Тогда
			ВремОбъект = ВремСсылка.ПолучитьОбъект();
			Если НЕ ВремОбъект = Неопределено Тогда
				Возврат ВремСсылка;
			КонецЕсли;
		КонецЕсли;
		
		Если Найти(ОписаниеЗначения.ИдПолный, "00000000000000000000000000000000") = 0 Тогда
			ВремСсылка = Коллекция[МетаОбъект.Имя].ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеЗначения.Ид));
			ВремОбъект = ВремСсылка.ПолучитьОбъект();
			Если НЕ ВремОбъект = Неопределено Тогда
				Возврат ВремСсылка;
			КонецЕсли;
		
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОписаниеЗначения.Представление)
	   И (ВРег(ОписаниеВида.Имя) = ВРег("Справочник")
			ИЛИ ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовХарактеристик")
			ИЛИ ВРег(ОписаниеВида.Имя) = ВРег("ПланСчетов")
			ИЛИ ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовРасчета")
			ИЛИ ВРег(ОписаниеВида.Имя) = ВРег("ПланОбмена")
			ИЛИ ВРег(ОписаниеВида.Имя) = ВРег("Задача")) Тогда
		
		ВремСсылка = Коллекция[МетаОбъект.Имя].НайтиПоНаименованию(ОписаниеЗначения.Представление, Истина); 
		Если НЕ ВремСсылка.Пустая() Тогда
			Возврат ВремСсылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Коллекция[МетаОбъект.Имя].ПустаяСсылка();
	
КонецФункции // НайтиЗначениеПоОписанию()

#КонецОбласти // ПолучениеЗначенийСлужебные

#Область ПреобразованияКоллекций

// Функция определяет тип значения, сохраненного в массив или структуру
// для преобразования в значение 1С
//
// Параметры:
//		СтруктураЗначения		- Структура, Массив		- Проверяемое значения
//
// Возвращаемое значение:
//   Тип			- Тип 1С (Массив, Структура, Соответствие, СписокЗначений, ТаблицаЗначений, ДеревоЗначений, ЛюбаяСсылка)
//
Функция СтруктураТипЗнч(СтруктураЗначения)
	
	Если ТипЗнч(СтруктураЗначения) = Тип("Массив") Тогда
		Если СтруктураЗначения.Количество() = 0 Тогда
			Возврат Тип("Массив");
		КонецЕсли;
		Если ТипЗнч(СтруктураЗначения[0]) = Тип("Структура") Тогда
			Если СтруктураЗначения[0].Свойство("Ключ")
			   И СтруктураЗначения[0].Свойство("Значение") Тогда
				Возврат Тип("Соответствие");
			ИначеЕсли СтруктураЗначения[0].Свойство("Представление")
			   И СтруктураЗначения[0].Свойство("Значение")
			   И СтруктураЗначения[0].Свойство("Пометка") Тогда
				Возврат Тип("СписокЗначений");
			Иначе
				Возврат Тип("ТаблицаЗначений");
			КонецЕсли;
		КонецЕсли;
		Возврат Тип("Массив");
	ИначеЕсли ТипЗнч(СтруктураЗначения) = Тип("Структура") Тогда
		Если СтруктураЗначения.Свойство("Тип")
		   И СтруктураЗначения.Свойство("Ид")
		   И СтруктураЗначения.Свойство("ИдПолный") Тогда
			Если СтруктураЗначения.Свойство("Ссылка") Тогда
				ПравилоЗагрузки = ПолучитьПравилоЗагрузкиТипа(СтруктураЗначения.Ссылка.Тип);
				Возврат Тип(СтрЗаменить(ПравилоЗагрузки.ТипЗначения, ".", "Объект."));
			Иначе
				ПравилоЗагрузки = ПолучитьПравилоЗагрузкиТипа(СтруктураЗначения.Тип);
				Возврат Тип(СтрЗаменить(ПравилоЗагрузки.ТипЗначения, ".", "Ссылка."));
			КонецЕсли;
		КонецЕсли;
		Если СтруктураЗначения.Свойство("Типы")
		   И СтруктураЗначения.Свойство("КвалификаторыСтроки")
		   И СтруктураЗначения.Свойство("КвалификаторыЧисла")
		   И СтруктураЗначения.Свойство("КвалификаторыДаты") Тогда
			Если ТипЗнч(СтруктураЗначения.Типы) = Тип("Массив") Тогда
				Возврат Тип("ОписаниеТипов");
			КонецЕсли;
		КонецЕсли;
		Если СтруктураЗначения.Свойство("Строки") Тогда
			Если ТипЗнч(СтруктураЗначения.Строки) = Тип("Массив") Тогда
				Возврат Тип("ДеревоЗначений");
			КонецЕсли;
		КонецЕсли;
		Возврат Тип("Структура");
	Иначе
		Возврат ТипЗнч(СтруктураЗначения);
	КонецЕсли;
	
КонецФункции // СтруктураТипЗнч()

// Функция рекурсивно преобразует структуру значений в структуру с "простыми" типами
//
// Параметры:
//		Значение		- Структура		- Значение для преобразования
//
// Возвращаемое значение:
//   Структура			- Результат преобразования
//
Функция СтруктураВСтруктуру(Значение)
	
	Если НЕ (ТипЗнч(Значение) = Тип("Структура")
		  ИЛИ ТипЗнч(Значение) = Тип("ФиксированнаяСтруктура")) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремСтруктура = Новый Структура();
	Для Каждого ТекЭлемент Из Значение Цикл
		ВремСтруктура.Вставить(ТекЭлемент.Ключ, ЗначениеВСтруктуру(ТекЭлемент.Значение));
	КонецЦикла;
	
	Возврат ВремСтруктура;
	
КонецФункции // СтруктураВСтруктуру()

// Функция рекурсивно преобразует структуру с "простыми" типами значений в структуру значений 1С
//
// Параметры:
//		СтруктураЗначения		- Структура		- Значение для преобразования
//
// Возвращаемое значение:
//   Структура			- Результат преобразования
//
Функция СтруктураИзСтруктуры(СтруктураЗначения)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремСтруктура = Новый Структура();
	
	Для Каждого ТекЭлемент Из СтруктураЗначения Цикл
		ВремСтруктура.Вставить(ТекЭлемент.Ключ, ЗначениеИзСтруктуры(ТекЭлемент.Значение));
	КонецЦикла;
	
	Возврат ВремСтруктура;
	
КонецФункции // СтруктураИзСтруктуры()

// Функция рекурсивно преобразует массив значений в массив с "простыми" типами
//
// Параметры:
//		Значение		- Массив		- Значение для преобразования
//
// Возвращаемое значение:
//   Массив			- Результат преобразования
//
Функция МассивВСтруктуру(Значение)
	
	Если НЕ (ТипЗнч(Значение) = Тип("Массив")
		  ИЛИ ТипЗнч(Значение) = Тип("ФиксированныйМассив")) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремМассив = Новый Массив();
	Для Каждого ТекЗначение Из Значение Цикл
		ВремМассив.Добавить(ЗначениеВСтруктуру(ТекЗначение));
	КонецЦикла;
	
	Возврат ВремМассив;
	
КонецФункции // МассивВСтруктуру()

// Функция рекурсивно преобразует массив с "простыми" типами значений в массив значений 1С
//
// Параметры:
//		СтруктураЗначения		- Массив		- Значение для преобразования
//
// Возвращаемое значение:
//   Массив			- Результат преобразования
//
Функция МассивИзСтруктуры(СтруктураЗначения)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремМассив = Новый Массив();
	Для Каждого ТекЗначение Из СтруктураЗначения Цикл
		ВремМассив.Добавить(ЗначениеИзСтруктуры(ТекЗначение));
	КонецЦикла;
	
	Возврат ВремМассив;
	
КонецФункции // МассивИзСтруктуры()

// Функция рекурсивно преобразует соответствие значений в массив структур с "простыми" типами
//
// Параметры:
//		Значение		- Соответствие		- Значение для преобразования
//
// Возвращаемое значение:
//   Массив(Структура)			- Результат преобразования
//		Ключ						- Произвольный		- Ключ элемента соответствия
//		Значение					- Произвольный		- Значение элемента соответствия
//
Функция СоответствиеВСтруктуру(Значение)
	
	Если НЕ (ТипЗнч(Значение) = Тип("Соответствие")
	 ИЛИ ТипЗнч(Значение) = Тип("ФиксированноеСоответствие")) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремСоответствие = Новый Массив();
	
	Для Каждого ТекЭлемент Из Значение Цикл
		ВремСоответствие.Добавить(Новый Структура("Ключ,
												  |Значение",
												  ЗначениеВСтруктуру(ТекЭлемент.Ключ),
												  ЗначениеВСтруктуру(ТекЭлемент.Значение)));
	КонецЦикла;
	
	Возврат ВремСоответствие;
	
КонецФункции // СоответствиеВСтруктуру()

// Функция рекурсивно преобразует массив структур с "простыми" типами значений в соответствие значений 1С
//
// Параметры:
//		СтруктураЗначения		- Массив(Структура)		- Значение для преобразования
//			Ключ					- Произвольный			- Ключ элемента соответствия
//			Значение				- Произвольный			- Значение элемента соответствия
//
// Возвращаемое значение:
//   Соответствие			- Результат преобразования
//
Функция СоответствиеИзСтруктуры(СтруктураЗначения)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураЗначения.Количество() = 0 Тогда
		Возврат Новый Соответствие();
	КонецЕсли;
	
	Если СтруктураЗначения[0].Свойство("Ключ")  = Неопределено Тогда
		Возврат Новый Соответствие();
	КонецЕсли;
	
	ВремСоответствие = Новый Соответствие();
	
	Для Каждого ТекЭлемент Из СтруктураЗначения Цикл
		ВремСоответствие.Вставить(ЗначениеИзСтруктуры(ТекЭлемент.Ключ), ЗначениеИзСтруктуры(ТекЭлемент.Значение));
	КонецЦикла;
	
	Возврат ВремСоответствие;
	
КонецФункции // СоответствиеИзСтруктуры()

// Функция рекурсивно преобразует список значений в массив структур с "простыми" типами
//
// Параметры:
//		Значение		- СписокЗначений		- Значение для преобразования
//
// Возвращаемое значение:
//   Массив(Структура)			- Результат преобразования
//		Значение					- Произвольный		- Значение элемента списка значений
//		Представление				- Строка			- Представление элемента списка значений
//		Пометка						- Булево			- Пометка элемента списка значений
//
Функция СписокЗначенийВСтруктуру(Значение)
	
	Если НЕ ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремМассив = Новый Массив();
	Для Каждого ТекЭлемент Из Значение Цикл
		ВремЭлемент = Новый Структура("Значение, Представление, Пометка, Картинка"
									, ЗначениеВСтруктуру(ТекЭлемент.Значение)
									, ТекЭлемент.Представление
									, ТекЭлемент.Пометка
									, Неопределено);
		ВремМассив.Добавить(ВремЭлемент);
	КонецЦикла;
	
	Возврат ВремМассив;
	
КонецФункции // СписокЗначенийВСтруктуру()

// Функция рекурсивно преобразует массив структур с "простыми" типами значений в список значений 1С
//
// Параметры:
//		СтруктураЗначения		- Массив(Структура)		- Значение для преобразования
//			Значение				- Произвольный			- Значение элемента соответствия
//			Представление			- Строка				- Представление элемента списка значений
//			Пометка					- Булево				- Пометка элемента списка значений
//
// Возвращаемое значение:
//   СписокЗначений			- Результат преобразования
//
Функция СписокЗначенийИзСтруктуры(СтруктураЗначения)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураЗначения.Количество() = 0 Тогда
		Возврат Новый СписокЗначений();
	КонецЕсли;
	
	Если СтруктураЗначения[0].Свойство("Значение")  = Неопределено Тогда
		Возврат Новый СписокЗначений();
	КонецЕсли;
	
	ВремСписок = Новый СписокЗначений();
	Для Каждого ТекЭлемент Из СтруктураЗначения Цикл
		ВремСписок.Добавить(ЗначениеИзСтруктуры(ТекЭлемент.Значение), ТекЭлемент.Представление, ТекЭлемент.Пометка);
	КонецЦикла;
	
	Возврат ВремСписок;
	
КонецФункции // СписокЗначенийИзСтруктуры()

// Функция рекурсивно преобразует строку таблицы/дерева значений в структуру с "простыми" типами
//
// Параметры:
//		СтрокаЗначений	- СтрокаТаблицыЗначений		- Строка для преобразования
//						  СтрокаДереваЗначений
//		СписокСвойств	- Массив(Строка)			- Список заполняемых полей строки
//
// Возвращаемое значение:
//   Структура			- Результат преобразования
//		<Имя колонки>			- Произвольный		- Значение поля строки таблицы/дерева
//
Функция СтрокаЗначенийВСтруктуру(СтрокаЗначений, СписокСвойств)
	
	ВремСтрока = Новый Структура();
	
	Для Каждого ТекСвойство Из СписокСвойств Цикл
		ВремСтрока.Вставить(ТекСвойство, ЗначениеВСтруктуру(СтрокаЗначений[ТекСвойство]));
	КонецЦикла;
	
	Возврат ВремСтрока;
	
КонецФункции // СтрокаЗначенийВСтруктуру()

// Процедура рекурсивно заполняет строку таблицы/дерева значений данными из структуры с "простыми" типами значений
//
// Параметры:
//		СтрокаЗначений		- СтрокаТаблицыЗначений		- Строка для преобразования
//							  СтрокаДереваЗначений
//		СтруктураЗначений	- Структура					- Значение для преобразования
//			<Имя колонки>		- Произвольный				- Значение поля строки таблицы/дерева
//		СписокСвойств		- Массив(Строка)			- Список заполняемых полей строки
//
Процедура СтрокаЗначенийИзСтруктуры(СтрокаЗначений, СтруктураЗначений, СписокСвойств)
	
	Для Каждого ТекСвойство Из СписокСвойств Цикл
		СтрокаЗначений[ТекСвойство] = ЗначениеИзСтруктуры(СтруктураЗначений[ТекСвойство]);
	КонецЦикла;
	
КонецПроцедуры // СтрокаЗначенийИзСтруктуры()

// Функция рекурсивно преобразует таблицу значений в массив структур с "простыми" типами
//
// Параметры:
//		Значение		- ТаблицаЗначений		- Значение для преобразования
//
// Возвращаемое значение:
//   Массив(Структура)			- Результат преобразования
//		<Имя колонки>				- Произвольный		- Значение поля строки таблицы значений
//
Функция ТаблицаЗначенийВСтруктуру(Значение, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВремТаб = Новый Массив();
	
	МассивКолонок = Новый Массив();
	Для Каждого ТекКолонка Из Значение.Колонки Цикл
		Если НЕ ИспользоватьСвойство(ТекКолонка.Имя, СписокСвойств, ИсключаяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		МассивКолонок.Добавить(ТекКолонка.Имя);
	КонецЦикла;
	
	Для Каждого ТекСтрока Из Значение Цикл
		ВремТаб.Добавить(СтрокаЗначенийВСтруктуру(ТекСтрока, МассивКолонок));
	КонецЦикла;
	
	Возврат ВремТаб;
	
КонецФункции // ТаблицаЗначенийВСтруктуру()

// Функция рекурсивно преобразует массив структур с "простыми" типами значений в таблицу значений 1С
//
// Параметры:
//		СтруктураЗначения		- Массив(Структура)		- Значение для преобразования
//			<Имя колонки>			- Произвольный			- Значение поля строки таблицы значений
//
// Возвращаемое значение:
//   ТаблицаЗначений			- Результат преобразования
//
Функция ТаблицаЗначенийИзСтруктуры(СтруктураЗначения, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураЗначения.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений();
	КонецЕсли;
	
	ВремТаб = Новый ТаблицаЗначений();
	МассивКолонок = Новый Массив();
	
	Для Каждого ТекЭлемент Из СтруктураЗначения[0] Цикл
		Если НЕ ИспользоватьСвойство(ТекЭлемент.Ключ, СписокСвойств, ИсключаяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		ВремТаб.Колонки.Добавить(ТекЭлемент.Ключ);
		МассивКолонок.Добавить(ТекЭлемент.Ключ);
	КонецЦикла;
	
	Для Каждого ТекСтрока Из СтруктураЗначения Цикл
		НоваяСтрока = ВремТаб.Добавить();
		СтрокаЗначенийИзСтруктуры(НоваяСтрока, ТекСтрока, МассивКолонок);
	КонецЦикла;
	
	Возврат ВремТаб;
	
КонецФункции // ТаблицаЗначенийИзСтруктуры()

// Функция рекурсивно преобразует строки дерева значений в массив структур с "простыми" типами
//
// Параметры:
//		СтрокиДерева	- КоллекцияСтрокДереваЗначений		- Строки для преобразования
//		СписокСвойств	- Массив(Строка)					- Список заполняемых полей строки
//
// Возвращаемое значение:
//   Массив(Структура)		- Результат преобразования
//		<Имя колонки>			- Произвольный		- Значение поля строки дерева
//
Функция СтрокиДереваВСтруктуру(СтрокиДерева, СписокСвойств)
	
	ВремСтроки = Новый Массив();
	
	Для Каждого ТекСтрока Из СтрокиДерева Цикл
		ВремСтрока = СтрокаЗначенийВСтруктуру(ТекСтрока, СписокСвойств);
		ВремСтрока.Вставить("Строки", СтрокиДереваВСтруктуру(ТекСтрока.Строки, СписокСвойств));
		ВремСтроки.Добавить(ВремСТрока);
	КонецЦикла;
	
	Возврат ВремСтроки;
	
КонецФункции // СтрокиДереваВСтруктуру()

// Процедура рекурсивно заполняет строки дерева значений данными из массива структур с "простыми" типами значений
//
// Параметры:
//		СтрокиДерева		- КоллекцияСтрокДереваЗначений		- Строка для преобразования
//		СтруктураСтрок		- Массив(Структура)					- Значение для преобразования
//			<Имя колонки>		- Произвольный						- Значение поля строки дерева
//		СписокСвойств		- Массив(Строка)					- Список заполняемых полей строк
//
Процедура СтрокиДереваИзСтруктуры(СтрокиДерева, СтруктураСтрок, СписокСвойств)
	
	Для Каждого ТекСтрока Из СтруктураСтрок Цикл
		НоваяСтрока = СтрокиДерева.Добавить();
		СтрокаЗначенийИзСтруктуры(НоваяСтрока, ТекСтрока, СписокСвойств);
		СтрокиДереваИзСтруктуры(НоваяСтрока.Строки, ТекСтрока.Строки, СписокСвойств);
	КонецЦикла;
	
КонецПроцедуры // СтрокиДереваИзСтруктуры()

// Функция рекурсивно преобразует дерево значений в структуру с "простыми" типами
//
// Параметры:
//		Значение		- ДеревоЗначений		- Значение для преобразования
//
// Возвращаемое значение:
//   Структура			- Результат преобразования
//		Строки				- Массив(Структура)	- Строки дерева значений
//			<Имя колонки>		- Произвольный		- Значение поля строки дерева значений
//
Функция ДеревоЗначенийВСтруктуру(Значение, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ТипЗнч(Значение) = Тип("ДеревоЗначений") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивКолонок = Новый Массив();
	Для Каждого ТекКолонка Из Значение.Колонки Цикл
		Если НЕ ИспользоватьСвойство(ТекКолонка.Имя, СписокСвойств, ИсключаяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		МассивКолонок.Добавить(ТекКолонка.Имя);
	КонецЦикла;
	
	ВремДерево = Новый Структура("Строки", СтрокиДереваВСтруктуру(Значение.Строки, МассивКолонок));
	
	Возврат ВремДерево;
	
КонецФункции // ДеревоЗначенийВСтруктуру()

// Функция рекурсивно преобразует структуру с "простыми" типами значений в дерево значений 1С
//
// Параметры:
//		СтруктураЗначения		- Структура				- Значение для преобразования
//			Строки					- Массив(Структура)		- Строки дерева значений
//				<Имя колонки>			- Произвольный			- Значение поля строки дерева значений
//
// Возвращаемое значение:
//   ДеревоЗначений			- Результат преобразования
//
Функция ДеревоЗначенийИзСтруктуры(СтруктураЗначения, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено)
	
	Если НЕ ТипЗнч(СтруктураЗначения) = Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ СтруктураЗначения.Свойство("Строки") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ТипЗнч(СтруктураЗначения.Строки) = Тип("Массив") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураЗначения.Строки.Количество() = 0 Тогда
		Возврат Новый ДеревоЗначений();
	КонецЕсли;
	
	ВремДерево = Новый ДеревоЗначений();
	
	МассивКолонок = Новый Массив();
	
	Для Каждого ТекЭлемент Из СтруктураЗначения.Строки[0] Цикл
		Если ТекЭлемент.Ключ = "Строки" Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ИспользоватьСвойство(ТекЭлемент.Ключ, СписокСвойств, ИсключаяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		ВремДерево.Колонки.Добавить(ТекЭлемент.Ключ);
		МассивКолонок.Добавить(ТекЭлемент.Ключ);
	КонецЦикла;
	
	СтрокиДереваИзСтруктуры(ВремДерево.Строки, СтруктураЗначения.Строки, МассивКолонок);
	
	Возврат ВремДерево;
	
КонецФункции // ДеревоЗначенийИзСтруктуры()

#КонецОбласти // ПреобразованияКоллекций

#Область ПреобразованияОбъектовОбщие

// Преобразует значение ссылочного типа в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ОбъектВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	ОписаниеВида = ПолучитьОписаниеВидаОбъектов(ТипЗнч(Объект));
	
	Если ОписаниеВида = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураОбъекта = Неопределено;
	
	Если ВРег(ОписаниеВида.Имя) = ВРег("Справочник") Тогда
		СтруктураОбъекта = СправочникВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("Документ") Тогда
		СтруктураОбъекта = ДокументВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовХарактеристик") Тогда
		СтруктураОбъекта = ПланВидовХарактеристикВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланСчетов") Тогда
		СтруктураОбъекта = ПланСчетовВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовРасчета") Тогда
		СтруктураОбъекта = ПланВидовРасчетаВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланОбмена") Тогда
		СтруктураОбъекта = ПланОбменаВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("БизнесПроцесс") Тогда
		СтруктураОбъекта = БизнесПроцессВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("Задача") Тогда
		СтруктураОбъекта = ЗадачаВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ОбъектВСтруктуру()

// Заполняет объект (ссылочного типа) из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ЗаполнитьОбъектИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	Попытка
		ОписаниеВида = ПолучитьОписаниеВидаОбъектов(СтруктураОбъекта.Ссылка.Тип);
	Исключение
		ВызватьИсключение СтрШаблон("Ошибка получения описания вида объекта ""%1"": %2%3",
		                            СтруктураОбъекта.Ссылка.Тип,
		                            Символы.ПС,
		                            ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ВРег(ОписаниеВида.Имя) = ВРег("Справочник") Тогда
		СправочникИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("Документ") Тогда
		ДокументИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовХарактеристик") Тогда
		ПланВидовХарактеристикИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланСчетов") Тогда
		ПланСчетовИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовРасчета") Тогда
		ПланВидовРасчетаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланОбмена") Тогда
		ПланОбменаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("БизнесПроцесс") Тогда
		БизнесПроцессИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("Задача") Тогда
		ЗадачаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьОбъектИзСтруктуры()

// Создает объект (ссылочного типа) из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Объект				- созданный объект
//
Функция СоздатьОбъектИзСтруктуры(СтруктураОбъекта, УстановитьСсылку = Ложь, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	Попытка
		ОписаниеВида = ПолучитьОписаниеВидаОбъектов(СтруктураОбъекта.Ссылка.Тип);
	Исключение
		ВызватьИсключение СтрШаблон("Ошибка получения описания вида объекта ""%1"": %2%3",
		                            СтруктураОбъекта.Ссылка.Тип,
		                            Символы.ПС,
		                            ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	МетаОбъект = Метаданные.НайтиПоПолномуИмени(СтруктураОбъекта.Ссылка.Тип);
	Менеджер = ОписаниеВида.Коллекция[МетаОбъект.Имя];
	Конструктор = ОписаниеВида.Конструктор;
	
	Если ВРег(ОписаниеВида.Имя) = ВРег("Справочник") Тогда
		Если ОписаниеВида.МетаКоллекция[МетаОбъект.Имя].Иерархический
		   И ОписаниеВида.МетаКоллекция[МетаОбъект.Имя].ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			Если СтруктураОбъекта.ЭтоГруппа Тогда 
				Конструктор = ОписаниеВида.КонструкторАльт;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("ПланВидовХарактеристик") Тогда
		Если ОписаниеВида.МетаКоллекция[МетаОбъект.Имя].Иерархический Тогда
			Если СтруктураОбъекта.ЭтоГруппа Тогда 
				Конструктор = ОписаниеВида.КонструкторАльт;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Объект = Неопределено;
	
	Если ЗначениеЗаполнено(Конструктор) Тогда
		
		Объект = Вычислить("Менеджер." + Конструктор + "()");
		
		Если УстановитьСсылку Тогда
			Объект.УстановитьСсылкуНового(Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СтруктураОбъекта.Ссылка.Ид)));
		КонецЕсли;
		
		ЗаполнитьОбъектИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства);
		
	КонецЕсли;
	
	Возврат Объект;
	
КонецФункции // СоздатьОбъектИзСтруктуры()

// Преобразует набор записей регистра, полученный по указанному отбору, в структуру
//
// Параметры:
//  Регистр				- Строка					- Строковое представление типа регистра (например: РегистрСведений.КурсыВалют)
//  Отбор				- Структура					- Структура вида <Имя поля>: <Значение поля>, значения полей отбора
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Отбор			- Структура				- Описание отбора записей регистра
//			<имя поля>		- Произвольный			- Значение отбора
//		Записи			- Массив(Структура)		- Массив записей регистра
//			<имя поля>		- Произвольный			- Значения измерений, ресурсов и реквизитов записи регистра
//
Функция РегистрПоОтборуВСтруктуру(Регистр, Отбор) Экспорт
	
	ОписаниеВида = ПолучитьОписаниеВидаОбъектов(Регистр);
	
	Если ОписаниеВида = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МетаОбъект = Метаданные.НайтиПоПолномуИмени(Регистр);
	
	НаборЗаписей = ОписаниеВида.Коллекция[МетаОбъект.Имя].СоздатьНаборЗаписей();
	Для Каждого ТекЭлемент Из Отбор Цикл
		НаборЗаписей.Отбор[ТекЭлемент.Ключ].Установить(ТекЭлемент.Значение);
	КонецЦикла;
	НаборЗаписей.Прочитать();
	
	СтруктураОбъекта = Неопределено;
	
	Если ВРег(ОписаниеВида.Имя) = ВРег("РегистрСведений") Тогда
		СтруктураОбъекта = НаборЗаписейРегистраСведенийВСтруктуру(НаборЗаписей);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрНакопления") Тогда
		СтруктураОбъекта = НаборЗаписейРегистраНакопленияВСтруктуру(НаборЗаписей);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрБухгалтерии") Тогда
		СтруктураОбъекта = НаборЗаписейРегистраБухгалтерииВСтруктуру(НаборЗаписей);
	ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрРасчета") Тогда
		СтруктураОбъекта = НаборЗаписейРегистраРасчетаВСтруктуру(НаборЗаписей);
	КонецЕсли;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // РегистрПоОтборуВСтруктуру()

// Преобразует движения документа в структуру
//
// Параметры:
//  Объект				- ДокументСсылка			- Документ, для которого получаются движения
//  СписокРегистров		- Строка, Массив(Строка)	- Список обрабатываемых регистров (если пустой, то обрабатываются все регистры)
//  ИсключаяРегистры	- Строка, Массив(Строка)	- Список исключаемых регистров (если пустой, то обрабатываются все регистры)
//
// Возвращаемое значение:
//   Структура				- движения документа по регистрам
//		<Имя регистра>			- Структура				- Движения регистра
//			Отбор					- Структура				- Описание отбора записей регистра
//				<имя поля>				- Произвольный			- Значение отбора
//			Записи				- Массив(Структура)		- Массив записей регистра
//				<имя поля>			- Произвольный			- Значения измерений, ресурсов и реквизитов записи регистра
//
Функция ДвиженияДокументаВСтруктуру(Объект, СписокРегистров = Неопределено, ИсключаяРегистры = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	Отбор = Новый Структура("Регистратор", Объект.Ссылка);
	
	СтруктураДвижений = Новый Структура();
	Для Каждого ТекДвижения Из МетаОбъект.Движения Цикл
		Если НЕ ИспользоватьСвойство(ТекДвижения.Имя, СписокРегистров, ИсключаяРегистры) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураДвижений.Вставить(ТекДвижения.Имя, РегистрПоОтборуВСтруктуру(ТекДвижения.ПолноеИмя(), Отбор));
	КонецЦикла;
	
	Возврат СтруктураДвижений;
	
КонецФункции // ДвиженияДокументаВСтруктуру()

// Заполняет движения документа из структуры
//
// Параметры:
//  Объект				- ДокументОбъект			- Документ, для которого заполняются движения
//  СтруктураДвижений	- Структура					- Движения документа по регистрам
//		<Имя регистра>		- Структура				- Движения регистра
//			Отбор				- Структура				- Описание отбора записей регистра
//				<имя поля>			- Произвольный			- Значение отбора
//			Записи			- Массив(Структура)		- Массив записей регистра
//				<имя поля>		- Произвольный			- Значения измерений, ресурсов и реквизитов записи регистра
//  СписокРегистров		- Строка, Массив(Строка)	- Список обрабатываемых регистров (если пустой, то обрабатываются все регистры)
//  ИсключаяРегистры	- Строка, Массив(Строка)	- Список исключаемых регистров (если пустой, то обрабатываются все регистры)
//
// Возвращаемое значение:
//
Процедура ДвиженияДокументаИзСтруктуры(Объект, СтруктураДвижений, СписокРегистров = Неопределено, ИсключаяРегистры = Неопределено) Экспорт

	МетаОбъект = Объект.Метаданные();
	
	Для Каждого ТекДвижения Из МетаОбъект.Движения Цикл
		
		Если НЕ СтруктураДвижений.Свойство(ТекДвижения.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ИспользоватьСвойство(ТекДвижения.Имя, СписокРегистров, ИсключаяРегистры) Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеВида = ПолучитьОписаниеВидаОбъектов(ТекДвижения.ПолноеИмя());
		
		Если ВРег(ОписаниеВида.Имя) = ВРег("РегистрСведений") Тогда
			НаборЗаписейРегистраСведенийИзСтруктуры(Объект.Движения[ТекДвижения.Имя], СтруктураДвижений[ТекДвижения.Имя]);
		ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрНакопления") Тогда
			НаборЗаписейРегистраНакопленияИзСтруктуры(Объект.Движения[ТекДвижения.Имя], СтруктураДвижений[ТекДвижения.Имя]);
		ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрБухгалтерии") Тогда
			НаборЗаписейРегистраБухгалтерииИзСтруктуры(Объект.Движения[ТекДвижения.Имя], СтруктураДвижений[ТекДвижения.Имя]);
		ИначеЕсли ВРег(ОписаниеВида.Имя) = ВРег("РегистрРасчета") Тогда
			НаборЗаписейРегистраРасчетаИзСтруктуры(Объект.Движения[ТекДвижения.Имя], СтруктураДвижений[ТекДвижения.Имя]);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДвиженияДокументаИзСтруктуры()

#КонецОбласти // ПреобразованияОбъектовОбщие

#Область ПреобразованияОбъектовПоВидам

// Преобразует элемент справочника в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция СправочникВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ЭтоГруппа", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Родитель", Неопределено);
	Если МетаОбъект.Иерархический Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Родитель", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
			
	СтруктураОбъекта.Вставить("Владелец", Неопределено);
	Если МетаОбъект.Владельцы.Количество() > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Владелец", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Код", Неопределено);
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		Если МетаОбъект.Иерархический И МетаОбъект.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			Если (Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		Если МетаОбъект.Иерархический И МетаОбъект.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			Если (Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // СправочникВСтруктуру()

// Заполняет справочник из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура СправочникИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	
	Если МетаОбъект.Иерархический Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Родитель", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
			
	Если МетаОбъект.Владельцы.Количество() > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Владелец", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		Если МетаОбъект.Иерархический И МетаОбъект.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			Если (Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		Если МетаОбъект.Иерархический И МетаОбъект.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
			Если (Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // СправочникИзСтруктуры()

// Преобразует счет в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ПланСчетовВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Забалансовый", СписокСвойств, ИсключаяСвойства);
	
	Если ИспользоватьСвойство("Вид", СписокСвойств, ИсключаяСвойства) Тогда
		СтруктураОбъекта.Вставить("Вид", ?(Объект.Вид = ВидСчета.Активный, "Активный",
										   ?(Объект.Вид = ВидСчета.Пассивный, "Пассивный",
											 "АктивноПассивный")));
	КонецЕсли;
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Родитель", СписокСвойств, ИсключаяСвойства);
			
	СтруктураОбъекта.Вставить("Код", Неопределено);
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Порядок", Неопределено);
	Если МетаОбъект.ДлинаПорядка > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Порядок", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекПризнак Из МетаОбъект.ПризнакиУчета Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекПризнак.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	СтруктураОбъекта.Вставить("ВидыСубконто", Новый Массив());
	Если ИспользоватьСвойство("ВидыСубконто", СписокСвойств, ИсключаяСвойства)
	   И МетаОбъект.МаксКоличествоСубконто > 0 Тогда
		й = 1;
		Для Каждого ТекСтрока Из Объект.ВидыСубконто Цикл
			СтрокаСубконто = Новый Структура("НомерСтроки, ВидСубконто, ТолькоОбороты"
											, й
											, ЗначениеВСтруктуру(ТекСтрока.ВидСубконто)
											, ТекСтрока.ТолькоОбороты);
			
			Для Каждого ТекПризнак Из МетаОбъект.ПризнакиУчетаСубконто Цикл
				СтрокаСубконто.Вставить(ТекПризнак.Имя, ТекСтрока[ТекПризнак.Имя]);
			КонецЦикла;
			СтруктураОбъекта.ВидыСубконто.Добавить(СтрокаСубконто);
			й = й + 1;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПланСчетовВСтруктуру()

// Заполняет счет из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ПланСчетовИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Забалансовый", СписокСвойств, ИсключаяСвойства);
	Если ИспользоватьСвойство("Вид", СписокСвойств, ИсключаяСвойства) Тогда
		Объект.Вид = Вычислить("ВидСчета." + СтруктураОбъекта.Вид);
	КонецЕсли;
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Родитель", СписокСвойств, ИсключаяСвойства);
			
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаПорядка > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Порядок", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекПризнак Из МетаОбъект.ПризнакиУчета Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекПризнак.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Если СтруктураОбъекта.Свойство("ВидыСубконто")
	   И ИспользоватьСвойство("ВидыСубконто", СписокСвойств, ИсключаяСвойства)
	   И МетаОбъект.МаксКоличествоСубконто > 0 Тогда
		Объект.ВидыСубконто.Очистить();
		Для Каждого ТекСтрока Из СтруктураОбъекта.ВидыСубконто Цикл
			Если Объект.ВидыСубконто.Количество() >= МетаОбъект.МаксКоличествоСубконто Тогда
				Прервать;
			КонецЕсли;
			НоваяСтрока = Объект.ВидыСубконто.Добавить();
			НоваяСтрока.ВидСубконто		= ЗначениеИзСтруктуры(ТекСтрока.ВидСубконто);
			НоваяСтрока.ТолькоОбороты	= ТекСтрока.ТолькоОбороты;
			
			Для Каждого ТекПризнак Из МетаОбъект.ПризнакиУчетаСубконто Цикл
				Если НЕ ТекСтрока.Свойство(ТекПризнак.Имя) Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока[ТекПризнак.Имя] = ТекСтрока[ТекПризнак.Имя];
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // ПланСчетовИзСтруктуры()

// Преобразует элемент плана видов характеристик в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ПланВидовХарактеристикВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ЭтоГруппа", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Родитель", Неопределено);
	Если МетаОбъект.Иерархический Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Родитель", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
			
	СтруктураОбъекта.Вставить("Код", Неопределено);
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("ТипЗначения", ОписаниеТиповВСтруктуру(Объект.ТипЗначения));

	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		Если МетаОбъект.Иерархический Тогда
			Если (Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		Если МетаОбъект.Иерархический Тогда
			Если (Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПланВидовХарактеристикВСтруктуру()

// Заполняет элемент плана вида характеристик из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ПланВидовХарактеристикИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	
	Если МетаОбъект.Иерархический Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Родитель", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
			
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если СтруктураОбъекта.ТипЗначения.Типы.Количество() > 0 Тогда
		Объект.ТипЗначения = ОписаниеТиповИзСтруктуры(СтруктураОбъекта.ТипЗначения);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		Если МетаОбъект.Иерархический Тогда
			Если (Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		Если МетаОбъект.Иерархический Тогда
			Если (Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
			 ИЛИ (НЕ Объект.ЭтоГруппа И ТекТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы)Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // ПланВидовХарактеристикИзСтруктуры()

// Преобразует вид расчета в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ПланВидовРасчетаВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("БазовыеВидыРасчета", Новый Массив());
	Если НЕ МетаОбъект.ЗависимостьОтВидовРасчета = Метаданные.СвойстваОбъектов.ИспользованиеБазыПланаВидовРасчета.НеИспользовать
	   И ИспользоватьСвойство("БазовыеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Для Каждого ТекВР Из Объект.БазовыеВидыРасчета Цикл
			СтруктураОбъекта.БазовыеВидыРасчета.Добавить(ЗначениеВСтруктуру(ТекВР.ВидРасчета));
		КонецЦикла;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("ВедущиеВидыРасчета", Новый Массив());
	Если ИспользоватьСвойство("ВедущиеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Для Каждого ТекВР Из Объект.ВедущиеВидыРасчета Цикл
			СтруктураОбъекта.ВедущиеВидыРасчета.Добавить(ЗначениеВСтруктуру(ТекВР.ВидРасчета));
		КонецЦикла;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("ВытесняющиеВидыРасчета", Новый Массив());
	Если МетаОбъект.ИспользованиеПериодаДействия
	   И ИспользоватьСвойство("ВытесняющиеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Для Каждого ТекВР Из Объект.ВытесняющиеВидыРасчета Цикл
			СтруктураОбъекта.ВытесняющиеВидыРасчета.Добавить(ЗначениеВСтруктуру(ТекВР.ВидРасчета));
		КонецЦикла;
	КонецЕсли;
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПериодДействияБазовый", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Код", Неопределено);
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПланВидовРасчетаВСтруктуру()

// Заполняет вид расчета из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ПланВидовРасчетаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	
	Если НЕ МетаОбъект.ЗависимостьОтВидовРасчета = Метаданные.СвойстваОбъектов.ИспользованиеБазыПланаВидовРасчета.НеИспользовать
	   И ИспользоватьСвойство("БазовыеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Объект.БазовыеВидыРасчета.Очистить();
		Для Каждого ТекВР Из СтруктураОбъекта.БазовыеВидыРасчета Цикл
			ВремВР = ЗначениеИзСтруктуры(ТекВР);
			Если НЕ ЗначениеЗаполнено(ВремВР) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.БазовыеВидыРасчета.Добавить();
			НоваяСтрока.ВидРасчета = ВремВР;
		КонецЦикла;
	КонецЕсли;
	
	Если ИспользоватьСвойство("ВедущиеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Объект.ВедущиеВидыРасчета.Очистить();
		Для Каждого ТекВР Из СтруктураОбъекта.ВедущиеВидыРасчета Цикл
			ВремВР = ЗначениеИзСтруктуры(ТекВР);
			Если НЕ ЗначениеЗаполнено(ВремВР) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.ВедущиеВидыРасчета.Добавить();
			НоваяСтрока.ВидРасчета = ВремВР;
		КонецЦикла;
	КонецЕсли;
	
	Если МетаОбъект.ИспользованиеПериодаДействия
	   И ИспользоватьСвойство("ВытесняющиеВидыРасчета", СписокСвойств, ИсключаяСвойства) Тогда
		Объект.ВытесняющиеВидыРасчета.Очистить();
		Для Каждого ТекВР Из СтруктураОбъекта.ВытесняющиеВидыРасчета Цикл
			ВремВР = ЗначениеИзСтруктуры(ТекВР);
			Если НЕ ЗначениеЗаполнено(ВремВР) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.ВытесняющиеВидыРасчета.Добавить();
			НоваяСтрока.ВидРасчета = ВремВР;
		КонецЦикла;
	КонецЕсли;
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПериодДействияБазовый", СписокСвойств, ИсключаяСвойства);
	
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // ПланВидовРасчетаИзСтруктуры()

// Преобразует узел обмена в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ПланОбменаВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	Если ИспользоватьСвойство("ЭтотУзел", СписокСвойств, ИсключаяСвойства) Тогда
		СтруктураОбъекта.Вставить("ЭтотУзел", ПланыОбмена[МетаОбъект.Имя].ЭтотУзел() = Объект.Ссылка);
	КонецЕсли;
	//СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ЭтотУзел", СписокСвойств, ИсключаяСвойства); Не совместимо со старыми версиями
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "НомерОтправленного", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "НомерПринятого", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Код", Неопределено);
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПланОбменаВСтруктуру()

// Заполняет узел плана обмена из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ПланОбменаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	
	Если НЕ Объект.ЭтотУзел Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "НомерОтправленного", СписокСвойств, ИсключаяСвойства);
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "НомерПринятого", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаКода > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Код", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // ПланОбменаИзСтруктуры()

// Преобразует документ в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ДокументВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Проведен", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Дата", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Номер", Неопределено);
	//TODO: Тут еще нужно проверить использование нумератора
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Если ИспользоватьСвойство("Движения", СписокСвойств, ИсключаяСвойства) Тогда
		СтруктураОбъекта.Вставить("Движения", ДвиженияДокументаВСтруктуру(Объект, СписокСвойств, ИсключаяСвойства));
	КонецЕсли;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ДокументВСтруктуру()

// Заполняет документ из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ДокументИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Проведен", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Дата", СписокСвойств, ИсключаяСвойства);
	
	//TODO: Тут еще нужно проверить использование нумератора
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Если ИспользоватьСвойство("Движения", СписокСвойств, ИсключаяСвойства) И СтруктураОбъекта.Свойство("Движения") Тогда
		ДвиженияДокументаИзСтруктуры(Объект, СтруктураОбъекта.Движения, СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
КонецПроцедуры // ДокументИзСтруктуры()

// Преобразует бизнес-процесс в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция БизнесПроцессВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Дата", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Номер", Неопределено);
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Стартован", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Завершен", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ВедущаяЗадача", СписокСвойств, ИсключаяСвойства);
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // БизнесПроцессВСтруктуру()

// Заполняет бизнес-процесс из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура БизнесПроцессИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Дата", СписокСвойств, ИсключаяСвойства);
	
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Стартован", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Завершен", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ВедущаяЗадача", СписокСвойств, ИсключаяСвойства);
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // БизнесПроцессИзСтруктуры()

// Преобразует задачу в структуру
//
// Параметры:
//  Объект				- Объект, ЛюбаяСсылка		- Преобразуемый объект
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура				- результат преобразования
//		Ссылка			- Структура		- Описание ссылки на объект
//			Тип 						- Строка		- Текстовое описание типа объекта
//			Ид                          - Строка		- Уникальный идентификатор ссылки
//			ИдПолный					- Строка		- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево		- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка		- Имя предопределенного значения
//			Представление				- Строка		- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный	- Дополнительные поля добавленные при выгрузке
//		__ТЧ			- Массив		- Массив табличных частей объекта
//		<имя поля>						- Произвольный	- Значения реквизитов объекта
//
Функция ЗадачаВСтруктуру(Объект, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = Объект.Метаданные();
	
	СтруктураОбъекта.Вставить("Ссылка", ЗначениеВСтруктуру(Объект));
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Дата", СписокСвойств, ИсключаяСвойства);
	
	СтруктураОбъекта.Вставить("Номер", Неопределено);
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Наименование", Неопределено);
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "БизнесПроцесс", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "Выполнена", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, "ТочкаМаршрута", СписокСвойств, ИсключаяСвойства);
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаВСтруктуру(СтруктураОбъекта, Объект, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ЗадачаВСтруктуру()

// Заполняет задачу из структуры
//
// Параметры:
//  Объект				- Объект					- Заполняемый объект
//  СтруктураОбъекта	- Структура					- Данные для заполнения объекта
//		Ссылка					- Структура				- Описание ссылки на объект
//			Тип 						- Строка			- Текстовое описание типа объекта
//			Ид                          - Строка			- Уникальный идентификатор ссылки
//			ИдПолный					- Строка			- Преставление ссылки на объект, полученное функцией ЗначениеВСтрокуВнутр
//			Предопределенный			- Булево			- Истина - предопределенное значение; Ложь - в противном случае
//			ИмяПредопределенныхДанных	- Строка			- Имя предопределенного значения
//			Представление				- Строка			- Стандатрное строковое представление объекта
//			<имя поля>					- Произвольный		- Дополнительные поля добавленные при выгрузке
//		__ТЧ					- Массив				- Массив табличных частей объекта
//		<имя поля>				- Произвольный			- Значения реквизитов объекта
//  СписокСвойств		- Строка, Массив(Строка)	- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)	- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура ЗадачаИзСтруктуры(Объект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = Объект.Метаданные();
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ПометкаУдаления", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Дата", СписокСвойств, ИсключаяСвойства);
	
	Если МетаОбъект.ДлинаНомера > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Номер", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	Если МетаОбъект.ДлинаНаименования > 0 Тогда
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Наименование", СписокСвойств, ИсключаяСвойства);
	КонецЕсли;
	
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "БизнесПроцесс", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "Выполнена", СписокСвойств, ИсключаяСвойства);
	СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, "ТочкаМаршрута", СписокСвойств, ИсключаяСвойства);
	
	Для Каждого ТекРеквизит Из МетаОбъект.Реквизиты Цикл
		СвойствоОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекРеквизит.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
	Для Каждого ТекТЧ Из МетаОбъект.ТабличныеЧасти Цикл
		ТЧОбъектаИзСтруктуры(Объект, СтруктураОбъекта, ТекТЧ.Имя, СписокСвойств, ИсключаяСвойства);
	КонецЦикла;
	
КонецПроцедуры // ЗадачаИзСтруктуры()

// Преобразует набор записей регистра сведений в структуру
//
// Параметры:
//  НаборЗаписей        - РегистрСведенийНаборЗаписей    - набор записей регистра сведений для преобразования
//  СписокСвойств       - Строка, Массив(Строка)         - Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства    - Строка, Массив(Строка)         - Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура       - результат преобразования
//		Отбор          - Структура            - Описание отбора записей регистра
//			<имя поля>     - Произвольный          - Значение отбора
//		Записи         - Массив(Структура)    - Массив записей регистра
//			<имя поля>     - Произвольный          - Значения измерений, ресурсов и реквизитов записи регистра
//
Функция НаборЗаписейРегистраСведенийВСтруктуру(НаборЗаписей, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	СтруктураОбъекта.Вставить("Отбор", Новый Структура());
	
	Если МетаОбъект.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
		СтруктураОбъекта.Отбор.Вставить("Регистратор", ЗначениеВСтруктуру(НаборЗаписей.Отбор.Регистратор.Значение));
	Иначе
		Если НЕ МетаОбъект.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			СтруктураОбъекта.Отбор.Вставить("Период", НаборЗаписей.Отбор.Период.Значение);
		КонецЕсли;
		
		Для Каждого ТекЭлемент Из НаборЗаписей.Отбор Цикл
			Если НЕ ИспользоватьСвойство(ТекЭлемент.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураОбъекта.Отбор.Вставить(ТекЭлемент.Имя, ЗначениеВСтруктуру(ТекЭлемент.Значение));
		КонецЦикла;
	КонецЕсли;
	
	МассивСтрок = Новый Массив();
	
	//@skip-warning
	Для Каждого ТекСтрока Из НаборЗаписей Цикл
		
		СтруктураСтроки = Новый Структура();
		
		Если МетаОбъект.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
			СтруктураСтроки.Вставить("Регистратор", ЗначениеВСтруктуру(ТекСтрока.Регистратор));
			Если ИспользоватьСвойство("НомерСтроки", СписокСвойств, ИсключаяСвойства) Тогда
				СтруктураСтроки.Вставить("НомерСтроки", ТекСтрока.НомерСтроки);
			КонецЕсли;
			СтруктураСтроки.Вставить("Активность", ТекСтрока.Активность);
		КонецЕсли;
		
		Если НЕ МетаОбъект.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			СтруктураСтроки.Вставить("Период", ТекСтрока.Период);
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		МассивСтрок.Добавить(СтруктураСтроки);
		
	КонецЦикла;

	СтруктураОбъекта.Вставить("Записи", МассивСтрок);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НаборЗаписейРегистраСведенийВСтруктуру()

// Заполняет набор записей регистра сведений из структуры
//
// Параметры:
//  НаборЗаписей		- РегистрСведенийНаборЗаписей		- Набор записей регистра для заполнения
//  СтруктураОбъекта	- Структура							- Данные для заполнения объекта
//		Отбор				- Структура							- Описание отбора записей регистра
//			<имя поля>			- Произвольный						- Значение отбора
//		Записи				- Массив(Структура)					- Массив записей регистра
//			<имя поля>			- Произвольный						- Значения измерений, ресурсов и реквизитов записи регистра
//  СписокСвойств		- Строка, Массив(Строка)			- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)			- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура НаборЗаписейРегистраСведенийИзСтруктуры(НаборЗаписей, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	Если МетаОбъект.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
		НаборЗаписей.Отбор.Регистратор.Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор.Регистратор));
	Иначе
		Если НЕ МетаОбъект.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			НаборЗаписей.Отбор.Период.Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор.Период));
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ (СтруктураОбъекта.Отбор.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НаборЗаписей.Отбор[ТекПоле.Имя].Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор[ТекПоле.Имя]));
		КонецЦикла;
	КонецЕсли;
		
	НаборЗаписей.Очистить();
	
	Для Каждого ТекЗапись Из СтруктураОбъекта.Записи Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		Если МетаОбъект.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
			НоваяЗапись.Регистратор = ЗначениеИзСтруктуры(ТекЗапись.Регистратор);
			НоваяЗапись.Активность = ЗначениеИзСтруктуры(ТекЗапись.Активность);
		КонецЕсли;
		
		Если НЕ МетаОбъект.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			НоваяЗапись.Период = ЗначениеИзСтруктуры(ТекЗапись.Период);
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // НаборЗаписейРегистраСведенийИзСтруктуры()

// Преобразует набор записей регистра накопления в структуру
//
// Параметры:
//  НаборЗаписей        - РегистрНакопленияНаборЗаписей    - набор записей регистра накопления для преобразования
//  СписокСвойств       - Строка, Массив(Строка)           - Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства    - Строка, Массив(Строка)           - Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура       - результат преобразования
//		Отбор           - Структура            - Описание отбора записей регистра
//			<имя поля>      - Произвольный         - Значение отбора
//		Записи          - Массив(Структура)    - Массив записей регистра
//			<имя поля>      - Произвольный         - Значения измерений, ресурсов и реквизитов записи регистра
//
Функция НаборЗаписейРегистраНакопленияВСтруктуру(НаборЗаписей, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	СтруктураОбъекта.Вставить("Отбор", Новый Структура());
	
	СтруктураОбъекта.Отбор.Вставить("Регистратор", ЗначениеВСтруктуру(НаборЗаписей.Отбор.Регистратор.Значение));
	
	МассивСтрок = Новый Массив();
	
	//@skip-warning
	Для Каждого ТекСтрока Из НаборЗаписей Цикл
		
		СтруктураСтроки = Новый Структура();
		
		СтруктураСтроки.Вставить("Регистратор", ЗначениеВСтруктуру(ТекСтрока.Регистратор));
		
		Если ИспользоватьСвойство("НомерСтроки", СписокСвойств, ИсключаяСвойства) Тогда
			СтруктураСтроки.Вставить("НомерСтроки", ТекСтрока.НомерСтроки);
		КонецЕсли;
	
		СтруктураСтроки.Вставить("Активность", ТекСтрока.Активность);
		
		Если МетаОбъект.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			СтруктураСтроки.Вставить("ВидДвижения", ?(ТекСтрока.ВидДвижения = ВидДвиженияНакопления.Приход, "Приход", "Расход"));
		КонецЕсли;
		
		СтруктураСтроки.Вставить("Период", ТекСтрока.Период);
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		МассивСтрок.Добавить(СтруктураСтроки);
		
	КонецЦикла;

	СтруктураОбъекта.Вставить("Записи", МассивСтрок);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НаборЗаписейРегистраНакопленияВСтруктуру()

// Заполняет набор записей регистра накопления из структуры
//
// Параметры:
//  НаборЗаписей		- РегистрСведенийНаборЗаписей		- Набор записей регистра для заполнения
//  СтруктураОбъекта	- Структура							- Данные для заполнения объекта
//		Отбор				- Структура							- Описание отбора записей регистра
//			<имя поля>			- Произвольный						- Значение отбора
//		Записи				- Массив(Структура)					- Массив записей регистра
//			<имя поля>			- Произвольный						- Значения измерений, ресурсов и реквизитов записи регистра
//  СписокСвойств		- Строка, Массив(Строка)			- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)			- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура НаборЗаписейРегистраНакопленияИзСтруктуры(НаборЗаписей, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	НаборЗаписей.Отбор.Регистратор.Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор.Регистратор));
	
	НаборЗаписей.Очистить();
	
	Для Каждого ТекЗапись Из СтруктураОбъекта.Записи Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Регистратор = ЗначениеИзСтруктуры(ТекЗапись.Регистратор);
		
		НоваяЗапись.Активность = ЗначениеИзСтруктуры(ТекЗапись.Активность);
		
		Если МетаОбъект.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			НоваяЗапись.ВидДвижения = Вычислить("ВидДвиженияНакопления." + ТекЗапись.ВидДвижения);
		КонецЕсли;
		
		НоваяЗапись.Период = ЗначениеИзСтруктуры(ТекЗапись.Период);
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // НаборЗаписейРегистраНакопленияИзСтруктуры()

// Преобразует набор записей регистра бухгалтерии в структуру
//
// Параметры:
//  НаборЗаписей        - РегистрБухгалтерииНаборЗаписей    - набор записей регистра бухгалтерии для преобразования
//  СписокСвойств       - Строка, Массив(Строка)            - Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства    - Строка, Массив(Строка)            - Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура       - результат преобразования
//		Отбор           - Структура            - Описание отбора записей регистра
//			<имя поля>      - Произвольный         - Значение отбора
//		Записи          - Массив(Структура)    - Массив записей регистра
//			<имя поля>      - Произвольный         - Значения измерений, ресурсов и реквизитов записи регистра
//
Функция НаборЗаписейРегистраБухгалтерииВСтруктуру(НаборЗаписей, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	СтруктураОбъекта.Вставить("Отбор", Новый Структура());
	
	СтруктураОбъекта.Отбор.Вставить("Регистратор", ЗначениеВСтруктуру(НаборЗаписей.Отбор.Регистратор.Значение));
	
	МассивСтрок = Новый Массив();
	
	//@skip-warning
	Для Каждого ТекСтрока Из НаборЗаписей Цикл
		
		СтруктураСтроки = Новый Структура();
		
		СтруктураСтроки.Вставить("Регистратор", ЗначениеВСтруктуру(ТекСтрока.Регистратор));
		
		Если ИспользоватьСвойство("НомерСтроки", СписокСвойств, ИсключаяСвойства) Тогда
			СтруктураСтроки.Вставить("НомерСтроки", ТекСтрока.НомерСтроки);
		КонецЕсли;
	
		СтруктураСтроки.Вставить("Активность", ТекСтрока.Активность);
		
		СтруктураСтроки.Вставить("Период", ТекСтрока.Период);
		
		Если МетаОбъект.ДлинаУточненияПериода > 0
		   И ИспользоватьСвойство("УточнениеПериода", СписокСвойств, ИсключаяСвойства) Тогда
			СтруктураСтроки.Вставить("УточнениеПериода", ТекСтрока.УточнениеПериода);
		КонецЕсли;
		
		Если МетаОбъект.Корреспонденция Тогда
			СтруктураСтроки.Вставить("СчетДт", ЗначениеВСтруктуру(ТекСтрока.СчетДт));
			СтруктураСтроки.Вставить("СчетКт", ЗначениеВСтруктуру(ТекСтрока.СчетКт));
			
			СтруктураСтроки.Вставить("СубконтоДт", ЗначенияСубконтоЗаписиВСтруктуру(ТекСтрока, "Дт", СписокСвойств, ИсключаяСвойства));
			СтруктураСтроки.Вставить("СубконтоКт", ЗначенияСубконтоЗаписиВСтруктуру(ТекСтрока, "Кт", СписокСвойств, ИсключаяСвойства));
		Иначе
			СтруктураСтроки.Вставить("ВидДвижения", ?(ТекСтрока.ВидДвижения = ВидДвиженияБухгалтерии.Дебет, "Дебет", "Кредит"));
			
			СтруктураСтроки.Вставить("Счет", ЗначениеВСтруктуру(ТекСтрока.Счет));
			
			СтруктураСтроки.Вставить("Субконто", ЗначенияСубконтоЗаписиВСтруктуру(ТекСтрока, "", СписокСвойств, ИсключаяСвойства));
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			Если МетаОбъект.Корреспонденция И НЕ ТекПоле.Балансовый Тогда
				СтруктураСтроки.Вставить(ТекПоле.Имя + "Дт", ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя + "Дт"]));
				СтруктураСтроки.Вставить(ТекПоле.Имя + "Кт", ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя + "Кт"]));
			Иначе
				СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			Если МетаОбъект.Корреспонденция И НЕ ТекПоле.Балансовый Тогда
				СтруктураСтроки.Вставить(ТекПоле.Имя + "Дт", ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя + "Дт"]));
				СтруктураСтроки.Вставить(ТекПоле.Имя + "Кт", ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя + "Кт"]));
			Иначе
				СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		МассивСтрок.Добавить(СтруктураСтроки);
		
	КонецЦикла;

	СтруктураОбъекта.Вставить("Записи", МассивСтрок);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НаборЗаписейРегистраБухгалтерииВСтруктуру()

// Заполняет набор записей регистра бухгалтерии из структуры
//
// Параметры:
//  НаборЗаписей		- РегистрСведенийНаборЗаписей		- Набор записей регистра для заполнения
//  СтруктураОбъекта	- Структура							- Данные для заполнения объекта
//		Отбор				- Структура							- Описание отбора записей регистра
//			<имя поля>			- Произвольный						- Значение отбора
//		Записи				- Массив(Структура)					- Массив записей регистра
//			<имя поля>			- Произвольный						- Значения измерений, ресурсов и реквизитов записи регистра
//  СписокСвойств		- Строка, Массив(Строка)			- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)			- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура НаборЗаписейРегистраБухгалтерииИзСтруктуры(НаборЗаписей, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	НаборЗаписей.Отбор.Регистратор.Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор.Регистратор));
	
	НаборЗаписей.Очистить();
	
	Для Каждого ТекЗапись Из СтруктураОбъекта.Записи Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Регистратор = ЗначениеИзСтруктуры(ТекЗапись.Регистратор);
		
		НоваяЗапись.Активность = ЗначениеИзСтруктуры(ТекЗапись.Активность);
		НоваяЗапись.Период = ЗначениеИзСтруктуры(ТекЗапись.Период);
		
		Если МетаОбъект.ДлинаУточненияПериода > 0
		   И ИспользоватьСвойство("УточнениеПериода", СписокСвойств, ИсключаяСвойства) Тогда
			НоваяЗапись.УточнениеПериода = ЗначениеИзСтруктуры(ТекЗапись.УточнениеПериода);
		КонецЕсли;
		
		Если МетаОбъект.Корреспонденция Тогда
			НоваяЗапись.СчетДт = ЗначениеИзСтруктуры(ТекЗапись.СчетДт);
			НоваяЗапись.СчетКт = ЗначениеИзСтруктуры(ТекЗапись.СчетКт);
			
			ЗначенияСубконтоЗаписиИзСтруктуры(НоваяЗапись, ТекЗапись, "Дт", СписокСвойств, ИсключаяСвойства);
			ЗначенияСубконтоЗаписиИзСтруктуры(НоваяЗапись, ТекЗапись, "Кт", СписокСвойств, ИсключаяСвойства);
		Иначе
			НоваяЗапись.ВидДвижения = Вычислить("ВидДвиженияБухгалтерии." + ТекЗапись.ВидДвижения);
			
			НоваяЗапись.Счет = ЗначениеИзСтруктуры(ТекЗапись.Счет);
			
			ЗначенияСубконтоЗаписиИзСтруктуры(НоваяЗапись, ТекЗапись, "", СписокСвойств, ИсключаяСвойства);
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			Если МетаОбъект.Корреспонденция И НЕ ТекПоле.Балансовый Тогда
				ЗаполнитьИзмерениеРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "Дт");
				ЗаполнитьИзмерениеРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "Кт");
			Иначе
				ЗаполнитьИзмерениеРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "");
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			Если МетаОбъект.Корреспонденция И НЕ ТекПоле.Балансовый Тогда
				ЗаполнитьРесурсРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "Дт");
				ЗаполнитьРесурсРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "Кт");
			Иначе
				ЗаполнитьРесурсРегистраБухгалтерии(НоваяЗапись, ТекЗапись, ТекПоле, "");
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // НаборЗаписейРегистраБухгалтерииИзСтруктуры()

// Преобразует набор записей регистра расчета в структуру
//
// Параметры:
//  НаборЗаписей        - РегистрРасчетаНаборЗаписей    - набор записей регистра расчета для преобразования
//  СписокСвойств       - Строка, Массив(Строка)        - Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства    - Строка, Массив(Строка)        - Список исключаемых свойств (если пустой, то допустимы все свойства)
//
// Возвращаемое значение:
//   Структура       - результат преобразования
//		Отбор           - Структура            - Описание отбора записей регистра
//			<имя поля>      - Произвольный         - Значение отбора
//		Записи          - Массив(Структура)    - Массив записей регистра
//			<имя поля>      - Произвольный         - Значения измерений, ресурсов и реквизитов записи регистра
//
Функция НаборЗаписейРегистраРасчетаВСтруктуру(НаборЗаписей, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	СтруктураОбъекта.Вставить("Отбор", Новый Структура());
	
	СтруктураОбъекта.Отбор.Вставить("Регистратор", ЗначениеВСтруктуру(НаборЗаписей.Отбор.Регистратор.Значение));
	
	Для Каждого ТекЭлемент Из НаборЗаписей.Отбор Цикл
		Если НЕ ИспользоватьСвойство(ТекЭлемент.Имя, СписокСвойств, ИсключаяСвойства) Тогда
			Продолжить;
		КонецЕсли;
		СтруктураОбъекта.Отбор.Вставить(ТекЭлемент.Имя, ЗначениеВСтруктуру(ТекЭлемент.Значение));
	КонецЦикла;
	
	МассивСтрок = Новый Массив();
	
	//@skip-warning
	Для Каждого ТекСтрока Из НаборЗаписей Цикл
		
		СтруктураСтроки = Новый Структура();
		
		СтруктураСтроки.Вставить("Регистратор", ЗначениеВСтруктуру(ТекСтрока.Регистратор));
		
		Если ИспользоватьСвойство("НомерСтроки", СписокСвойств, ИсключаяСвойства) Тогда
			СтруктураСтроки.Вставить("НомерСтроки", ТекСтрока.НомерСтроки);
		КонецЕсли;
	
		СтруктураСтроки.Вставить("Активность"		, ТекСтрока.Активность);
		
		СтруктураСтроки.Вставить("ВидРасчета"		, ЗначениеВСтруктуру(ТекСтрока.ВидРасчета));
		
		СтруктураСтроки.Вставить("Сторно"			, ТекСтрока.Сторно);
		
		СтруктураСтроки.Вставить("ПериодРегистрации", ТекСтрока.ПериодРегистрации);
		
		Если МетаОбъект.БазовыйПериод Тогда
			СтруктураСтроки.Вставить("БазовыйПериодНачало"	, ТекСтрока.БазовыйПериодНачало);
			СтруктураСтроки.Вставить("БазовыйПериодКонец"	, ТекСтрока.БазовыйПериодКонец);
		КонецЕсли;
		
		Если МетаОбъект.ПериодДействия Тогда
			СтруктураСтроки.Вставить("ПериодДействия"		, ТекСтрока.ПериодДействия);
			СтруктураСтроки.Вставить("ПериодДействияНачало"	, ТекСтрока.ПериодДействияНачало);
			СтруктураСтроки.Вставить("ПериодДействияКонец"	, ТекСтрока.ПериодДействияКонец);
			
			МассивПериодов = Новый Массив();
			
			Для Каждого ТекПериод Из ТекСтрока.ФактическийПериодДействия Цикл
				МассивПериодов.Добавить(Новый Структура("ДатаНачала, ДатаОкончания", ТекПериод.ДатаНачала, ТекПериод.ДатаОкончания));
			КонецЦикла;
			СтруктураСтроки.Вставить("ФактическийПериодДействия", МассивПериодов);
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки.Вставить(ТекПоле.Имя, ЗначениеВСтруктуру(ТекСтрока[ТекПоле.Имя]));
		КонецЦикла;
		
		МассивСтрок.Добавить(СтруктураСтроки);
		
	КонецЦикла;

	СтруктураОбъекта.Вставить("Записи", МассивСтрок);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НаборЗаписейРегистраРасчетаВСтруктуру()

// Заполняет набор записей регистра расчета из структуры
//
// Параметры:
//  НаборЗаписей		- РегистрСведенийНаборЗаписей		- Набор записей регистра для заполнения
//  СтруктураОбъекта	- Структура							- Данные для заполнения объекта
//		Отбор				- Структура							- Описание отбора записей регистра
//			<имя поля>			- Произвольный						- Значение отбора
//		Записи				- Массив(Структура)					- Массив записей регистра
//			<имя поля>			- Произвольный						- Значения измерений, ресурсов и реквизитов записи регистра
//  СписокСвойств		- Строка, Массив(Строка)			- Список допустимых свойств (если пустой, то допустимы все свойства)
//  ИсключаяСвойства	- Строка, Массив(Строка)			- Список исключаемых свойств (если пустой, то допустимы все свойства)
//
Процедура НаборЗаписейРегистраРасчетаИзСтруктуры(НаборЗаписей, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено) Экспорт
	
	МетаОбъект = НаборЗаписей.Метаданные();
	
	НаборЗаписей.Отбор.Регистратор.Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор.Регистратор));
	
	Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
		Если НЕ (СтруктураОбъекта.Отбор.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей.Отбор[ТекПоле.Имя].Установить(ЗначениеИзСтруктуры(СтруктураОбъекта.Отбор[ТекПоле.Имя]));
	КонецЦикла;
	
	НаборЗаписей.Очистить();
	
	Для Каждого ТекЗапись Из СтруктураОбъекта.Записи Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Регистратор = ЗначениеИзСтруктуры(ТекЗапись.Регистратор);
		
		НоваяЗапись.Активность 			= ЗначениеИзСтруктуры(ТекЗапись.Активность);
		
		НоваяЗапись.ВидРасчета			= ЗначениеИзСтруктуры(ТекЗапись.ВидРасчета);
		
		НоваяЗапись.Сторно				= ЗначениеИзСтруктуры(ТекЗапись.Сторно);
		
		НоваяЗапись.ПериодРегистрации	= ЗначениеИзСтруктуры(ТекЗапись.ПериодРегистрации);
		
		Если МетаОбъект.БазовыйПериод Тогда
			НоваяЗапись.БазовыйПериодНачало	= ЗначениеИзСтруктуры(ТекЗапись.БазовыйПериодНачало);
			НоваяЗапись.БазовыйПериодКонец	= ЗначениеИзСтруктуры(ТекЗапись.БазовыйПериодКонец);
		КонецЕсли;
		
		Если МетаОбъект.ПериодДействия Тогда
			НоваяЗапись.ПериодДействияНачало	= ЗначениеИзСтруктуры(ТекЗапись.ПериодДействияНачало);
			НоваяЗапись.ПериодДействияКонец		= ЗначениеИзСтруктуры(ТекЗапись.ПериодДействияКонец);
		КонецЕсли;
		
		Для Каждого ТекПоле Из МетаОбъект.Измерения Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Ресурсы Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
		
		Для Каждого ТекПоле Из МетаОбъект.Реквизиты Цикл
			Если НЕ (ТекЗапись.Свойство(ТекПоле.Имя) И ИспользоватьСвойство(ТекПоле.Имя, СписокСвойств, ИсключаяСвойства)) Тогда
				Продолжить;
			КонецЕсли;
			НоваяЗапись[ТекПоле.Имя] = ЗначениеИзСтруктуры(ТекЗапись[ТекПоле.Имя]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры // НаборЗаписейРегистраРасчетаИзСтруктуры()

#КонецОбласти // ПреобразованияОбъектовПоВидам

#Область ПреобразованияСКДИнтерфейсныеПроцедуры

// Функция - заполняет и возвращает структуру описания объекта СКД
// НастройкиКомпоновкиДанных
// 
// Параметры:
// 	Объект        - НастройкиКомпоновкиДанных - объект СКД для обработки
//
// Возвращаемое значение:
// 	Структура     - структура описания объекта
//
Функция НастройкиСКДВСтруктуру(Объект) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Выбор"                 , ВыбранныеПоляСКДВСтруктуру(Объект.Выбор));
	СтруктураОбъекта.Вставить("ДополнительныеСвойства", ЗначениеВСтруктуру(Объект.ДополнительныеСвойства));
	СтруктураОбъекта.Вставить("Отбор",
	                          ОтборСКДВСтруктуру(Объект.Отбор));
	СтруктураОбъекта.Вставить("ПараметрыВывода",
	                          ЗначенияПараметровВыводаСКДВСтруктуру(Объект.ПараметрыВывода));
	СтруктураОбъекта.Вставить("ПараметрыДанных",
	                          ЗначенияПараметровДанныхСКДВСтруктуру(Объект.ПараметрыДанных));
	СтруктураОбъекта.Вставить("ПользовательскиеПоля",
	                          ПользовательскиеПоляСКДВСтруктуру(Объект.ПользовательскиеПоля));
	СтруктураОбъекта.Вставить("Порядок",
	                          ПорядокСКДВСтруктуру(Объект.Порядок));

	СтруктураОбъекта.Вставить("Родитель", Неопределено); //TODO: Разбраться с родителем

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Структура Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Структура", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("УсловноеОформление",
	                          УсловноеОформлениеСКДВСтруктуру(Объект.УсловноеОформление));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НастройкиСКДВСтруктуру()

// Процедура - заполняет объект СКД НастройкиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НастройкиКомпоновкиДанных     - заполняемый объект
// 	СтруктураОбъекта   - Структура                     - данные для заполнения объекта
//
Процедура НастройкиСКДИзСтруктуры(Объект, СтруктураОбъекта) Экспорт
	
	Если НЕ ТипЗнч(Объект) = Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выбор") Тогда
		ВыбранныеПоляСКДИзСтруктуры(Объект.Выбор, СтруктураОбъекта.Выбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ДополнительныеСвойства") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ДополнительныеСвойства Цикл
			Объект.ДополнительныеСвойства.Вставить(ТекЭлемент.Ключ, ЗначениеИзСтруктуры(ТекЭлемент.Значение));
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Отбор") Тогда
		ОтборСКДИзСтруктуры(Объект.Отбор, СтруктураОбъекта.Отбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыВывода") Тогда
		ЗначенияПараметровВыводаСКДИзСтруктуры(Объект.ПараметрыВывода, СтруктураОбъекта.ПараметрыВывода);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыДанных") Тогда
		ЗначенияПараметровДанныхСКДИзСтруктуры(Объект.ПараметрыДанных, СтруктураОбъекта.ПараметрыДанных);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПользовательскиеПоля") Тогда
		ПользовательскиеПоляСКДИзСтруктуры(Объект.ПользовательскиеПоля, СтруктураОбъекта.ПользовательскиеПоля);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Порядок") Тогда
		ПорядокСКДИзСтруктуры(Объект.Порядок, СтруктураОбъекта.Порядок);
	КонецЕсли;

	//TODO: Разобраться с родителем
	
	Если СтруктураОбъекта.Свойство("Структура") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Структура Цикл
			НовыйЭлемент = Объект.Структура.Добавить(Тип(ТекЭлемент.Тип));
			ЭлементСтруктурыНастроекСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("УсловноеОформление") Тогда
		УсловноеОформлениеСКДИзСтруктуры(Объект.УсловноеОформление, СтруктураОбъекта.УсловноеОформление);
	КонецЕсли;

КонецПроцедуры // НастройкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// СхемаКомпоновкиДанных
// 
// Параметры:
// 	Объект        - СхемаКомпоновкиДанных   - объект СКД для обработки
//
// Возвращаемое значение:
// 	Структура     - структура описания объекта
//
Функция СКДВСтруктуру(Объект) Экспорт
	
	СтруктураОбъекта = Новый Структура();
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ИсточникиДанных Цикл
		МассивЭлементов.Добавить(ИсточникДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ИсточникиДанных", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.НаборыДанных Цикл
		МассивЭлементов.Добавить(НаборДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("НаборыДанных", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.СвязиНаборовДанных Цикл
		МассивЭлементов.Добавить(СвязьНаборовДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("СвязиНаборовДанных", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВычисляемыеПоля Цикл
		МассивЭлементов.Добавить(ВычисляемоеПолеСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВычисляемыеПоля", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Параметры Цикл
		МассивЭлементов.Добавить(ПараметрСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Параметры", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ПоляИтога Цикл
		МассивЭлементов.Добавить(ПолеИтогаСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ПоляИтога", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Макеты Цикл
		МассивЭлементов.Добавить(МакетСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Макеты", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.МакетыГруппировок Цикл
		МассивЭлементов.Добавить(МакетГруппировкиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("МакетыГруппировок", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.МакетыЗаголовковГруппировок Цикл
		МассивЭлементов.Добавить(МакетГруппировкиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("МакетыЗаголовковГруппировок", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.МакетыПолей Цикл
		МассивЭлементов.Добавить(МакетПоляСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("МакетыПолей", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.МакетыПолейИтога Цикл
		МассивЭлементов.Добавить(МакетПолейИтогаСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("МакетыПолейИтога", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВложенныеСхемыКомпоновкиДанных Цикл
		МассивЭлементов.Добавить(ВложеннаяСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВложенныеСхемыКомпоновкиДанных", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВариантыНастроек Цикл
		Если НЕ СтруктураОбъекта.Свойство("НастройкиПоУмолчанию") Тогда
			СтруктураОбъекта.Вставить("НастройкиПоУмолчанию", ТекЭлемент.Имя);
		КонецЕсли;
		МассивЭлементов.Добавить(ВариантНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВариантыНастроек", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // СКДВСтруктуру()

// Процедура - заполняет объект СКД СхемаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - СхемаКомпоновкиДанных     - заполняемый объект
// 	СтруктураОбъекта   - Структура                 - данные для заполнения объекта
//
Процедура СКДИзСтруктуры(Объект, СтруктураОбъекта) Экспорт
	
	Если НЕ ТипЗнч(Объект) = Тип("СхемаКомпоновкиДанных") Тогда
		Объект = Новый СхемаКомпоновкиДанных();
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИсточникиДанных") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ИсточникиДанных Цикл
			НовыйЭлемент = Объект.ИсточникиДанных.Добавить();
			ИсточникДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("НаборыДанных") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.НаборыДанных Цикл
			НовыйЭлемент = Объект.НаборыДанных.Добавить(Тип(ТекЭлемент.Тип));
			НаборДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("СвязиНаборовДанных") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.СвязиНаборовДанных Цикл
			НовыйЭлемент = Объект.СвязиНаборовДанных.Добавить();
			СвязьНаборовДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВычисляемыеПоля") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВычисляемыеПоля Цикл
			НовыйЭлемент = Объект.ВычисляемыеПоля.Добавить();
			ВычисляемоеПолеСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Параметры") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Параметры Цикл
			НовыйЭлемент = Объект.Параметры.Добавить();
			ПараметрСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ПоляИтога") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ПоляИтога Цикл
			НовыйЭлемент = Объект.ПоляИтога.Добавить();
			ПолеИтогаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Макеты") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Макеты Цикл
			НовыйЭлемент = Объект.Макеты.Добавить();
			МакетСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("МакетыГруппировок") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.МакетыГруппировок Цикл
			НовыйЭлемент = Объект.МакетыГруппировок.Добавить();
			МакетГруппировкиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("МакетыЗаголовковГруппировок") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.МакетыЗаголовковГруппировок Цикл
			НовыйЭлемент = Объект.МакетыЗаголовковГруппировок.Добавить();
			МакетГруппировкиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("МакетыПолей") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.МакетыПолей Цикл
			НовыйЭлемент = Объект.МакетыПолей.Добавить();
			МакетПоляСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("МакетыПолейИтога") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.МакетыПолейИтога Цикл
			НовыйЭлемент = Объект.МакетыПолейИтога.Добавить();
			МакетПолейИтогаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВложенныеСхемыКомпоновкиДанных") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВложенныеСхемыКомпоновкиДанных Цикл
			НовыйЭлемент = Объект.ВложенныеСхемыКомпоновкиДанных.Добавить();
			ВложеннаяСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВариантыНастроек") Тогда
		ЕстьНастройкиПоУмолчанию = Ложь;
		Если СтруктураОбъекта.Свойство("НастройкиПоУмолчанию") И Объект.ВариантыНастроек.Количество() > 0 Тогда
			Для Каждого ТекЭлемент Из СтруктураОбъекта.ВариантыНастроек Цикл
				Если ТекЭлемент.Имя = СтруктураОбъекта.НастройкиПоУмолчанию Тогда
					ЕстьНастройкиПоУмолчанию = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЕстьНастройкиПоУмолчанию Тогда
				ВариантНастроекСКДИзСтруктуры(Объект.ВариантыНастроек[0], ТекЭлемент);
			Иначе
				Объект.ВариантыНастроек.Очистить();
			КонецЕсли;
		Иначе
			Объект.ВариантыНастроек.Очистить();
		КонецЕсли;
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВариантыНастроек Цикл
			Если ЕстьНастройкиПоУмолчанию И ТекЭлемент.Имя = СтруктураОбъекта.НастройкиПоУмолчанию Тогда
				Продолжить;
			КонецЕсли;
			НовыйЭлемент = Объект.ВариантыНастроек.Добавить();
			ВариантНастроекСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // СКДИзСтруктуры()

#КонецОбласти // ПреобразованияСКДИнтерфейсныеПроцедуры

#Область ПреобразованияСКДПроцедурыПреобразованияОбъектов

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВариантНастроекКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВариантНастроекКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВариантНастроекСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантНастроекКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Имя"          , Объект.Имя);
	СтруктураОбъекта.Вставить("Настройки"    , НастройкиСКДВСтруктуру(Объект.Настройки));
	СтруктураОбъекта.Вставить("Представление", Объект.Представление);

	Возврат СтруктураОбъекта;
	
КонецФункции // ВариантНастроекСКДВСтруктуру()

// Процедура - заполняет объект СКД ВариантНастроекКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВариантНастроекКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ВариантНастроекСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантНастроекКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Настройки") Тогда
		НастройкиСКДИзСтруктуры(Объект.Настройки, СтруктураОбъекта.Настройки);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Представление") Тогда
		Объект.Представление = СтруктураОбъекта.Представление;
	КонецЕсли;
	
КонецПроцедуры // ВариантНастроекСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВложеннаяСхемаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВложеннаяСхемаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВложеннаяСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВложеннаяСхемаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Заголовок", Объект.Заголовок);
	СтруктураОбъекта.Вставить("Имя"      , Объект.Имя);
	СтруктураОбъекта.Вставить("Настройки", НастройкиСКДВСтруктуру(Объект.Настройки));
	СтруктураОбъекта.Вставить("Схема"    , СКДВСтруктуру(Объект.Схема));

	Возврат СтруктураОбъекта;
	
КонецФункции // ВложеннаяСКДВСтруктуру()

// Процедура - заполняет объект СКД ВложеннаяСхемаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВложеннаяСхемаКомпоновкиДанных  - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ВложеннаяСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВложеннаяСхемаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Настройки") Тогда
		НастройкиСКДИзСтруктуры(Объект.Настройки, СтруктураОбъекта.Настройки);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Схема") Тогда
		СКДИзСтруктуры(Объект.Схема, СтруктураОбъекта.Схема);
	КонецЕсли;
	
КонецПроцедуры // ВложеннаяСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВычисляемоеПолеСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВычисляемоеПолеСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВычисляемоеПолеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВычисляемоеПолеСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Выражение"               , Объект.Выражение);
	СтруктураОбъекта.Вставить("ВыражениеПредставления"  , Объект.ВыражениеПредставления);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВыраженияУпорядочивания Цикл
		МассивЭлементов.Добавить(ВыражениеУпорядочиванияСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВыраженияУпорядочивания" , МассивЭлементов);
	
	СтруктураОбъекта.Вставить("Заголовок"               , Объект.Заголовок);
	СтруктураОбъекта.Вставить("ОграничениеИспользования",
	                          ОграничениеИспользованияПоляСКДВСтруктуру(Объект.ОграничениеИспользования));
	СтруктураОбъекта.Вставить("Оформление"              , ОформлениеСКДВСтруктуру(Объект.Оформление));
	СтруктураОбъекта.Вставить("ПараметрыРедактирования" ,
	                          ПараметрыРедактированияСКДВСтруктуру(Объект.ПараметрыРедактирования));
	СтруктураОбъекта.Вставить("ПутьКДанным"             , Объект.ПутьКДанным);
	СтруктураОбъекта.Вставить("ТипЗначения"             , ЗначениеВСтруктуру(Объект.ТипЗначения));

	Возврат СтруктураОбъекта;
	
КонецФункции // ВычисляемоеПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД ВычисляемоеПолеСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВычисляемоеПолеСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура ВычисляемоеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВычисляемоеПолеСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ВыражениеПредставления") Тогда
		Объект.ВыражениеПредставления = СтруктураОбъекта.ВыражениеПредставления;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ВыраженияУпорядочивания") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВыраженияУпорядочивания Цикл
			НовыйЭлемент = Объект.ВыраженияУпорядочивания.Добавить();
			ВыражениеУпорядочиванияСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ОграничениеИспользования") Тогда
		ОграничениеИспользованияПоляСКДИзСтруктуры(Объект.ОграничениеИспользования,
		                                           СтруктураОбъекта.ОграничениеИспользования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Оформление") Тогда
		ОформлениеСКДИзСтруктуры(Объект.Оформление, СтруктураОбъекта.Оформление);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыРедактирования") Тогда
		ПараметрыРедактированияСКДИзСтруктуры(Объект.ПараметрыРедактирования,
		                                      СтруктураОбъекта.ПараметрыРедактирования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПутьКДанным") Тогда
		Объект.ПутьКДанным = СтруктураОбъекта.ПутьКДанным;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипЗначения") Тогда
		Объект.ТипЗначения = ЗначениеИзСтруктуры(СтруктураОбъекта.ТипЗначения);
	КонецЕсли;

КонецПроцедуры // ВычисляемоеПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ИсточникДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ИсточникДанныхСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ИсточникДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ИсточникДанныхСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Имя"               , Объект.Имя);
	СтруктураОбъекта.Вставить("СтрокаСоединения"  , Объект.СтрокаСоединения);
	СтруктураОбъекта.Вставить("ТипИсточникаДанных", Объект.ТипИсточникаДанных);

	Возврат СтруктураОбъекта;
	
КонецФункции // ИсточникДанныхСКДВСтруктуру()

// Процедура - заполняет объект СКД ИсточникДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ИсточникДанныхСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                           - данные для заполнения объекта
//
Процедура ИсточникДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ИсточникДанныхСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("СтрокаСоединения") Тогда
		Объект.СтрокаСоединения = СтруктураОбъекта.СтрокаСоединения;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипИсточникаДанных") Тогда
		Объект.ТипИсточникаДанных = СтруктураОбъекта.ТипИсточникаДанных;
	КонецЕсли;
	
КонецПроцедуры // ИсточникДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОписаниеМакетаСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОписаниеМакетаСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция МакетСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОписаниеМакетаСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Имя"    , Объект.Имя);

	Если ТипЗнч(Объект.Макет) = Тип("МакетОбластиКомпоновкиДанных") Тогда
		МассивЭлементов = Новый Массив();
		Для Каждого ТекЭлемент Из Объект.Макет Цикл
			МассивЭлементов.Добавить(СтрокаТаблицыОбластиСКДВСтруктуру(ТекЭлемент));
		КонецЦикла;
		СтруктураОбъекта.Вставить("Макет" , МассивЭлементов);
	КонецЕсли;
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Параметры Цикл
		МассивЭлементов.Добавить(ПараметрОбластиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Параметры" , МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // МакетСКДВСтруктуру()

// Процедура - заполняет объект СКД ОписаниеМакетаСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОписаниеМакетаСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                           - данные для заполнения объекта
//
Процедура МакетСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОписаниеМакетаСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Макет") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Макет Цикл
			Если НЕ ТипЗнч(Объект.Макет) = Тип("МакетОбластиКомпоновкиДанных") Тогда
				Объект.Макет = Новый МакетОбластиКомпоновкиДанных();
			КонецЕсли;
			НовыйЭлемент = Объект.Макет.Добавить(Тип(ТекЭлемент.Тип));
			СтрокаТаблицыОбластиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Параметры") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Параметры Цикл
			НовыйЭлемент = Объект.Параметры.Добавить(Тип(ТекЭлемент.Тип));
			ПараметрОбластиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // МакетСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// МакетГруппировкиСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - МакетГруппировкиСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция МакетГруппировкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетГруппировкиСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИмяГруппировки", Объект.ИмяГруппировки);
	СтруктураОбъекта.Вставить("Макет"         , Объект.Макет);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ПоляГруппировки Цикл
		МассивЭлементов.Добавить(СокрЛП(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ПоляГруппировки", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("ТипМакета" , ТипМакетаОбластиКомпоновкиДанныхВСтроку(Объект.ТипМакета));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // МакетГруппировкиСКДВСтруктуру()

// Процедура - заполняет объект СКД МакетГруппировкиСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - МакетГруппировкиСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура МакетГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетГруппировкиСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИмяГруппировки") Тогда
		Объект.ИмяГруппировки = СтруктураОбъекта.ИмяГруппировки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Макет") Тогда
		Объект.Макет = СтруктураОбъекта.Макет;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПоляГруппировки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ПоляГруппировки Цикл
			Объект.ПоляГруппировки.Добавить(Новый ПолеКомпоновкиДанных(ТекЭлемент));
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипМакета") Тогда
		Объект.ТипМакета = ТипМакетаОбластиКомпоновкиДанных[СтруктураОбъекта.ТипМакета];
	КонецЕсли;

КонецПроцедуры // МакетГруппировкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// МакетПоляСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - МакетПоляСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция МакетПоляСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетПоляСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Макет", Объект.Макет);
	СтруктураОбъекта.Вставить("Поле" , Объект.Поле);

	Возврат СтруктураОбъекта;
	
КонецФункции // МакетПоляСКДВСтруктуру()

// Процедура - заполняет объект СКД МакетПоляСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - МакетПоляСхемыКомпоновкиДанных  - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура МакетПоляСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетПоляСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Макет") Тогда
		Объект.Макет = СтруктураОбъекта.Макет;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = СтруктураОбъекта.Поле;
	КонецЕсли;

КонецПроцедуры // МакетПоляСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// МакетПолейИтогаСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - МакетПолейИтогаСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция МакетПолейИтогаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетПолейИтогаСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИмяГруппировки1", Объект.ИмяГруппировки1);
	СтруктураОбъекта.Вставить("ИмяГруппировки2", Объект.ИмяГруппировки2);
	СтруктураОбъекта.Вставить("Макет" , Объект.Макет);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ПоляГруппировки1 Цикл
		МассивЭлементов.Добавить(СокрЛП(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ПоляГруппировки1", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ПоляГруппировки2 Цикл
		МассивЭлементов.Добавить(СокрЛП(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ПоляГруппировки2", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("ТипМакета1" , ТипМакетаОбластиКомпоновкиДанныхВСтроку(Объект.ТипМакета1));
	СтруктураОбъекта.Вставить("ТипМакета2" , ТипМакетаОбластиКомпоновкиДанныхВСтроку(Объект.ТипМакета2));

	Возврат СтруктураОбъекта;
	
КонецФункции // МакетПолейИтогаСКДВСтруктуру()

// Процедура - заполняет объект СКД МакетПолейИтогаСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - МакетПолейИтогаСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура МакетПолейИтогаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("МакетПолейИтогаСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИмяГруппировки1") Тогда
		Объект.ИмяГруппировки1 = СтруктураОбъекта.ИмяГруппировки1;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИмяГруппировки2") Тогда
		Объект.ИмяГруппировки2 = СтруктураОбъекта.ИмяГруппировки2;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Макет") Тогда
		Объект.Макет = СтруктураОбъекта.Макет;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПоляГруппировки1") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ПоляГруппировки1 Цикл
			Объект.ПоляГруппировки1.Добавить(Новый ПолеКомпоновкиДанных(ТекЭлемент));
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПоляГруппировки2") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ПоляГруппировки2 Цикл
			Объект.ПоляГруппировки2.Добавить(Новый ПолеКомпоновкиДанных(ТекЭлемент));
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипМакета1") Тогда
		Объект.ТипМакета1 = ТипМакетаОбластиКомпоновкиДанных[СтруктураОбъекта.ТипМакета1];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипМакета2") Тогда
		Объект.ТипМакета2 = ТипМакетаОбластиКомпоновкиДанных[СтруктураОбъекта.ТипМакета2];
	КонецЕсли;

КонецПроцедуры // МакетПолейИтогаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// НаборДанныхЗапросСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - НаборДанныхЗапросСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция НаборДанныхЗапросСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип", "НаборДанныхЗапросСхемыКомпоновкиДанных");

	СтруктураОбъекта.Вставить("АвтозаполнениеДоступныхПолей", Объект.АвтозаполнениеДоступныхПолей);
	СтруктураОбъекта.Вставить("Запрос"                      , Объект.Запрос);
	СтруктураОбъекта.Вставить("Имя"                         , Объект.Имя);
	СтруктураОбъекта.Вставить("ИсточникДанных"              , Объект.ИсточникДанных);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Поля Цикл
		МассивЭлементов.Добавить(ПолеНабораДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Поля", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // НаборДанныхЗапросСКДВСтруктуру()

// Процедура - заполняет объект СКД НаборДанныхЗапросСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НаборДанныхЗапросСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                              - данные для заполнения объекта
//
Процедура НаборДанныхЗапросСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("АвтозаполнениеДоступныхПолей") Тогда
		Объект.АвтозаполнениеДоступныхПолей = СтруктураОбъекта.АвтозаполнениеДоступныхПолей;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Запрос") Тогда
		Объект.Запрос = СтруктураОбъекта.Запрос;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИсточникДанных") Тогда
		Объект.ИсточникДанных = СтруктураОбъекта.ИсточникДанных;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поля") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Поля Цикл
			НовыйЭлемент = Объект.Поля.Добавить(Тип(ТекЭлемент.Тип));
			ПолеНабораДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // НаборДанныхЗапросСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// НаборДанныхОбъектСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - НаборДанныхОбъектСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция НаборДанныхОбъектСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип", "НаборДанныхОбъектСхемыКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Имя"           , Объект.Имя);
	СтруктураОбъекта.Вставить("ИмяОбъекта"    , Объект.ИмяОбъекта);
	СтруктураОбъекта.Вставить("ИсточникДанных", Объект.ИсточникДанных);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Поля Цикл
		МассивЭлементов.Добавить(ПолеНабораДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Поля", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // НаборДанныхОбъектСКДВСтруктуру()

// Процедура - заполняет объект СКД НаборДанныхОбъектСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НаборДанныхОбъектСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                              - данные для заполнения объекта
//
Процедура НаборДанныхОбъектСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИмяОбъекта") Тогда
		Объект.ИмяОбъекта = СтруктураОбъекта.ИмяОбъекта;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИсточникДанных") Тогда
		Объект.ИсточникДанных = СтруктураОбъекта.ИсточникДанных;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поля") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Поля Цикл
			НовыйЭлемент = Объект.Поля.Добавить(Тип(ТекЭлемент.Тип));
			ПолеНабораДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // НаборДанныхОбъектСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// НаборДанныхОбъединениеСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - НаборДанныхОбъединениеСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция НаборДанныхОбъединениеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип", "НаборДанныхОбъединениеСхемыКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Имя"           , Объект.Имя);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Поля Цикл
		МассивЭлементов.Добавить(ПолеНабораДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Поля", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(НаборДанныхСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НаборДанныхОбъединениеСКДВСтруктуру()

// Процедура - заполняет объект СКД НаборДанныхОбъединениеСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НаборДанныхОбъединениеСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура НаборДанныхОбъединениеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поля") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Поля Цикл
			НовыйЭлемент = Объект.Поля.Добавить(Тип(ТекЭлемент.Тип));
			ПолеНабораДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			НаборДанныхСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // НаборДанныхОбъединениеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// НаборДанныхЗапросСхемыКомпоновкиДанных, НаборДанныхОбъектСхемыКомпоновкиДанных,
// НаборДанныхОбъединениеСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - НаборДанныхЗапросСхемыКомпоновкиДанных,     - заполняемый объект
//                       НаборДанныхОбъектСхемыКомпоновкиДанных
//                       НаборДанныхОбъединениеСхемыКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция НаборДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
		Возврат НаборДанныхЗапросСКДВСтруктуру(Объект);		
	ИначеЕсли ТипЗнч(Объект) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		Возврат НаборДанныхОбъектСКДВСтруктуру(Объект);		
	ИначеЕсли ТипЗнч(Объект) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		Возврат НаборДанныхОбъединениеСКДВСтруктуру(Объект);		
	Иначе
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
КонецФункции // НаборДанныхСКДВСтруктуру()

// Процедура - заполняет объекты СКД НаборДанныхЗапросСхемыКомпоновкиДанных,
// НаборДанныхОбъектСхемыКомпоновкиДанных, НаборДанныхОбъединениеСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НаборДанныхЗапросСхемыКомпоновкиДанных,      - заполняемый объект
//                       НаборДанныхОбъектСхемыКомпоновкиДанных,
//                       НаборДанныхОбъединениеСхемыКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                    - данные для заполнения объекта
//
Процедура НаборДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
		НаборДанныхЗапросСКДИзСтруктуры(Объект, СтруктураОбъекта);		
	ИначеЕсли ТипЗнч(Объект) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		НаборДанныхОбъектСКДИзСтруктуры(Объект, СтруктураОбъекта);		
	ИначеЕсли ТипЗнч(Объект) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		НаборДанныхОбъединениеСКДИзСтруктуры(Объект, СтруктураОбъекта);		
	КонецЕсли;
	
КонецПроцедуры // НаборДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПараметрСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПараметрСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПараметрСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("ВключатьВДоступныеПоля"        , Объект.ВключатьВДоступныеПоля);
	СтруктураОбъекта.Вставить("Выражение"                     , Объект.Выражение);
	СтруктураОбъекта.Вставить("ДоступенСписокЗначений"        , Объект.ДоступенСписокЗначений);
	СтруктураОбъекта.Вставить("Заголовок"                     , Объект.Заголовок);
	СтруктураОбъекта.Вставить("ЗапрещатьНезаполненныеЗначения", Объект.ЗапрещатьНезаполненныеЗначения);
	СтруктураОбъекта.Вставить("Значение"                      , ЗначениеВСтруктуру(Объект.Значение));
	СтруктураОбъекта.Вставить("Имя"                           , Объект.Имя);
	
	СтруктураОбъекта.Вставить("Использование",
	                          ИспользованиеПараметраКомпоновкиДанныхВСтроку(Объект.Использование));
	
	СтруктураОбъекта.Вставить("ОграничениеИспользования"      , Объект.ОграничениеИспользования);
	СтруктураОбъекта.Вставить("ПараметрФункциональныхОпций"   , Объект.ПараметрФункциональныхОпций);
	
	СтруктураОбъекта.Вставить("ПараметрыРедактирования",
	                          ПараметрыРедактированияСКДВСтруктуру(Объект.ПараметрыРедактирования));
	                          
	СтруктураОбъекта.Вставить("ТипЗначения"                   , ЗначениеВСтруктуру(Объект.ТипЗначения));

	Возврат СтруктураОбъекта;
	
КонецФункции // ПараметрСКДВСтруктуру()

// Процедура - заполняет объект СКД ПараметрСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПараметрСхемыКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ПараметрСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВключатьВДоступныеПоля") Тогда
		Объект.ВключатьВДоступныеПоля = СтруктураОбъекта.ВключатьВДоступныеПоля;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ДоступенСписокЗначений") Тогда
		Объект.ДоступенСписокЗначений = СтруктураОбъекта.ДоступенСписокЗначений;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ЗапрещатьНезаполненныеЗначения") Тогда
		Объект.ЗапрещатьНезаполненныеЗначения = СтруктураОбъекта.ЗапрещатьНезаполненныеЗначения;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Значение") Тогда
		Объект.Значение = ЗначениеИзСтруктуры(СтруктураОбъекта.Значение);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = ИспользованиеПараметраКомпоновкиДанных[СтруктураОбъекта.Использование];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ОграничениеИспользования") Тогда
		Объект.ОграничениеИспользования = СтруктураОбъекта.ОграничениеИспользования;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрФункциональныхОпций") Тогда
		Объект.ПараметрФункциональныхОпций = СтруктураОбъекта.ПараметрФункциональныхОпций;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыРедактирования") Тогда
		ПараметрыРедактированияСКДИзСтруктуры(Объект.ПараметрыРедактирования, СтруктураОбъекта.ПараметрыРедактирования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипЗначения") Тогда
		Объект.ТипЗначения = ЗначениеИзСтруктуры(СтруктураОбъекта.ТипЗначения);
	КонецЕсли;

КонецПроцедуры // ПараметрСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПолеИтогаСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПолеИтогаСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеИтогаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеИтогаСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Выражение"  , Объект.Выражение);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Группировки Цикл
		МассивЭлементов.Добавить(СокрЛП(ТекЭлемент)); // TODO: Проверить тип элемента массива группировок
	КонецЦикла;
	СтруктураОбъекта.Вставить("Группировки", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("ПутьКДанным", Объект.ПутьКДанным);

	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеИтогаСКДВСтруктуру()

// Процедура - заполняет объект СКД ПолеИтогаСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПолеИтогаСхемыКомпоновкиДанных  - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ПолеИтогаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеИтогаСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Группировки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Группировки Цикл
			Объект.Группировки.Добавить(ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПутьКДанным") Тогда
		Объект.ПутьКДанным = СтруктураОбъекта.ПутьКДанным;
	КонецЕсли;

КонецПроцедуры // ПолеИтогаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// СвязьНаборовДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - СвязьНаборовДанныхСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция СвязьНаборовДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("СвязьНаборовДанныхСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("ВыражениеИсточник"       , Объект.ВыражениеИсточник);
	СтруктураОбъекта.Вставить("ВыражениеПриемник"       , Объект.ВыражениеПриемник);
	СтруктураОбъекта.Вставить("НаборДанныхИсточник"     , Объект.НаборДанныхИсточник);
	СтруктураОбъекта.Вставить("НаборДанныхПриемник"     , Объект.НаборДанныхПриемник);
	СтруктураОбъекта.Вставить("НачальноеВыражение"      , Объект.НачальноеВыражение);
	СтруктураОбъекта.Вставить("Обязательная"            , Объект.Обязательная);
	СтруктураОбъекта.Вставить("Параметр"                , Объект.Параметр);
	СтруктураОбъекта.Вставить("РазрешенСписокПараметров", Объект.РазрешенСписокПараметров);
	СтруктураОбъекта.Вставить("УсловиеСвязи"            , Объект.УсловиеСвязи);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // СвязьНаборовДанныхСКДВСтруктуру()

// Процедура - заполняет объект СКД СвязьНаборовДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - СвязьНаборовДанныхСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                               - данные для заполнения объекта
//
Процедура СвязьНаборовДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("СвязьНаборовДанныхСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВыражениеИсточник") Тогда
		Объект.ВыражениеИсточник = СтруктураОбъекта.ВыражениеИсточник;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ВыражениеПриемник") Тогда
		Объект.ВыражениеПриемник = СтруктураОбъекта.ВыражениеПриемник;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НаборДанныхИсточник") Тогда
		Объект.НаборДанныхИсточник = СтруктураОбъекта.НаборДанныхИсточник;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НаборДанныхПриемник") Тогда
		Объект.НаборДанныхПриемник = СтруктураОбъекта.НаборДанныхПриемник;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НачальноеВыражение") Тогда
		Объект.НачальноеВыражение = СтруктураОбъекта.НачальноеВыражение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Обязательная") Тогда
		Объект.Обязательная = СтруктураОбъекта.Обязательная;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Параметр") Тогда
		Объект.Параметр = СтруктураОбъекта.Параметр;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РазрешенСписокПараметров") Тогда
		Объект.РазрешенСписокПараметров = СтруктураОбъекта.РазрешенСписокПараметров;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("УсловиеСвязи") Тогда
		Объект.УсловиеСвязи = СтруктураОбъекта.УсловиеСвязи;
	КонецЕсли;

КонецПроцедуры // СвязьНаборовДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВыражениеУпорядочиванияКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВыражениеУпорядочиванияКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВыражениеУпорядочиванияСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыражениеУпорядочиванияКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Автоупорядочивание", Объект.Автоупорядочивание);
	СтруктураОбъекта.Вставить("Выражение"         , Объект.Выражение);
	СтруктураОбъекта.Вставить("ТипУпорядочивания",
	                          НаправлениеСортировкиКомпоновкиДанныхВСтроку(Объект.ТипУпорядочивания));

	Возврат СтруктураОбъекта;
	
КонецФункции // ВыражениеУпорядочиванияСКДВСтруктуру()

// Процедура - заполняет объект СКД ВыражениеУпорядочиванияКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВыражениеУпорядочиванияКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                               - данные для заполнения объекта
//
Процедура ВыражениеУпорядочиванияСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыражениеУпорядочиванияКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Автоупорядочивание") Тогда
		Объект.Автоупорядочивание = СтруктураОбъекта.Автоупорядочивание;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипУпорядочивания") Тогда
		Объект.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных[СтруктураОбъекта.ТипУпорядочивания];
	КонецЕсли;

КонецПроцедуры // ВыражениеУпорядочиванияСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОграничениеИспользованияПоляСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОграничениеИспользованияПоляСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОграничениеИспользованияПоляСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОграничениеИспользованияПоляСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Группировка", Объект.Группировка);
	СтруктураОбъекта.Вставить("Поле"       , Объект.Поле);
	СтруктураОбъекта.Вставить("Порядок"    , Объект.Порядок);
	СтруктураОбъекта.Вставить("Условие"    , Объект.Условие);

	Возврат СтруктураОбъекта;
	
КонецФункции // ОграничениеИспользованияПоляСКДВСтруктуру()

// Процедура - заполняет объект СКД ОграничениеИспользованияПоляСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОграничениеИспользованияПоляСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                         - данные для заполнения объекта
//
Процедура ОграничениеИспользованияПоляСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОграничениеИспользованияПоляСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Группировка") Тогда
		Объект.Группировка = СтруктураОбъекта.Группировка;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = СтруктураОбъекта.Поле;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Порядок") Тогда
		Объект.Порядок = СтруктураОбъекта.Порядок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Условие") Тогда
		Объект.Условие = СтруктураОбъекта.Условие;
	КонецЕсли;

КонецПроцедуры // ОграничениеИспользованияПоляСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОформлениеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОформлениеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОформлениеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ОформлениеСКДВСтруктуру()

// Процедура - заполняет объект СКД ОформлениеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОформлениеКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                    - данные для заполнения объекта
//
Процедура ОформлениеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);

КонецПроцедуры // ОформлениеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЗначениеПараметраНастроекКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЗначениеПараметраНастроекКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЗначениеПараметраНастроекСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"     , "ЗначениеПараметраНастроекКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Значение", ЗначениеВСтруктуру(Объект.Значение));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ЗначенияВложенныхПараметров Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ЗначенияВложенныхПараметров", МассивЭлементов);

	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("Параметр", СокрЛП(Объект.Параметр));

	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	
	//TODO: Проверить как работает родитель
	Возврат СтруктураОбъекта;
	
КонецФункции // ЗначениеПараметраНастроекСКДВСтруктуру()

// Процедура - заполняет объект СКД ЗначениеПараметраНастроекКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЗначениеПараметраНастроекКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                 - данные для заполнения объекта
//
Процедура ЗначениеПараметраНастроекСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Значение") Тогда
		Объект.Значение = ЗначениеИзСтруктуры(СтруктураОбъекта.Значение);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ЗначенияВложенныхПараметров") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ЗначенияВложенныхПараметров Цикл
			НовыйЭлемент = Объект.ЗначенияВложенныхПараметров.Добавить();
			ЗначениеПараметраСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Параметр") Тогда
		Объект.Параметр = Новый ПараметрКомпоновкиДанных(СтруктураОбъекта.Параметр);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;
	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;
	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения =
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;
	
КонецПроцедуры // ЗначениеПараметраНастроекСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// ЗначениеПараметраНастроекКомпоновкиДанных, ЗначениеПараметраКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЗначениеПараметраНастроекКомпоновкиДанных,     - заполняемый объект
//                       ЗначениеПараметраКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЗначениеПараметраСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		Возврат ЗначениеПараметраНастроекСКДВСтруктуру(Объект);
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"     , "ЗначениеПараметраКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Значение", ЗначениеВСтруктуру(Объект.Значение));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ЗначенияВложенныхПараметров Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ЗначенияВложенныхПараметров", МассивЭлементов);

	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("Параметр", СокрЛП(Объект.Параметр));

	//TODO: Проверить как работает родитель
	Возврат СтруктураОбъекта;
	
КонецФункции // ЗначениеПараметраСКДВСтруктуру()

// Процедура - заполняет объект СКД ЗначениеПараметраНастроекКомпоновкиДанных,
// ЗначениеПараметраКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЗначениеПараметраНастроекКомпоновкиДанных,  - заполняемый объект
//                       ЗначениеПараметраКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура ЗначениеПараметраСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ЗначениеПараметраНастроекСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Значение") Тогда
		ВремЗначение = ЗначениеИзСтруктуры(СтруктураОбъекта.Значение);
		// Обработка случая пустого значения несериализуемого типа
		Если НЕ ВремЗначение = Неопределено ИЛИ ТипЗнч(Объект.Значение) = Тип("Неопределено") Тогда
			Объект.Значение = ВремЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ЗначенияВложенныхПараметров") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ЗначенияВложенныхПараметров Цикл
			НовыйЭлемент = Объект.ЗначенияВложенныхПараметров.Добавить();
			ЗначениеПараметраСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Параметр") Тогда
		Объект.Параметр = Новый ПараметрКомпоновкиДанных(СтруктураОбъекта.Параметр);
	КонецЕсли;
	
КонецПроцедуры // ЗначениеПараметраСКДИзСтруктуры()

// Процедура - заполняет коллекцию объектов СКД ЗначениеПараметраНастроекКомпоновкиДанных,
// ЗначениеПараметраКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	ОбъектВладелец           - ОбъектСКД        - объект верхнего уровня, содержащий заполняемые коллекции,
//                                                для проверки доступных параметров
// 	Объект                   - ОбъектСКД        - объект, содержащий заполняемую коллекцию
// 	СтруктураОбъекта         - Структура        - данные для заполнения коллекции
// 	ИмяКоллекцииПараметров   - Строка           - имя заполняемой коллекции
//
Процедура ЗначенияПараметровОбъектаСКДИзСтруктуры(ОбъектВладелец,
                                                  Объект,
                                                  СтруктураОбъекта,
                                                  ИмяКоллекцииПараметров = "Элементы")
	
	Если НЕ СтруктураОбъекта.Свойство(ИмяКоллекцииПараметров) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекЭлемент Из СтруктураОбъекта[ИмяКоллекцииПараметров] Цикл
		Если ОбъектВладелец.ДоступныеПараметры = Неопределено Тогда
			НовыйЭлемент = Объект[ИмяКоллекцииПараметров].Добавить();
			ЗначениеПараметраСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		Иначе	
			ТекПараметр = ОбъектВладелец.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ТекЭлемент.Параметр));
			ВремЗначение = ЗначениеИзСтруктуры(ТекЭлемент.Значение);
			// Обработка случая пустого значения несериализуемого типа
			Если НЕ ВремЗначение = Неопределено ИЛИ ТипЗнч(ТекПараметр.Значение) = Тип("Неопределено") Тогда
				ТекПараметр.Значение = ВремЗначение;
			КонецЕсли;
			ЗначенияПараметровОбъектаСКДИзСтруктуры(ОбъектВладелец, ТекПараметр, ТекЭлемент, "ЗначенияВложенныхПараметров");
			ТекПараметр.Использование = ТекЭлемент.Использование;
			Если ТипЗнч(ТекПараметр) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				ТекПараметр.ИдентификаторПользовательскойНастройки = ТекЭлемент.ИдентификаторПользовательскойНастройки;
				ТекПараметр.ПредставлениеПользовательскойНастройки = ТекЭлемент.ПредставлениеПользовательскойНастройки;
				ТекПараметр.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[ТекЭлемент.РежимОтображения];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры // ЗначенияПараметровОбъектаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПараметрыРедактированияКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПараметрыРедактированияКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПараметрыРедактированияСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрыРедактированияКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ПараметрыРедактированияСКДВСтруктуру()

// Процедура - заполняет объект СКД ПараметрыРедактированияКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПараметрыРедактированияКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                               - данные для заполнения объекта
//
Процедура ПараметрыРедактированияСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрыРедактированияКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);

КонецПроцедуры // ПараметрыРедактированияСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// СтрокаТаблицыОбластиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - СтрокаТаблицыОбластиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция СтрокаТаблицыОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("СтрокаТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"                 , "СтрокаТаблицыОбластиКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("ИдентификаторТаблицы", Объект.ИдентификаторТаблицы);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Ячейки Цикл
		МассивЭлементов.Добавить(ЯчейкаТаблицыОбластиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Ячейки", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // СтрокаТаблицыОбластиСКДВСтруктуру()

// Процедура - заполняет объект СКД СтрокаТаблицыОбластиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - СтрокаТаблицыОбластиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура СтрокаТаблицыОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("СтрокаТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторТаблицы") Тогда
		Объект.ИдентификаторТаблицы = СтруктураОбъекта.ИдентификаторТаблицы;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Ячейки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Ячейки Цикл
			НовыйЭлемент = Объект.Ячейки.Добавить();
			ЯчейкаТаблицыОбластиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // СтрокаТаблицыОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЯчейкаТаблицыОбластиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЯчейкаТаблицыОбластиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЯчейкаТаблицыОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ЯчейкаТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Оформление", ОформлениеЯчейкиТаблицыОбластиСКДВСтруктуру(Объект.Оформление));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ПолеОбластиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ЯчейкаТаблицыОбластиСКДВСтруктуру()

// Процедура - заполняет объект СКД ЯчейкаТаблицыОбластиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЯчейкаТаблицыОбластиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура ЯчейкаТаблицыОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ЯчейкаТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Оформление") Тогда
		ОформлениеЯчейкиТаблицыОбластиСКДИзСтруктуры(Объект.Оформление, СтруктураОбъекта.Оформление);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ПолеОбластиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ЯчейкаТаблицыОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОформлениеЯчейкиТаблицыОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ОформлениеЯчейкиТаблицыОбластиСКДВСтруктуру()

// Процедура - заполняет объект СКД ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                      - данные для заполнения объекта
//
Процедура ОформлениеЯчейкиТаблицыОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеЯчейкиТаблицыОбластиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);

КонецПроцедуры // ОформлениеЯчейкиТаблицыОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОформлениеПоляОбластиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОформлениеПоляОбластиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОформлениеПоляОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеПоляОбластиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ОформлениеПоляОбластиСКДВСтруктуру()

// Процедура - заполняет объект СКД ОформлениеПоляОбластиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОформлениеПоляОбластиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ОформлениеПоляОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформлениеПоляОбластиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);

КонецПроцедуры // ОформлениеПоляОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПараметрОбластиВыражениеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПараметрОбластиВыражениеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПараметрОбластиВыражениеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрОбластиВыражениеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"      , "ПараметрОбластиВыражениеКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Имя"      , Объект.Имя);
	СтруктураОбъекта.Вставить("Выражение", Объект.Выражение);

	Возврат СтруктураОбъекта;
	
КонецФункции // ПараметрОбластиВыражениеСКДВСтруктуру()

// Процедура - заполняет объект СКД ПараметрОбластиВыражениеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПараметрОбластиВыражениеКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                - данные для заполнения объекта
//
Процедура ПараметрОбластиВыражениеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрОбластиВыражениеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;
	
КонецПроцедуры // ПараметрОбластиВыражениеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПараметрОбластиРасшифровкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПараметрОбластиРасшифровкаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПараметрОбластиРасшифровкаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрОбластиРасшифровкаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип", "ПараметрОбластиРасшифровкаКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Имя", Объект.Имя);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВыраженияПолей Цикл
		МассивЭлементов.Добавить(ВыражениеПоляПараметраРасшифровкиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВыраженияПолей", МассивЭлементов);

	СтруктураОбъекта.Вставить("ОсновноеДействие",
	                          ДействиеОбработкиРасшифровкиКомпоновкиДанныхВСтроку(Объект.ОсновноеДействие));

	Возврат СтруктураОбъекта;
	
КонецФункции // ПараметрОбластиРасшифровкаСКДВСтруктуру()

// Процедура - заполняет объект СКД ПараметрОбластиРасшифровкаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПараметрОбластиРасшифровкаКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                  - данные для заполнения объекта
//
Процедура ПараметрОбластиРасшифровкаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПараметрОбластиРасшифровкаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВыраженияПолей") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВыраженияПолей Цикл
			НовыйЭлемент = Объект.ВыраженияПолей.Добавить();
			ВыражениеПоляПараметраРасшифровкиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ОсновноеДействие") Тогда
		Объект.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных[СтруктураОбъекта.ОсновноеДействие];
	КонецЕсли;
	
КонецПроцедуры // ПараметрОбластиРасшифровкаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПараметрОбластиВыражениеКомпоновкиДанных, ПараметрОбластиРасшифровкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПараметрОбластиВыражениеКомпоновкиДанных,     - заполняемый объект
//                       ПараметрОбластиРасшифровкаКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПараметрОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ПараметрОбластиВыражениеКомпоновкиДанных") Тогда
		Возврат ПараметрОбластиВыражениеСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПараметрОбластиРасшифровкаКомпоновкиДанных") Тогда
		Возврат ПараметрОбластиРасшифровкаСКДВСтруктуру(Объект);
	Иначе
		Возврат СтруктураОбъекта;
	КонецЕсли;

КонецФункции // ПараметрОбластиСКДВСтруктуру()

// Процедура - заполняет объекты СКД ПараметрОбластиВыражениеКомпоновкиДанных,
// ПараметрОбластиРасшифровкаКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПараметрОбластиВыражениеКомпоновкиДанных,   - заполняемый объект
//                       ПараметрОбластиРасшифровкаКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура ПараметрОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ПараметрОбластиВыражениеКомпоновкиДанных") Тогда
		ПараметрОбластиВыражениеСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПараметрОбластиРасшифровкаКомпоновкиДанных") Тогда
		ПараметрОбластиРасшифровкаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ПараметрОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВыражениеПоляПараметраРасшифровкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Выражение", Объект.Выражение);
	СтруктураОбъекта.Вставить("Поле"     , Объект.Поле);

	Возврат СтруктураОбъекта;
	
КонецФункции // ВыражениеПоляПараметраРасшифровкиСКДВСтруктуру()

// Процедура - заполняет объект СКД ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                                - данные для заполнения объекта
//
Процедура ВыражениеПоляПараметраРасшифровкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыражениеПоляПараметраОбластиРасшифровкаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выражение") Тогда
		Объект.Выражение = СтруктураОбъекта.Выражение;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = СтруктураОбъекта.Поле;
	КонецЕсли;
	
КонецПроцедуры // ВыражениеПоляПараметраРасшифровкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПолеОбластиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПолеОбластиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеОбластиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеОбластиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"       , "ПолеОбластиКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Значение"  , ЗначениеВСтруктуру(Объект.Значение));
	СтруктураОбъекта.Вставить("Оформление", ОформлениеПоляОбластиСКДВСтруктуру(Объект.Оформление));

	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеОбластиСКДВСтруктуру()

// Процедура - заполняет объект СКД ПолеОбластиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПолеОбластиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                   - данные для заполнения объекта
//
Процедура ПолеОбластиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеОбластиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Значение") Тогда
		Объект.Значение = ЗначениеИзСтруктуры(СтруктураОбъекта.Значение);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Оформление") Тогда
		ОформлениеПоляОбластиСКДИзСтруктуры(Объект.Оформление, СтруктураОбъекта.Оформление);
	КонецЕсли;
	
КонецПроцедуры // ПолеОбластиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПолеНабораДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПолеНабораДанныхСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеНабораДанныхПолеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип"      , "ПолеНабораДанныхСхемыКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("ВыражениеПредставления", Объект.ВыражениеПредставления);

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.ВыраженияУпорядочивания Цикл
		МассивЭлементов.Добавить(ВыражениеУпорядочиванияСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("ВыраженияУпорядочивания", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("Заголовок"                  , Объект.Заголовок);
	СтруктураОбъекта.Вставить("НаборДанныхПроверкиИерархии", Объект.НаборДанныхПроверкиИерархии);
	
	СтруктураОбъекта.Вставить("ОграничениеИспользования",
	                          ОграничениеИспользованияПоляСКДВСтруктуру(Объект.ОграничениеИспользования));
	СтруктураОбъекта.Вставить("ОграничениеИспользованияРеквизитов",
	                          ОграничениеИспользованияПоляСКДВСтруктуру(Объект.ОграничениеИспользованияРеквизитов));
	                          
	СтруктураОбъекта.Вставить("Оформление", ОформлениеСКДВСтруктуру(Объект.Оформление));
	СтруктураОбъекта.Вставить("ПараметрНабораДанныхПроверкиИерархии", Объект.ПараметрНабораДанныхПроверкиИерархии);
	
	СтруктураОбъекта.Вставить("ПараметрыРедактирования",
	                          ПараметрыРедактированияСКДВСтруктуру(Объект.ПараметрыРедактирования));
	                          
	СтруктураОбъекта.Вставить("Поле"       , Объект.Поле);
	СтруктураОбъекта.Вставить("ПутьКДанным", Объект.ПутьКДанным);
	СтруктураОбъекта.Вставить("Роль"       , РольПоляНабораДанныхСКДВСтруктуру(Объект.Роль));
	СтруктураОбъекта.Вставить("ТипЗначения", ЗначениеВСтруктуру(Объект.ТипЗначения));
	 
	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеНабораДанныхПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД ПолеНабораДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПолеНабораДанныхСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ПолеНабораДанныхПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВыражениеПредставления") Тогда
		Объект.ВыражениеПредставления = СтруктураОбъекта.ВыражениеПредставления;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ВыраженияУпорядочивания") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.ВыраженияУпорядочивания Цикл
			НовыйЭлемент = Объект.ВыраженияУпорядочивания.Добавить();
			ВыражениеУпорядочиванияСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НаборДанныхПроверкиИерархии") Тогда
		Объект.НаборДанныхПроверкиИерархии = СтруктураОбъекта.НаборДанныхПроверкиИерархии;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ОграничениеИспользования") Тогда
		ОграничениеИспользованияПоляСКДИзСтруктуры(Объект.ОграничениеИспользования,
		                                           СтруктураОбъекта.ОграничениеИспользования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ОграничениеИспользованияРеквизитов") Тогда
		ОграничениеИспользованияПоляСКДИзСтруктуры(Объект.ОграничениеИспользованияРеквизитов,
		                                           СтруктураОбъекта.ОграничениеИспользованияРеквизитов);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Оформление") Тогда
		ОформлениеСКДИзСтруктуры(Объект.Оформление, СтруктураОбъекта.Оформление);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрНабораДанныхПроверкиИерархии") Тогда
		Объект.ПараметрНабораДанныхПроверкиИерархии = СтруктураОбъекта.ПараметрНабораДанныхПроверкиИерархии;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыРедактирования") Тогда
		ПараметрыРедактированияСКДИзСтруктуры(Объект.ПараметрыРедактирования, СтруктураОбъекта.ПараметрыРедактирования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = СтруктураОбъекта.Поле;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПутьКДанным") Тогда
		Объект.ПутьКДанным = СтруктураОбъекта.ПутьКДанным;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Роль") Тогда
		РольПоляНабораДанныхСКДИзСтруктуры(Объект.Роль, СтруктураОбъекта.Роль);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипЗначения") Тогда
		Объект.ТипЗначения = ЗначениеИзСтруктуры(СтруктураОбъекта.ТипЗначения);
	КонецЕсли;

КонецПроцедуры // ПолеНабораДанныхПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПапкаПолейНабораДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПапкаПолейНабораДанныхСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеНабораДанныхПапкаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип"      , "ПапкаПолейНабораДанныхСхемыКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Заголовок", Объект.Заголовок);
	
	СтруктураОбъекта.Вставить("ОграничениеИспользования",
	                          ОграничениеИспользованияПоляСКДВСтруктуру(Объект.ОграничениеИспользования));

	СтруктураОбъекта.Вставить("ПутьКДанным", Объект.ПутьКДанным);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеНабораДанныхПапкаСКДВСтруктуру()

// Процедура - заполняет объект СКД ПапкаПолейНабораДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПапкаПолейНабораДанныхСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура ПолеНабораДанныхПапкаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ОграничениеИспользования") Тогда
		ОграничениеИспользованияПоляСКДИзСтруктуры(Объект.ОграничениеИспользования,
		                                           СтруктураОбъекта.ОграничениеИспользования);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПутьКДанным") Тогда
		Объект.ПутьКДанным = СтруктураОбъекта.ПутьКДанным;
	КонецЕсли;

КонецПроцедуры // ПолеНабораДанныхПапкаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВложенныйНаборДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВложенныйНаборДанныхСхемыКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеНабораДанныхВложенныйНаборСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВложенныйНаборДанныхСхемыКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип"        , "ВложенныйНаборДанныхСхемыКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Заголовок"  , Объект.Заголовок);
	СтруктураОбъекта.Вставить("Поле"       , Объект.Поле);
	СтруктураОбъекта.Вставить("ПутьКДанным", Объект.ПутьКДанным);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеНабораДанныхВложенныйНаборСКДВСтруктуру()

// Процедура - заполняет объект СКД ВложенныйНаборДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВложенныйНаборДанныхСхемыКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                 - данные для заполнения объекта
//
Процедура ПолеНабораДанныхВложенныйНаборСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВложенныйНаборДанныхСхемыКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = СтруктураОбъекта.Поле;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПутьКДанным") Тогда
		Объект.ПутьКДанным = СтруктураОбъекта.ПутьКДанным;
	КонецЕсли;

КонецПроцедуры // ПолеНабораДанныхВложенныйНаборСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПолеНабораДанныхСхемыКомпоновкиДанных, ПапкаПолейНабораДанныхСхемыКомпоновкиДанных,
// ВложенныйНаборДанныхСхемыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПолеНабораДанныхСхемыКомпоновкиДанных,       - заполняемый объект
//                       ПапкаПолейНабораДанныхСхемыКомпоновкиДанных
//                       ВложенныйНаборДанныхСхемыКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеНабораДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат ПолеНабораДанныхПолеСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных") Тогда
		Возврат ПолеНабораДанныхПапкаСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ВложенныйНаборДанныхСхемыКомпоновкиДанных") Тогда
		Возврат ПолеНабораДанныхВложенныйНаборСКДВСтруктуру(Объект);
	Иначе
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
КонецФункции // ПолеНабораДанныхСКДВСтруктуру()

// Процедура - заполняет объекты СКД ПолеНабораДанныхСхемыКомпоновкиДанных,
// ПапкаПолейНабораДанныхСхемыКомпоновкиДанных, ВложенныйНаборДанныхСхемыКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПолеНабораДанныхСхемыКомпоновкиДанных,        - заполняемый объект
//                       ПапкаПолейНабораДанныхСхемыКомпоновкиДанных,
//                       ВложенныйНаборДанныхСхемыКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                     - данные для заполнения объекта
//
Процедура ПолеНабораДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
		ПолеНабораДанныхПолеСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных") Тогда
		ПолеНабораДанныхПапкаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ВложенныйНаборДанныхСхемыКомпоновкиДанных") Тогда
		ПолеНабораДанныхВложенныйНаборСКДИзСтруктуры(Объект, СтруктураОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ПолеНабораДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВыбранныеПоляКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВыбранныеПоляКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВыбранныеПоляСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	                          
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ВыбранноеПолеСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ВыбранныеПоляСКДВСтруктуру()

// Процедура - заполняет объект СКД ВыбранныеПоляКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВыбранныеПоляКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                     - данные для заполнения объекта
//
Процедура ВыбранныеПоляСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения =
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ВыбранноеПолеСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ВыбранныеПоляСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ГруппаВыбранныхПолейКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппаВыбранныхПолейКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ГруппаВыбранныхПолейСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"             , "ГруппаВыбранныхПолейКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Заголовок"       , Объект.Заголовок);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	СтруктураОбъекта.Вставить("Поле"            , СокрЛП(Объект.Поле));
	СтруктураОбъекта.Вставить("Расположение"    , РасположениеПоляКомпоновкиДанныхВСтроку(Объект.Расположение));
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	// TODO: проверить родителя
	                          
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ВыбранноеПолеСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ГруппаВыбранныхПолейСКДВСтруктуру()

// Процедура - заполняет объект СКД ГруппаВыбранныхПолейКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ГруппаВыбранныхПолейКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура ГруппаВыбранныхПолейСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.Поле);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Расположение") Тогда
		Объект.Расположение = РасположениеПоляКомпоновкиДанных[СтруктураОбъекта.Расположение];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения =
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;
	// TODO: проверить родителя

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ВыбранноеПолеСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ГруппаВыбранныхПолейСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// АвтоВыбранноеПолеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - АвтоВыбранноеПолеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция АвтоВыбранноеПолеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"          , "АвтоВыбранноеПолеКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	// TODO: проверить родителя
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ВыбранноеПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД АвтоВыбранноеПолеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - АвтоВыбранноеПолеКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                         - данные для заполнения объекта
//
Процедура АвтоВыбранноеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;
	// TODO: проверить родителя

КонецПроцедуры // АвтоВыбранноеПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ГруппаВыбранныхПолейКомпоновкиДанных, АвтоВыбранноеПолеКомпоновкиДанных
// ВыбранноеПолеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппаВыбранныхПолейКомпоновкиДанных,     - заполняемый объект
//                       АвтоВыбранноеПолеКомпоновкиДанных,
//                       ВыбранноеПолеКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВыбранноеПолеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		Возврат ГруппаВыбранныхПолейСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат АвтоВыбранноеПолеСКДВСтруктуру(Объект);
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"             , "ВыбранноеПолеКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Заголовок"       , Объект.Заголовок);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	СтруктураОбъекта.Вставить("Поле"            , СокрЛП(Объект.Поле));
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	// TODO: проверить родителя

	Возврат СтруктураОбъекта;
	
КонецФункции // ВыбранноеПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД ВыбранноеПолеКомпоновкиДанных,
// ГруппаВыбранныхПолейКомпоновкиДанных, АвтоВыбранноеПолеКомпоновкиДанных
//из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВыбранноеПолеКомпоновкиДанных,        - заполняемый объект
//                       ГруппаВыбранныхПолейКомпоновкиДанных
//                       АвтоВыбранноеПолеКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ВыбранноеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		ГруппаВыбранныхПолейСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	ИначеЕсли ТипЗнч(Объект) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
		АвтоВыбранноеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	КонецЕсли;
	
	Если НЕ ТипЗнч(Объект) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.Поле);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения =
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;
	// TODO: проверить родителя

КонецПроцедуры // ВыбранноеПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// РольПоляНабораДанныхКомпоновкиДанных
// 
// Параметры:
// 	Объект             - РольПоляНабораДанныхКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция РольПоляНабораДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("РольПоляНабораДанныхКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ВыражениеВидаСчета"      , Объект.ВыражениеВидаСчета);
	СтруктураОбъекта.Вставить("ГруппаОстатка"           , Объект.ГруппаОстатка);
	СтруктураОбъекта.Вставить("ИгнорироватьЗначенияNULL", Объект.ИгнорироватьЗначенияNULL);
	СтруктураОбъекта.Вставить("Измерение"               , Объект.Измерение);
	СтруктураОбъекта.Вставить("НомерПериода"            , Объект.НомерПериода);
	СтруктураОбъекта.Вставить("Обязательное"            , Объект.Обязательное);
	СтруктураОбъекта.Вставить("Остаток"                 , Объект.Остаток);
	СтруктураОбъекта.Вставить("ПолеСчета"               , Объект.ПолеСчета);
	СтруктураОбъекта.Вставить("РеквизитИзмерения"       , Объект.РеквизитИзмерения);
	СтруктураОбъекта.Вставить("РодительскоеИзмерение"   , Объект.РодительскоеИзмерение);
	СтруктураОбъекта.Вставить("Счет"                    , Объект.Счет);
	СтруктураОбъекта.Вставить("ТипБухгалтерскогоОстатка",
	                          ТипБухгалтерскогоОстаткаКомпоновкиДанныхВСтроку(Объект.ТипБухгалтерскогоОстатка));
	СтруктураОбъекта.Вставить("ТипОстатка"    , ТипОстаткаКомпоновкиДанныхВСтроку(Объект.ТипОстатка));
	СтруктураОбъекта.Вставить("ТипПериода"    , ТипПериодаКомпоновкиДанныхВСтроку(Объект.ТипПериода));
	
	Возврат СтруктураОбъекта;

КонецФункции // РольПоляНабораДанныхСКДВСтруктуру()

// Процедура - заполняет объект СКД РольПоляНабораДанныхКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - РольПоляНабораДанныхКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура РольПоляНабораДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("РольПоляНабораДанныхКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ВыражениеВидаСчета") Тогда
		Объект.ВыражениеВидаСчета = СтруктураОбъекта.ВыражениеВидаСчета;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ГруппаОстатка") Тогда
		Объект.ГруппаОстатка = СтруктураОбъекта.ГруппаОстатка;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИгнорироватьЗначенияNULL") Тогда
		Объект.ИгнорироватьЗначенияNULL = СтруктураОбъекта.ИгнорироватьЗначенияNULL;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Измерение") Тогда
		Объект.Измерение = СтруктураОбъекта.Измерение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НомерПериода") Тогда
		Объект.НомерПериода = СтруктураОбъекта.НомерПериода;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Обязательное") Тогда
		//@skip-error - на самом деле свойство записывается
		Объект.Обязательное = СтруктураОбъекта.Обязательное;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Остаток") Тогда
		Объект.Остаток = СтруктураОбъекта.Остаток;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПолеСчета") Тогда
		Объект.ПолеСчета = СтруктураОбъекта.ПолеСчета;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РеквизитИзмерения") Тогда
		Объект.РеквизитИзмерения = СтруктураОбъекта.РеквизитИзмерения;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РодительскоеИзмерение") Тогда
		Объект.РодительскоеИзмерение = СтруктураОбъекта.РодительскоеИзмерение;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Счет") Тогда
		Объект.Счет = СтруктураОбъекта.Счет;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипБухгалтерскогоОстатка") Тогда
		Объект.ТипБухгалтерскогоОстатка =
			ТипБухгалтерскогоОстаткаКомпоновкиДанных[СтруктураОбъекта.ТипБухгалтерскогоОстатка];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипОстатка") Тогда
		Объект.ТипОстатка = ТипОстаткаКомпоновкиДанных[СтруктураОбъекта.ТипОстатка];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипПериода") Тогда
		Объект.ТипПериода = ТипПериодаКомпоновкиДанных[СтруктураОбъекта.ТипПериода];
	КонецЕсли;

КонецПроцедуры // РольПоляНабораДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ГруппаЭлементовОтбораКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппаЭлементовОтбораКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ГруппаЭлементовОтбораСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип", "ГруппаЭлементовОтбораКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	СтруктураОбъекта.Вставить("Представление"   , Объект.Представление);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("Применение",
	                          ТипПримененияОтбораКомпоновкиДанныхВСтроку(Объект.Применение));
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	// TODO: проверить родителя
	СтруктураОбъекта.Вставить("ТипГруппы",
	                          ТипГруппыЭлементовОтбораКомпоновкиДанныхВСтроку(Объект.ТипГруппы));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЭлементОтбораСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ГруппаЭлементовОтбораСКДВСтруктуру()

// Процедура - заполняет объект СКД ГруппаЭлементовОтбораКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ГруппаЭлементовОтбораКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ГруппаЭлементовОтбораСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Представление") Тогда
		Объект.Представление = СтруктураОбъекта.Представление;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Применение") Тогда
		Объект.Применение = ТипПримененияОтбораКомпоновкиДанных[СтруктураОбъекта.Применение];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипГруппы") Тогда
		Объект.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных[СтруктураОбъекта.ТипГруппы];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ЭлементОтбораСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ГруппаЭлементовОтбораСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// ГруппаЭлементовОтбораКомпоновкиДанных, ЭлементОтбораКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппаЭлементовОтбораКомпоновкиДанных,     - заполняемый объект
//                       ЭлементОтбораКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЭлементОтбораСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		Возврат ГруппаЭлементовОтбораСКДВСтруктуру(Объект);
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип", "ЭлементОтбораКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	СтруктураОбъекта.Вставить("ЛевоеЗначение"   , СокрЛП(Объект.ЛевоеЗначение));
	СтруктураОбъекта.Вставить("ПравоеЗначение"  , ЗначениеВСтруктуру(Объект.Представление));
	СтруктураОбъекта.Вставить("Представление"   , Объект.Представление);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("Применение",
	                          ТипПримененияОтбораКомпоновкиДанныхВСтроку(Объект.Применение));
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	// TODO: проверить родителя

	Возврат СтруктураОбъекта;
	
КонецФункции // ЭлементОтбораСКДВСтруктуру()

// Процедура - заполняет объект СКД ГруппаЭлементовОтбораКомпоновкиДанных,
// ЭлементОтбораКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ГруппаЭлементовОтбораКомпоновкиДанных,   - заполняемый объект
//                       ЭлементОтбораКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                - данные для заполнения объекта
//
Процедура ЭлементОтбораСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		ГруппаЭлементовОтбораСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ЛевоеЗначение") Тогда
		Объект.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.ЛевоеЗначение);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПравоеЗначение") Тогда
		Объект.ПравоеЗначение = ЗначениеИзСтруктуры(СтруктураОбъекта.Представление);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Представление") Тогда
		Объект.Представление = СтруктураОбъекта.Представление;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Применение") Тогда
		Объект.Применение = ТипПримененияОтбораКомпоновкиДанных[СтруктураОбъекта.Применение];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

КонецПроцедуры // ЭлементОтбораСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОтборКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОтборКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОтборСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОтборКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЭлементОтбораСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ОтборСКДВСтруктуру()

// Процедура - заполняет объект СКД ОтборКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОтборКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура               - данные для заполнения объекта
//
Процедура ОтборСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОтборКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ЭлементОтбораСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОтборСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЗначенияПараметровВыводаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЗначенияПараметровВыводаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЗначенияПараметровВыводаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ЗначенияПараметровВыводаСКДВСтруктуру()

// Процедура - заполняет объект СКД ЗначенияПараметровВыводаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЗначенияПараметровВыводаКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                - данные для заполнения объекта
//
Процедура ЗначенияПараметровВыводаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);
	
КонецПроцедуры // ЗначенияПараметровВыводаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЗначенияПараметровДанныхКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЗначенияПараметровДанныхКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЗначенияПараметровДанныхСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ЗначенияПараметровДанныхСКДВСтруктуру()

// Процедура - заполняет объект СКД ЗначенияПараметровДанныхКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЗначенияПараметровДанныхКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                - данные для заполнения объекта
//
Процедура ЗначенияПараметровДанныхСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);
	
КонецПроцедуры // ЗначенияПараметровДанныхСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПользовательскоеПолеВыражениеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыражениеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПользовательскоеПолеВыражениеСКДВСтруктуру(Объект)

	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"          , "ПользовательскоеПолеВыражениеКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Заголовок"    , Объект.Заголовок);
	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("ПутьКДанным"  , Объект.ПутьКДанным);
	
	СтруктураОбъекта.Вставить("ВыражениеДетальныхЗаписей", Объект.ПолучитьВыражениеДетальныхЗаписей());
	СтруктураОбъекта.Вставить("ВыражениеИтоговыхЗаписей" , Объект.ПолучитьВыражениеИтоговыхЗаписей());
	СтруктураОбъекта.Вставить("ПредставлениеВыраженияДетальныхЗаписей",
	                          Объект.ПолучитьПредставлениеВыраженияДетальныхЗаписей());
	СтруктураОбъекта.Вставить("ПредставлениеВыраженияИтоговыхЗаписей" ,
	                          Объект.ПолучитьПредставлениеВыраженияИтоговыхЗаписей());
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПользовательскоеПолеВыражениеСКДВСтруктуру()

// Процедура - заполняет объект СКД ПользовательскоеПолеВыражениеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыражениеКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                     - данные для заполнения объекта
//
Процедура ПользовательскоеПолеВыражениеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	ВыражениеДетальныхЗаписей = "";
	Если СтруктураОбъекта.Свойство("ВыражениеДетальныхЗаписей") Тогда
		ВыражениеДетальныхЗаписей = СтруктураОбъекта.ВыражениеДетальныхЗаписей;
	КонецЕсли;

	ВыражениеИтоговыхЗаписей = "";
	Если СтруктураОбъекта.Свойство("ВыражениеИтоговыхЗаписей") Тогда
		ВыражениеИтоговыхЗаписей = СтруктураОбъекта.ВыражениеИтоговыхЗаписей;
	КонецЕсли;
	
	Объект.УстановитьВыражения(ВыражениеДетальныхЗаписей, ВыражениеИтоговыхЗаписей);
	
	ПредставлениеВыраженияДетальныхЗаписей = "";
	Если СтруктураОбъекта.Свойство("ПредставлениеВыраженияДетальныхЗаписей") Тогда
		ПредставлениеВыраженияДетальныхЗаписей = СтруктураОбъекта.ПредставлениеВыраженияДетальныхЗаписей;
	КонецЕсли;

	ПредставлениеВыраженияИтоговыхЗаписей = "";
	Если СтруктураОбъекта.Свойство("ПредставлениеВыраженияИтоговыхЗаписей") Тогда
		ПредставлениеВыраженияИтоговыхЗаписей = СтруктураОбъекта.ПредставлениеВыраженияИтоговыхЗаписей;
	КонецЕсли;
	
	Объект.УстановитьПредставлениеВыражений(ПредставлениеВыраженияДетальныхЗаписей,
	                                        ПредставлениеВыраженияИтоговыхЗаписей);
	
КонецПроцедуры // ПользовательскоеПолеВыражениеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПользовательскоеПолеВыборКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыборКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПользовательскоеПолеВыборСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"          , "ПользовательскоеПолеВыборКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Заголовок"    , Объект.Заголовок);
	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("ПутьКДанным"  , Объект.ПутьКДанным);

	СтруктураОбъекта.Вставить("Варианты"     , ВариантыПользовательскогоПоляВыбораСКДВСтруктуру(Объект.Варианты));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПользовательскоеПолеВыборСКДВСтруктуру()

// Процедура - заполняет объект СКД ПользовательскоеПолеВыборКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыборКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                 - данные для заполнения объекта
//
Процедура ПользовательскоеПолеВыборСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Заголовок") Тогда
		Объект.Заголовок = СтруктураОбъекта.Заголовок;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Варианты") Тогда
		ВариантыПользовательскогоПоляВыбораСКДИзСтруктуры(Объект.Варианты, СтруктураОбъекта.Варианты);
	КонецЕсли;

КонецПроцедуры // ПользовательскоеПолеВыборСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПользовательскоеПолеВыражениеКомпоновкиДанных, ПользовательскоеПолеВыборКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыражениеКомпоновкиДанных,     - заполняемый объект
//                       ПользовательскоеПолеВыборКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПользовательскоеПолеСКДВСтруктуру(Объект)
	
	Если ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		Возврат ПользовательскоеПолеВыражениеСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
		Возврат ПользовательскоеПолеВыборСКДВСтруктуру(Объект);
	КонецЕсли;
	
КонецФункции // ПользовательскоеПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД ПользовательскоеПолеВыражениеКомпоновкиДанных,
// ПользовательскоеПолеВыборКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПользовательскоеПолеВыражениеКомпоновкиДанных,   - заполняемый объект
//                       ПользовательскоеПолеВыборКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ПользовательскоеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
		ПользовательскоеПолеВыражениеСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
		ПользовательскоеПолеВыборСКДИзСтруктуры(Объект, СтруктураОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ПользовательскоеПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПользовательскиеПоляКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПользовательскиеПоляКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПользовательскиеПоляСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскиеПоляКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ПользовательскоеПолеСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ПользовательскиеПоляСКДВСтруктуру()

// Процедура - заполняет объект СКД ПользовательскиеПоляКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПользовательскиеПоляКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                            - данные для заполнения объекта
//
Процедура ПользовательскиеПоляСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПользовательскиеПоляКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ПользовательскоеПолеСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПользовательскиеПоляСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВариантПользовательскогоПоляВыборКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВариантПользовательскогоПоляВыборКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВариантПользовательскогоПоляВыбораСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантПользовательскогоПоляВыборКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Значение"     , ЗначениеВСтруктуру(Объект.Значение));
	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("Представление", Объект.Представление);
	СтруктураОбъекта.Вставить("Отбор"        , ОтборСКДВСтруктуру(Объект));

	Возврат СтруктураОбъекта;
	
КонецФункции // ВариантПользовательскогоПоляВыбораСКДВСтруктуру()

// Процедура - заполняет объект СКД ВариантПользовательскогоПоляВыборКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВариантПользовательскогоПоляВыборКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                         - данные для заполнения объекта
//
Процедура ВариантПользовательскогоПоляВыбораСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантПользовательскогоПоляВыборКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Значение") Тогда
		Объект.Значение = ЗначениеИзСтруктуры(СтруктураОбъекта.Значение);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Представление") Тогда
		Объект.Представление = СтруктураОбъекта.Представление;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Отбор") Тогда
		ОтборСКДИзСтруктуры(Объект.Отбор, СтруктураОбъекта.Отбор);
	КонецЕсли;

КонецПроцедуры // ВариантПользовательскогоПоляВыбораСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ВариантыПользовательскогоПоляВыборКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ВариантыПользовательскогоПоляВыборКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ВариантыПользовательскогоПоляВыбораСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ВариантПользовательскогоПоляВыбораСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ВариантыПользовательскогоПоляВыбораСКДВСтруктуру()

// Процедура - заполняет объект СКД ВариантыПользовательскогоПоляВыборКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ВариантыПользовательскогоПоляВыборКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                          - данные для заполнения объекта
//
Процедура ВариантыПользовательскогоПоляВыбораСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить();
			ВариантПользовательскогоПоляВыбораСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ВариантыПользовательскогоПоляВыбораСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// АвтоЭлементПорядкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - АвтоЭлементПорядкаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция АвтоЭлементПорядкаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"   , "АвтоЭлементПорядкаКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);

	Возврат СтруктураОбъекта;
	
КонецФункции // АвтоЭлементПорядкаСКДВСтруктуру()

// Процедура - заполняет объект СКД АвтоЭлементПорядкаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - АвтоЭлементПорядкаКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                          - данные для заполнения объекта
//
Процедура АвтоЭлементПорядкаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

КонецПроцедуры // АвтоЭлементПорядкаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// АвтоЭлементПорядкаКомпоновкиДанных, ЭлементПорядкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - АвтоЭлементПорядкаКомпоновкиДанных,     - заполняемый объект
//                       ЭлементПорядкаКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЭлементПорядкаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат АвтоЭлементПорядкаСКДВСтруктуру(Объект);
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип"   , "ЭлементПорядкаКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	СтруктураОбъекта.Вставить("Поле"            , СокрЛП(Объект.Поле));
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	СтруктураОбъекта.Вставить("ТипУпорядочивания",
	                          НаправлениеСортировкиКомпоновкиДанныхВСтроку(Объект.ТипУпорядочивания));

	Возврат СтруктураОбъекта;
	
КонецФункции // ЭлементПорядкаСКДВСтруктуру()

// Процедура - заполняет объекты СКД АвтоЭлементПорядкаКомпоновкиДанных,
// ЭлементПорядкаКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - АвтоЭлементПорядкаКомпоновкиДанных,   - заполняемый объект
//                       ЭлементПорядкаКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ЭлементПорядкаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
		АвтоЭлементПорядкаСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.Поле);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипУпорядочивания") Тогда
		Объект.ТипУпорядочивания =  НаправлениеСортировкиКомпоновкиДанных[СтруктураОбъекта.ТипУпорядочивания];
	КонецЕсли;

КонецПроцедуры // ЭлементПорядкаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПорядокКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПорядокКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПорядокСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПорядокКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЭлементПорядкаСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);

	Возврат СтруктураОбъекта;
	
КонецФункции // ПорядокСКДВСтруктуру()

// Процедура - заполняет объект СКД ПорядокКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПорядокКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                 - данные для заполнения объекта
//
Процедура ПорядокСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПорядокКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ЭлементПорядкаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПорядокСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// НастройкиВложенногоОбъектаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - НастройкиВложенногоОбъектаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция НастройкиВложенногоОбъектаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"                 , "НастройкиВложенногоОбъектаКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Идентификатор"       , Объект.Идентификатор);
	СтруктураОбъекта.Вставить("ИдентификаторОбъекта", Объект.ИдентификаторОбъекта);
	СтруктураОбъекта.Вставить("Имя"                 , Объект.Имя);
	СтруктураОбъекта.Вставить("Использование"       , Объект.Использование);
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	// TODO: проверить родителя
	СтруктураОбъекта.Вставить("Настройки", НастройкиСКДВСтруктуру(Объект.Настройки));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // НастройкиВложенногоОбъектаСКДВСтруктуру()

// Процедура - заполняет объект СКД НастройкиВложенногоОбъектаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НастройкиВложенногоОбъектаКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                                  - данные для заполнения объекта
//
Процедура НастройкиВложенногоОбъектаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторОбъекта") Тогда
		Объект.УстановитьИдентификатор(СтруктураОбъекта.ИдентификаторОбъекта);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Настройки") Тогда
		НастройкиСКДИзСтруктуры(Объект.Настройки, СтруктураОбъекта.Настройки);
	КонецЕсли;

КонецПроцедуры // НастройкиВложенногоОбъектаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ГруппировкаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппировкаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ГруппировкаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Тип"             , "ГруппировкаКомпоновкиДанных");

	СтруктураОбъекта.Вставить("Выбор"           , ВыбранныеПоляСКДВСтруктуру(Объект.Выбор));
	СтруктураОбъекта.Вставить("Идентификатор"   , Объект.Идентификатор);
	СтруктураОбъекта.Вставить("Имя"             , Объект.Имя);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	СтруктураОбъекта.Вставить("Отбор",
	                          ОтборСКДВСтруктуру(Объект.Отбор));
	СтруктураОбъекта.Вставить("ПараметрыВывода",
	                          ЗначенияПараметровВыводаГруппировкиСКДВСтруктуру(Объект.ПараметрыВывода));
	СтруктураОбъекта.Вставить("ПоляГруппировки",
	                          ПоляГруппировкиСКДВСтруктуру(Объект.ПоляГруппировки));
	СтруктураОбъекта.Вставить("Порядок",
	                          ПорядокСКДВСтруктуру(Объект.Порядок));

	// TODO: проверить родителя
	СтруктураОбъекта.Вставить("Состояние",
	                          СостояниеЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.Состояние));
	                          
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Структура Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Структура", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("УсловноеОформление",
	                          УсловноеОформлениеСКДВСтруктуру(Объект.УсловноеОформление));

 	Возврат СтруктураОбъекта;
	
КонецФункции // ГруппировкаСКДВСтруктуру()

// Процедура - заполняет объект СКД ГруппировкаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ГруппировкаКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                     - данные для заполнения объекта
//
Процедура ГруппировкаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выбор") Тогда
		ВыбранныеПоляСКДИзСтруктуры(Объект.Выбор, СтруктураОбъекта.Выбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Отбор") Тогда
		ОтборСКДИзСтруктуры(Объект.Отбор, СтруктураОбъекта.Отбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыВывода") Тогда
		ЗначенияПараметровВыводаГруппировкиСКДИзСтруктуры(Объект.ПараметрыВывода, СтруктураОбъекта.ПараметрыВывода);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПоляГруппировки") Тогда
		ПоляГруппировкиСКДИзСтруктуры(Объект.ПоляГруппировки, СтруктураОбъекта.ПоляГруппировки);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Порядок") Тогда
		ПорядокСКДИзСтруктуры(Объект.Порядок, СтруктураОбъекта.Порядок);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Состояние") Тогда
		Объект.Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.Состояние];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Структура") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Структура Цикл
			НовыйЭлемент = Объект.Структура.Добавить(Тип(ТекЭлемент.Тип));
			ЭлементСтруктурыНастроекСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("УсловноеОформление") Тогда
		УсловноеОформлениеСКДИзСтруктуры(Объект.УсловноеОформление, СтруктураОбъекта.УсловноеОформление);
	КонецЕсли;

КонецПроцедуры // ГруппировкаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ТаблицаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ТаблицаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ТаблицаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"             , "ТаблицаКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Выбор"           , ВыбранныеПоляСКДВСтруктуру(Объект.Выбор));
	СтруктураОбъекта.Вставить("Идентификатор"   , Объект.Идентификатор);
	СтруктураОбъекта.Вставить("Имя"             , Объект.Имя);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	СтруктураОбъекта.Вставить("ПараметрыВывода",
	                          ЗначенияПараметровВыводаГруппировкиСКДВСтруктуру(Объект.ПараметрыВывода));

	// TODO: проверить родителя
	                          
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Колонки Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Колонки", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Строки Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Строки", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("УсловноеОформление",
	                          УсловноеОформлениеСКДВСтруктуру(Объект.УсловноеОформление));

	Возврат СтруктураОбъекта;
	
КонецФункции // ТаблицаСКДВСтруктуру()

// Процедура - заполняет объект СКД ТаблицаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ТаблицаКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                 - данные для заполнения объекта
//
Процедура ТаблицаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выбор") Тогда
		ВыбранныеПоляСКДИзСтруктуры(Объект.Выбор, СтруктураОбъекта.Выбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыВывода") Тогда
		ЗначенияПараметровВыводаГруппировкиСКДИзСтруктуры(Объект.ПараметрыВывода, СтруктураОбъекта.ПараметрыВывода);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Колонки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Колонки Цикл
			НовыйЭлемент = Объект.Колонки.Добавить();
			ЭлементПорядкаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Строки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Строки Цикл
			НовыйЭлемент = Объект.Строки.Добавить();
			ЭлементПорядкаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("УсловноеОформление") Тогда
		УсловноеОформлениеСКДИзСтруктуры(Объект.УсловноеОформление, СтруктураОбъекта.УсловноеОформление);
	КонецЕсли;

КонецПроцедуры // ТаблицаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ДиаграммаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ДиаграммаКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ДиаграммаСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"             , "ДиаграммаКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Выбор"           , ВыбранныеПоляСКДВСтруктуру(Объект.Выбор));
	СтруктураОбъекта.Вставить("Идентификатор"   , Объект.Идентификатор);
	СтруктураОбъекта.Вставить("Имя"             , Объект.Имя);
	СтруктураОбъекта.Вставить("Использование"   , Объект.Использование);
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));
	СтруктураОбъекта.Вставить("ПараметрыВывода",
	                          ЗначенияПараметровВыводаГруппировкиСКДВСтруктуру(Объект.ПараметрыВывода));

	// TODO: проверить родителя
	                          
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Серии Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Серии", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Точки Цикл
		МассивЭлементов.Добавить(ЭлементСтруктурыНастроекСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Точки", МассивЭлементов);
	
	СтруктураОбъекта.Вставить("УсловноеОформление",
	                          УсловноеОформлениеСКДВСтруктуру(Объект.УсловноеОформление));

	Возврат СтруктураОбъекта;
	
КонецФункции // ДиаграммаСКДВСтруктуру()

// Процедура - заполняет объект СКД ДиаграммаКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ДиаграммаКомпоновкиДанных   - заполняемый объект
// 	СтруктураОбъекта   - Структура                   - данные для заполнения объекта
//
Процедура ДиаграммаСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Выбор") Тогда
		ВыбранныеПоляСКДИзСтруктуры(Объект.Выбор, СтруктураОбъекта.Выбор);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Имя") Тогда
		Объект.Имя = СтруктураОбъекта.Имя;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПараметрыВывода") Тогда
		ЗначенияПараметровВыводаГруппировкиСКДИзСтруктуры(Объект.ПараметрыВывода, СтруктураОбъекта.ПараметрыВывода);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Серии") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Серии Цикл
			НовыйЭлемент = Объект.Серии.Добавить();
			ЭлементПорядкаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Точки") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Точки Цикл
			НовыйЭлемент = Объект.Точки.Добавить();
			ЭлементПорядкаСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("УсловноеОформление") Тогда
		УсловноеОформлениеСКДИзСтруктуры(Объект.УсловноеОформление, СтруктураОбъекта.УсловноеОформление);
	КонецЕсли;

КонецПроцедуры // ДиаграммаСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// ГруппировкаКомпоновкиДанных, ТаблицаКомпоновкиДанных,
// ДиаграммаКомпоновкиДанных, НастройкиВложенногоОбъектаКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ГруппировкаКомпоновкиДанных,               - заполняемый объект
//                       ТаблицаКомпоновкиДанных,
//                       ДиаграммаКомпоновкиДанных,
//                       НастройкиВложенногоОбъектаКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЭлементСтруктурыНастроекСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат ГруппировкаСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат ТаблицаСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат ДиаграммаСКДВСтруктуру(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат НастройкиВложенногоОбъектаСКДВСтруктуру(Объект);
	Иначе
		Возврат СтруктураОбъекта;
	КонецЕсли;

КонецФункции // ЭлементСтруктурыНастроекСКДВСтруктуру()

// Процедура - заполняет объекты СКД ГруппировкаКомпоновкиДанных,
// ТаблицаКомпоновкиДанных, ДиаграммаКомпоновкиДанных,
// НастройкиВложенногоОбъектаКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ГруппировкаКомпоновкиДанных,                - заполняемый объект
//                       ТаблицаКомпоновкиДанных,
//                       ДиаграммаКомпоновкиДанных,
//                       НастройкиВложенногоОбъектаКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура ЭлементСтруктурыНастроекСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("ГруппировкаКомпоновкиДанных") Тогда
		ГруппировкаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ТаблицаКомпоновкиДанных") Тогда
		ТаблицаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("ДиаграммаКомпоновкиДанных") Тогда
		ДиаграммаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	ИначеЕсли ТипЗнч(Объект) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		НастройкиВложенногоОбъектаСКДИзСтруктуры(Объект, СтруктураОбъекта);
	КонецЕсли;
	
КонецПроцедуры // ЭлементСтруктурыНастроекСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// АвтоПолеГруппировкиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - АвтоПолеГруппировкиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция АвтоПолеГруппировкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"          , "АвтоПолеГруппировкиКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // АвтоПолеГруппировкиСКДВСтруктуру()

// Процедура - заполняет объект СКД АвтоПолеГруппировкиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - АвтоПолеГруппировкиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                           - данные для заполнения объекта
//
Процедура АвтоПолеГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

КонецПроцедуры // АвтоПолеГруппировкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объектов СКД
// АвтоПолеГруппировкиКомпоновкиДанных, ПолеГруппировкиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - АвтоПолеГруппировкиКомпоновкиДанных,     - заполняемый объект
//                       ПолеГруппировкиКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПолеГруппировкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если ТипЗнч(Объект) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат АвтоПолеГруппировкиСКДВСтруктуру(Объект);
	КонецЕсли;

	Если НЕ ТипЗнч(Объект) = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Тип"           , "ПолеГруппировкиКомпоновкиДанных");
	
	СтруктураОбъекта.Вставить("Использование" , Объект.Использование);
	СтруктураОбъекта.Вставить("НачалоПериода" , ?(ТипЗнч(Объект.НачалоПериода) = Тип("ПолеКомпоновкиДанных"),
	                                              СокрЛП(Объект.НачалоПериода),
	                                              Объект.НачалоПериода));
	СтруктураОбъекта.Вставить("КонецПериода"  , ?(ТипЗнч(Объект.КонецПериода) = Тип("ПолеКомпоновкиДанных"),
	                                              СокрЛП(Объект.КонецПериода),
	                                              Объект.КонецПериода));
	СтруктураОбъекта.Вставить("Поле"          , СокрЛП(Объект.Поле));
	СтруктураОбъекта.Вставить("ТипГруппировки", ТипГруппировкиКомпоновкиДанныхВСтроку(Объект.ТипГруппировки));
	СтруктураОбъекта.Вставить("ТипДополнения" , ТипДополненияПериодаКомпоновкиДанныхВСтроку(Объект.ТипДополнения));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПолеГруппировкиСКДВСтруктуру()

// Процедура - заполняет объекты СКД АвтоПолеГруппировкиКомпоновкиДанных,
// ПолеГруппировкиКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - АвтоПолеГруппировкиКомпоновкиДанных,   - заполняемый объект
//                       ПолеГруппировкиКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                             - данные для заполнения объекта
//
Процедура ПолеГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если ТипЗнч(Объект) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
		АвтоПолеГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта);
		Возврат;
	КонецЕсли;
	
	Если НЕ ТипЗнч(Объект) = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("НачалоПериода") Тогда
		Если ТипЗнч(СтруктураОбъекта.НачалоПериода) = Тип("Строка") Тогда
			Объект.НачалоПериода = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.НачалоПериода);
		ИначеЕсли ТипЗнч(СтруктураОбъекта.НачалоПериода) = Тип("Дата") Тогда
			Объект.НачалоПериода = СтруктураОбъекта.НачалоПериода;
		КонецЕсли;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("КонецПериода") Тогда
		Если ТипЗнч(СтруктураОбъекта.КонецПериода) = Тип("Строка") Тогда
			Объект.КонецПериода = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.КонецПериода);
		ИначеЕсли ТипЗнч(СтруктураОбъекта.КонецПериода) = Тип("Дата") Тогда
			Объект.КонецПериода = СтруктураОбъекта.КонецПериода;
		КонецЕсли;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.Поле);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипГруппировки") Тогда
		Объект.ТипГруппировки = ТипГруппировкиКомпоновкиДанных[СтруктураОбъекта.ТипГруппировки];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ТипДополнения") Тогда
		Объект.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных[СтруктураОбъекта.ТипДополнения];
	КонецЕсли;

КонецПроцедуры // ПолеГруппировкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ПоляГруппировкиКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ПоляГруппировкиКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ПоляГруппировкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ПоляГруппировкиКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ПолеГруппировкиСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ПоляГруппировкиСКДВСтруктуру()

// Процедура - заполняет объект СКД ПоляГруппировкиКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ПоляГруппировкиКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ПоляГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ПоляГруппировкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить(Тип(ТекЭлемент.Тип));
			ПолеГруппировкиСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПоляГруппировкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных, ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных,
// ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных,          - заполняемый объект
//                       ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных
//                       ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЗначенияПараметровВыводаГруппировкиСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ (ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных")) Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЗначениеПараметраСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ЗначенияПараметровВыводаГруппировкиСКДВСтруктуру()

// Процедура - заполняет объекты СКД ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных,
// ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных,
// ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных           - заполняемый объект
//                       ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных
//                       ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных
// 	СтруктураОбъекта   - Структура                                                     - данные для заполнения объекта
//
Процедура ЗначенияПараметровВыводаГруппировкиСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ (ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(Объект) = Тип("ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных")) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметровОбъектаСКДИзСтруктуры(Объект, Объект, СтруктураОбъекта);
	
КонецПроцедуры // ЗначенияПараметровВыводаГруппировкиСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ЭлементУсловногоОформленияКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ЭлементУсловногоОформленияКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ЭлементУсловногоОформленияСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	
	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));

	СтруктураОбъекта.Вставить("ИспользоватьВГруппировке",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВГруппировке));
	СтруктураОбъекта.Вставить("ИспользоватьВЗаголовке",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВЗаголовке));
	СтруктураОбъекта.Вставить("ИспользоватьВЗаголовкеПолей",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВЗаголовкеПолей));
	СтруктураОбъекта.Вставить("ИспользоватьВИерархическойГруппировке",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВИерархическойГруппировке));
	СтруктураОбъекта.Вставить("ИспользоватьВОбщемИтоге",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВОбщемИтоге));
	СтруктураОбъекта.Вставить("ИспользоватьВОтборе",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВОтборе));
	СтруктураОбъекта.Вставить("ИспользоватьВПараметрах",
	                          ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Объект.ИспользоватьВПараметрах));

	СтруктураОбъекта.Вставить("Отбор"     , ОтборСКДВСтруктуру(Объект.Отбор));
	СтруктураОбъекта.Вставить("Оформление", ОформлениеСКДВСтруктуру(Объект.Оформление));
	СтруктураОбъекта.Вставить("Поля"      , ОформляемыеПоляСКДВСтруктуру(Объект.Поля));
 
	СтруктураОбъекта.Вставить("Представление", Объект.Представление);

	Возврат СтруктураОбъекта;
	
КонецФункции // ЭлементУсловногоОформленияСКДВСтруктуру()

// Процедура - заполняет объект СКД ЭлементУсловногоОформленияКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ЭлементУсловногоОформленияКомпоновкиДанных  - заполняемый объект
// 	СтруктураОбъекта   - Структура                                   - данные для заполнения объекта
//
Процедура ЭлементУсловногоОформленияСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВГруппировке") Тогда
		Объект.ИспользоватьВГруппировке =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВГруппировке];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВЗаголовке") Тогда
		Объект.ИспользоватьВЗаголовке =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВЗаголовке];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВЗаголовкеПолей") Тогда
		Объект.ИспользоватьВЗаголовкеПолей =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВЗаголовкеПолей];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВИерархическойГруппировке") Тогда
		Объект.ИспользоватьВИерархическойГруппировке =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВИерархическойГруппировке];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВОбщемИтоге") Тогда
		Объект.ИспользоватьВОбщемИтоге =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВОбщемИтоге];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВОтборе") Тогда
		Объект.ИспользоватьВОтборе =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВОтборе];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ИспользоватьВПараметрах") Тогда
		Объект.ИспользоватьВПараметрах =
			ИспользованиеУсловногоОформленияКомпоновкиДанных[СтруктураОбъекта.ИспользоватьВПараметрах];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Отбор") Тогда
		ОтборСКДИзСтруктуры(Объект.Отбор, СтруктураОбъекта.Отбор);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Оформление") Тогда
		ОформлениеСКДИзСтруктуры(Объект.Оформление, СтруктураОбъекта.Оформление);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Поля") Тогда
		ОформляемыеПоляСКДИзСтруктуры(Объект.Поля, СтруктураОбъекта.Поля);
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Представление") Тогда
		Объект.Представление = СтруктураОбъекта.Представление;
	КонецЕсли;

КонецПроцедуры // ЭлементУсловногоОформленияСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// УсловноеОформлениеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - УсловноеОформлениеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция УсловноеОформлениеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("УсловноеОформлениеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("ИдентификаторПользовательскойНастройки",
	                          Объект.ИдентификаторПользовательскойНастройки);
	СтруктураОбъекта.Вставить("ПредставлениеПользовательскойНастройки",
	                          Объект.ПредставлениеПользовательскойНастройки);
	СтруктураОбъекта.Вставить("РежимОтображения",
	                          РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Объект.РежимОтображения));

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ЭлементУсловногоОформленияСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // УсловноеОформлениеСКДВСтруктуру()

// Процедура - заполняет объект СКД УсловноеОформлениеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - УсловноеОформлениеКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                          - данные для заполнения объекта
//
Процедура УсловноеОформлениеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("УсловноеОформлениеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ИдентификаторПользовательскойНастройки") Тогда
		Объект.ИдентификаторПользовательскойНастройки = СтруктураОбъекта.ИдентификаторПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("ПредставлениеПользовательскойНастройки") Тогда
		Объект.ПредставлениеПользовательскойНастройки = СтруктураОбъекта.ПредставлениеПользовательскойНастройки;
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("РежимОтображения") Тогда
		Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных[СтруктураОбъекта.РежимОтображения];
	КонецЕсли;

	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить();
			ЭлементУсловногоОформленияСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УсловноеОформлениеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОформляемоеПолеКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОформляемоеПолеКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОформляемоеПолеСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформляемоеПолеКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	СтруктураОбъекта.Вставить("Использование", Объект.Использование);
	СтруктураОбъекта.Вставить("Поле"         , СокрЛП(Объект.Поле));
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ОформляемоеПолеСКДВСтруктуру()

// Процедура - заполняет объект СКД ОформляемоеПолеКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - ОформляемоеПолеКомпоновкиДанных - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ОформляемоеПолеСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформляемоеПолеКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Использование") Тогда
		Объект.Использование = СтруктураОбъекта.Использование;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Поле") Тогда
		Объект.Поле = Новый ПолеКомпоновкиДанных(СтруктураОбъекта.Поле);
	КонецЕсли;
	
КонецПроцедуры // ОформляемоеПолеСКДИзСтруктуры()

// Функция - заполняет и возвращает структуру описания объекта СКД
// ОформляемыеПоляКомпоновкиДанных
// 
// Параметры:
// 	Объект             - ОформляемыеПоляКомпоновкиДанных     - заполняемый объект
//
// Возвращаемое значение:
// 	Структура          - структура описания объекта
//
Функция ОформляемыеПоляСКДВСтруктуру(Объект)
	
	СтруктураОбъекта = Новый Структура();
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформляемыеПоляКомпоновкиДанных") Тогда
		Возврат СтруктураОбъекта;
	КонецЕсли;

	МассивЭлементов = Новый Массив();
	Для Каждого ТекЭлемент Из Объект.Элементы Цикл
		МассивЭлементов.Добавить(ОформляемоеПолеСКДВСтруктуру(ТекЭлемент));
	КонецЦикла;
	СтруктураОбъекта.Вставить("Элементы", МассивЭлементов);
	
	Возврат СтруктураОбъекта;
	
КонецФункции // ОформляемыеПоляСКДВСтруктуру()

// Процедура - заполняет объект СКД ОформляемыеПоляКомпоновкиДанных
// из структуры описания объекта
// 
// Параметры:
// 	Объект             - НастройкиКомпоновкиДанных     - заполняемый объект
// 	СтруктураОбъекта   - Структура                       - данные для заполнения объекта
//
Процедура ОформляемыеПоляСКДИзСтруктуры(Объект, СтруктураОбъекта)
	
	Если НЕ ТипЗнч(Объект) = Тип("ОформляемыеПоляКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Элементы") Тогда
		Для Каждого ТекЭлемент Из СтруктураОбъекта.Элементы Цикл
			НовыйЭлемент = Объект.Элементы.Добавить();
			ОформляемоеПолеСКДИзСтруктуры(НовыйЭлемент, ТекЭлемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОформляемыеПоляСКДИзСтруктуры()

#КонецОбласти // ПреобразованияСКДИнтерфейсныеПроцедуры

#Область ПреобразованияСКДФункцииПолученияЗначенийПеречислений

// Функция - возвращает строковое представление значения перечисления СКД
// ТипМакетаОбластиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипМакетаОбластиКомпоновкиДанных - значение для получения стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипМакетаОбластиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипМакетаОбластиКомпоновкиДанных.Заголовок Тогда
		Возврат "Заголовок";
	ИначеЕсли Значение = ТипМакетаОбластиКомпоновкиДанных.ЗаголовокИерархии Тогда
		Возврат "ЗаголовокИерархии";
	ИначеЕсли Значение = ТипМакетаОбластиКомпоновкиДанных.ОбщийИтогЗаголовок Тогда
		Возврат "ОбщийИтогЗаголовок";
	ИначеЕсли Значение = ТипМакетаОбластиКомпоновкиДанных.ОбщийИтогПодвал Тогда
		Возврат "ОбщийИтогПодвал";
	ИначеЕсли Значение = ТипМакетаОбластиКомпоновкиДанных.Подвал Тогда
		Возврат "Подвал";
	ИначеЕсли Значение = ТипМакетаОбластиКомпоновкиДанных.ПодвалИерархии Тогда
		Возврат "ПодвалИерархии";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипМакетаОбластиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ИспользованиеПараметраКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ИспользованиеПараметраКомпоновкиДанных - значение для получения стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ИспользованиеПараметраКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ИспользованиеПараметраКомпоновкиДанных.Авто Тогда
		Возврат "Авто";
	ИначеЕсли Значение = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
		Возврат "Всегда";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ИспользованиеПараметраКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// НаправлениеСортировкиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - НаправлениеСортировкиКомпоновкиДанных - значение для получения стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция НаправлениеСортировкиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
		Возврат "Возр";
	ИначеЕсли Значение = НаправлениеСортировкиКомпоновкиДанных.Убыв Тогда
		Возврат "Убыв";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // НаправлениеСортировкиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// РежимОтображенияЭлементаНастройкиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - значение для получения
//                                                                         стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто Тогда
		Возврат "Авто";
	ИначеЕсли Значение = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда
		Возврат "БыстрыйДоступ";
	ИначеЕсли Значение = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Возврат "Недоступный";
	ИначеЕсли Значение = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный Тогда
		Возврат "Обычный";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // РежимОтображенияЭлементаНастройкиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипПримененияОтбораКомпоновкиДанныхВСтроку
// 
// Параметры:
// 	Значение         - ТипПримененияОтбораКомпоновкиДанныхВСтроку - значение для получения
//                                                                  стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипПримененияОтбораКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипПримененияОтбораКомпоновкиДанных.Иерархия Тогда
		Возврат "Иерархия";
	ИначеЕсли Значение = ТипПримененияОтбораКомпоновкиДанных.ТолькоИерархия Тогда
		Возврат "ТолькоИерархия";
	ИначеЕсли Значение = ТипПримененияОтбораКомпоновкиДанных.Элементы Тогда
		Возврат "Элементы";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипПримененияОтбораКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипГруппыЭлементовОтбораКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипГруппыЭлементовОтбораКомпоновкиДанных - значение для получения
//                                                                стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипГруппыЭлементовОтбораКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ Тогда
		Возврат "ГруппаИ";
	ИначеЕсли Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли Тогда
		Возврат "ГруппаИли";
	ИначеЕсли Значение = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе Тогда
		Возврат "ГруппаНе";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипГруппыЭлементовОтбораКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ДействиеОбработкиРасшифровкиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ДействиеОбработкиРасшифровкиКомпоновкиДанных - значение для получения
//                                                                    стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ДействиеОбработкиРасшифровкиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет Тогда
		Возврат "Нет";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		Возврат "ОткрытьЗначение";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Отфильтровать Тогда
		Возврат "Отфильтровать";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Оформить Тогда
		Возврат "Оформить";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать Тогда
		Возврат "Расшифровать";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Сгруппировать Тогда
		Возврат "Сгруппировать";
	ИначеЕсли Значение = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Упорядочить Тогда
		Возврат "Упорядочить";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ДействиеОбработкиРасшифровкиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// РасположениеПоляКомпоновкиДанных
// 
// Параметры:
// 	Значение         - РасположениеПоляКомпоновкиДанных - значение для получения
//                                                        стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция РасположениеПоляКомпоновкиДанныхВСтроку(Значение)

	Если Значение = РасположениеПоляКомпоновкиДанных.Авто Тогда
		Возврат "Авто";
	ИначеЕсли Значение = РасположениеПоляКомпоновкиДанных.Вертикально Тогда
		Возврат "Вертикально";
	ИначеЕсли Значение = РасположениеПоляКомпоновкиДанных.Вместе Тогда
		Возврат "Вместе";
	ИначеЕсли Значение = РасположениеПоляКомпоновкиДанных.Горизонтально Тогда
		Возврат "Горизонтально";
	ИначеЕсли Значение = РасположениеПоляКомпоновкиДанных.ОтдельнаяКолонка Тогда
		Возврат "ОтдельнаяКолонка";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // РасположениеПоляКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипБухгалтерскогоОстаткаКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипБухгалтерскогоОстаткаКомпоновкиДанных - значение для получения
//                                                                стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипБухгалтерскогоОстаткаКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипБухгалтерскогоОстаткаКомпоновкиДанных.Дебет Тогда
		Возврат "Дебет";
	ИначеЕсли Значение = ТипБухгалтерскогоОстаткаКомпоновкиДанных.Кредит Тогда
		Возврат "Кредит";
	ИначеЕсли Значение = ТипБухгалтерскогоОстаткаКомпоновкиДанных.Нет Тогда
		Возврат "Нет";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипБухгалтерскогоОстаткаКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипОстаткаКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипОстаткаКомпоновкиДанных - значение для получения
//                                                  стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипОстаткаКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипОстаткаКомпоновкиДанных.КонечныйОстаток Тогда
		Возврат "КонечныйОстаток";
	ИначеЕсли Значение = ТипОстаткаКомпоновкиДанных.НачальныйОстаток Тогда
		Возврат "НачальныйОстаток";
	ИначеЕсли Значение = ТипОстаткаКомпоновкиДанных.Нет Тогда
		Возврат "Нет";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипОстаткаКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипПериодаКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипПериодаКомпоновкиДанных - значение для получения
//                                                  стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипПериодаКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипПериодаКомпоновкиДанных.Дополнительный Тогда
		Возврат "Дополнительный";
	ИначеЕсли Значение = ТипПериодаКомпоновкиДанных.Основной Тогда
		Возврат "Основной";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипПериодаКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// СостояниеЭлементаНастройкиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - СостояниеЭлементаНастройкиКомпоновкиДанных - значение для получения
//                                                                  стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция СостояниеЭлементаНастройкиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = СостояниеЭлементаНастройкиКомпоновкиДанных.Включен Тогда
		Возврат "Включен";
	ИначеЕсли Значение = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен Тогда
		Возврат "Отключен";
	ИначеЕсли Значение = СостояниеЭлементаНастройкиКомпоновкиДанных.УдаленПользователем Тогда
		Возврат "УдаленПользователем";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // СостояниеЭлементаНастройкиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипГруппировкиКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипГруппировкиКомпоновкиДанных - значение для получения
//                                                      стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипГруппировкиКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ТипГруппировкиКомпоновкиДанных.Иерархия Тогда
		Возврат "Иерархия";
	ИначеЕсли Значение = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия Тогда
		Возврат "ТолькоИерархия";
	ИначеЕсли Значение = ТипГруппировкиКомпоновкиДанных.Элементы Тогда
		Возврат "Элементы";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // СостояниеЭлементаНастройкиКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ТипДополненияПериодаКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ТипДополненияПериодаКомпоновкиДанных - значение для получения
//                                                            стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ТипДополненияПериодаКомпоновкиДанныхВСтроку(Значение)

	РасширенныйРежим = Ложь;
	СИ = Новый СистемнаяИнформация();
	Если Лев(СИ.ВерсияПриложения, 6) = "8.3.12" Тогда
		Если Метаданные.РежимСовместимости = Метаданные.СвойстваОбъектов.РежимСовместимости["НеИспользовать"] Тогда
			РасширенныйРежим = Истина;
		КонецЕсли;
	ИначеЕсли Лев(СИ.ВерсияПриложения, 6) > "8.3.12" Тогда
		Если Метаданные.РежимСовместимости >= Метаданные.СвойстваОбъектов.РежимСовместимости["Версия8_3_12"] Тогда
			РасширенныйРежим = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Значение = ТипДополненияПериодаКомпоновкиДанных.БезДополнения Тогда
		Возврат "БезДополнения";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Год Тогда
		Возврат "Год";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Декада Тогда
		Возврат "Декада";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.День Тогда
		Возврат "День";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Квартал Тогда
		Возврат "Квартал";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Месяц Тогда
		Возврат "Месяц";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Минута Тогда
		Возврат "Минута";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Неделя Тогда
		Возврат "Неделя";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Полугодие Тогда
		Возврат "Полугодие";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Секунда Тогда
		Возврат "Секунда";
	ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.Час Тогда
		Возврат "Час";
	ИначеЕсли РасширенныйРежим Тогда
		Если Значение = ТипДополненияПериодаКомпоновкиДанных.ГодОтНачалаПериода Тогда
			Возврат "ГодОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.ГодОтНачалаПериода445 Тогда
			Возврат "ГодОтНачалаПериода445";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.ДеньОтНачалаПериода Тогда
			Возврат "ДеньОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.КварталОтНачалаПериода Тогда
			Возврат "КварталОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.КварталОтНачалаПериода445 Тогда
			Возврат "КварталОтНачалаПериода445";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.МесяцОтНачалаПериода Тогда
			Возврат "МесяцОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.МесяцОтНачалаПериода445 Тогда
			Возврат "МесяцОтНачалаПериода445";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.МинутаОтНачалаПериода Тогда
			Возврат "МинутаОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.НеделяОтНачалаПериода Тогда
			Возврат "НеделяОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.ПолугодиеОтНачалаПериода Тогда
			Возврат "ПолугодиеОтНачалаПериода";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.ПолугодиеОтНачалаПериода445 Тогда
			Возврат "ПолугодиеОтНачалаПериода445";
		ИначеЕсли Значение = ТипДополненияПериодаКомпоновкиДанных.ЧасОтНачалаПериода Тогда
			Возврат "ЧасОтНачалаПериода";
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ТипДополненияПериодаКомпоновкиДанныхВСтроку()

// Функция - возвращает строковое представление значения перечисления СКД
// ИспользованиеУсловногоОформленияКомпоновкиДанных
// 
// Параметры:
// 	Значение         - ИспользованиеУсловногоОформленияКомпоновкиДанных - значение для получения
//                                                                        стрококвого представления
// Возвращаемое значение:
// 	Строка, Неопределено - строковое представление значения
//
Функция ИспользованиеУсловногоОформленияКомпоновкиДанныхВСтроку(Значение)

	Если Значение = ИспользованиеУсловногоОформленияКомпоновкиДанных.Использовать Тогда
		Возврат "Использовать";
	ИначеЕсли Значение = ИспользованиеУсловногоОформленияКомпоновкиДанных.НеИспользовать Тогда
		Возврат "НеИспользовать";
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // СостояниеЭлементаНастройкиКомпоновкиДанныхВСтроку()

#КонецОбласти // СКДСлужебныеФункцииПолученияЗначенийПеречислений
